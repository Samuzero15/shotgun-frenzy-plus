///////////////////////////////////////////////////
//      SHOTGUN FRENZY PLUS
//   a Shotgun Frenzy fork by Samuzero15tlh

// fp_dmhud.acs
// Damage hud scripts.

// Meant to remeber you: you're still human and you could die in any minute.
// Now, this should display correctly on different aspect ratios!

#library "FP_DMHUD"

#import "f_intdb.acs"

#include "zcommon.acs"
#include "acsutils.acs" 

#define DMHUD_ID -2504
#define DMHUD_PERC 0.50
#define DMHUD_ALPHA_MIN 0.1
#define DMHUD_ALPHA_MAX 0.4

script "SFPlus_DamagedHud_Enter" ENTER{
	ACS_NamedExecuteAlways("SFPlus_DamagedHud", 0);
}

script "SFPlus_DamagedHud_Respawn" RESPAWN{
	ACS_NamedExecuteAlways("SFPlus_DamagedHud", 0);
}

script "SFPlus_DamagedHud" (void) {
	
	while(GetActorProperty(0, APROP_HEALTH) > 0){
		int hp = GetActorProperty(0, APROP_HEALTH);
		int maxhp = // If we're using the mech, the max health changes.
		cond(p_TankSlot[playernumber()] == 2, 
			1500 + 900 * checkinventory("MechUp_Armor"), 
			GetActorProperty(0, APROP_SPAWNHEALTH));
		int lowHp = ut_floor(maxHp * DMHUD_PERC);
		if(lowHp == 0) lowHP = 1;
		
		
		if(hp <= lowHp >> 16){
			ACS_NamedExecuteAlways( "SFPlus_DamagedHud_Show", 0, hp, lowhp);
		}
		delay(35);
	}
}

script "SFPlus_DamagedHud_Show" (int hp, int lowHp) CLIENTSIDE{
	int percLowHp = fixedDiv(hp << 16, lowHP);
	int alpha = DMHUD_ALPHA_MIN + (DMHUD_ALPHA_MAX - fixedMul(DMHUD_ALPHA_MAX, percLowHp));
	int w, h;
	
	// Kennon told me to make this graphic wide-screen friendly.
	w = fixedDiv(640.0, fixedDiv(GetAspectRatio(), ASPECT_4_3));
	h = 480.0;
	
	SetHudSize(w>>16, h>>16, 0);
	SetFont("HUD_DMG1");
	HudMessage(s:"A"; HUDMSG_FADEINOUT | HUDMSG_ALPHA, DMHUD_ID, CR_UNTRANSLATED, 
	// Put it on the center point, being a centered msg.
	ut_floor(fixedDiv(w, 2.0)),
	ut_floor(fixedDiv(h, 2.0)), 
	0.2, 0.2, 0.2, alpha);
	
	SetHudSize(0,0, 0);
}