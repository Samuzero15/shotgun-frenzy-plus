///////////////////////////////////////////////////
//      SHOTGUN FRENZY PLUS
//   a Shotgun Frenzy fork by Samuzero15tlh

// fl_ItemDir.acs
// item directory

// Here it lays all of the weapons on the game.
// For the prices, take a look at cvarinfo.prices lump.

#library "fl_WDir"

#import "fp_lumpread.acs"

#include "sfcommon.acs"
#include "libbcs.bcs"

enum WEAPON_SETS {
	WEAPON_SET_HIDDEN = -2,
	WEAPON_SET_BASIC = -1,
	WEAPON_SET_STANDARD = 0,
	WEAPON_SET_ADVANCED = 1,
	WEAPON_SET_OVERPOWERED = 2,
	WEAPON_SET_ELITE = 3,
	WEAPON_SET_CATEGORIES = 3
};

enum FP_SIZES {
	FP_WEP_PACKS_PER_CATEGORY = 50,
	FP_WEP_PACKS = FP_WEP_PACKS_PER_CATEGORY * WEAPON_SET_CATEGORIES,
	FP_WEAPONS = 150 * WEAPON_SET_CATEGORIES,
	FP_WEAPONS_PER_PACK = 3,
	FP_WEPUPS = 8
};

// Ammo definition array and constants
#libdefine FP_AMMODEFS 200
#libdefine FP_AMMODEF_PROPS 10

enum FP_LANGUAGEPROP : str {
	FP_LANGUAGEPROP_NAME = "NAME",
	FP_LANGUAGEPROP_DESC = "DESC",
	FP_LANGUAGEPROP_ICON = "ICON"
};

enum FP_STARTER_CONSTS {
	FP_STARTER_CLASSES = 50,
	FP_STARTER_ITEMS_PER_CLASS = 10,
	FP_STARTERITEM_PROPS = 10
};

struct WeaponPackT {
	str name;           
	int category;       
	str description;    
	int price;          
	int mon_pressure;   
	int wpsnum;         
	str langstring;     
	str forclass;       
};

struct WeaponT {
	str actor;
	str name;
	str description;
	int price;
	int upsnum;
	str langstring;
	str icon;
	str forclass;
	int pack_id;
};

struct WeaponUpgradeT {
	str actor;
	str name;
	str description;
	int ups_price;
	int rcy_price;
	str up_type;
};

struct AmmoT {
	str name;
	int base_max_amount;
	int pb_extra_ammo;
	int dispenser_give;
	str supply_spawn;
	int max_cap;
	bool not_expandable;
};

struct StarterItemT {
	str classPlayer;
	str item;
	int amount;
	bool equipweapon;
};

struct WeaponCategoryT {
	str name;
	int color;
};

WeaponCategoryT term_catStyle[WEAPON_SET_CATEGORIES];
WeaponPackT fp_WpPacks[FP_WEP_PACKS];
WeaponT fp_Wpns[FP_WEAPONS];
WeaponUpgradeT fp_WpUps[FP_WEAPONS][FP_WEPUPS];
AmmoT fp_AmmoList[FP_AMMODEFS];
StarterItemT fp_StarterItems[FP_STARTER_CLASSES][FP_STARTER_ITEMS_PER_CLASS];

int fp_WpCategoryStack[WEAPON_SET_CATEGORIES];
int fp_StarterItemStack[FP_STARTER_CLASSES]; // Tracks item count for each class
str fp_StarterClassNames[FP_STARTER_CLASSES];
int fp_StarterClassCount = 0;
int fp_WpCategory[WEAPON_SET_CATEGORIES][50];

// Weapon pack tracker. For difficulty increasing.
int fp_WpTrack[FP_WEP_PACKS];
int fp_UpTrack;


// Saved weapons per each player.
int fp_WpnsPerPlayer[32][FP_WEAPONS];

int fp_WeaponPackStack;
int fp_WeaponStack = 0;
int fp_AmmoDefStack = 0;

// fp_WeaponPacks
//	Pack name
//	Category (BAS, ADV, POW)
//  Description
// 	Buy Price

// fp_Wpns[wepaonid]:
//  Score Gain (To ramp-up difficulty.)
// 	Weapon Name
// 	Actor
// 	Pack id
//  Description
// 	Recycle Price (On Credits)

// fp_WeaponUpgrades[wepaonid]:
// 	Weapon Upgrade Name
// 	Actor
//  Description
// 	Upgrade Price (On Ups.)

function void SFPlus_DefineWeapons (void){
	int wep = 0;
	int pack = 0;
	int ups = 0;
	str string;
	
	fp_WeaponPackStack = 0;
	fp_WeaponStack = 0;
	fp_WpCategoryStack[0] = 0;
	fp_WpCategoryStack[1] = 0;
	fp_WpCategoryStack[2] = 0;

	term_catStyle[WEAPON_SET_STANDARD].color = CR_GREEN;
	term_catStyle[WEAPON_SET_STANDARD].name = "Standard";
	term_catStyle[WEAPON_SET_ADVANCED].color = CR_CYAN;
	term_catStyle[WEAPON_SET_ADVANCED].name = "Advanced";
	term_catStyle[WEAPON_SET_OVERPOWERED].color = CR_ORANGE;
	term_catStyle[WEAPON_SET_OVERPOWERED].name = "Overpowered";

	WDir_ReadSFWEPDEF();
}

function void WDir_ReadSFWEPDEF(void){
	ACS_NamedExecuteWithResult("LumpRead_LoadFile", "SFWEPDEF", "SFPlus_WDir_SFWEPDEFReadLine");
}

int file_wpid;
int file_wepid;

function int StarterClass_FindOrAdd(str classPlayer) {
	for (int i = 0; i < fp_StarterClassCount; i++) {
		if (StrEquals(fp_StarterClassNames[i], classPlayer)) {
			return i;
		}
	}
	// Not found, add new class if space
	if (fp_StarterClassCount < FP_STARTER_CLASSES) {
		fp_StarterClassNames[fp_StarterClassCount] = classPlayer;
		fp_StarterClassCount++;
		return fp_StarterClassCount - 1;
	}
	return -1; // No space left
}

function void StarterItem_Add(int classIdx, str classPlayer, str item, int amount, bool equipweapon){
	int itemIdx = fp_StarterItemStack[classIdx];
	if(itemIdx < FP_STARTER_ITEMS_PER_CLASS) {
		fp_StarterItems[classIdx][itemIdx].classPlayer = classPlayer;
		fp_StarterItems[classIdx][itemIdx].item = item;
		fp_StarterItems[classIdx][itemIdx].amount = amount;
		fp_StarterItems[classIdx][itemIdx].equipweapon = equipweapon;
		fp_StarterItemStack[classIdx]++;
	} else {
		log(s:"StarterItem: Maximum starter items reached for class ", d:classIdx, s:".");
	}
}

enum {
	WDEF_TK_AddWeaponPack = "AddWeaponPack",
	WDEF_TK_AddWeapon = "AddWeapon",
	WDEF_TK_AddWeaponNoPack = "AddWeaponNoPack",
	WDEF_TK_AddWeaponUp = "AddWeaponUp",
	WDEF_TK_ClearAllWeapons = "ClearAllWeapons",
	WDEF_TK_ReplaceWeapon = "ReplaceWeapon",
	WDEF_TK_ReplaceWeaponPack = "ReplaceWeaponPack",
	WDEF_TK_ReplaceWeaponUp = "ReplaceWeaponUp",
	WDEF_TK_AddAmmo = "AddAmmo",
	WDEF_TK_ClearAllAmmo = "ClearAllAmmo",
	WDEF_TK_StarterItem = "StarterItem",
	WDEF_TK_StarterWeapon = "StarterWeapon",
	WDEF_TK_ClearClasses = "ClearClasses"
};

#if 1
#define SFWEPDEF_FUNC( tok, call, min, max) \
	if (StrIEquals(op, tok)) { \
		if(!LumpRead_CountArgs (line, line_num, min, max, tok)) { \
			terminate; \
		} \
		call; \
		terminate; \
	}
#endif

Script "SFPlus_WDir_SFWEPDEFReadLine" (int line, int line_num) {
	str op = LumpRead_NextArg(line, LUMPR_VALUE_STR);
	//log(s:"OP: ", s:op);
	SFWEPDEF_FUNC( WDEF_TK_AddWeaponPack, 		SFWEPDEF_AddWeaponPack(line), 			5, 6);
	SFWEPDEF_FUNC( WDEF_TK_AddWeapon, 			SFWEPDEF_AddWeapon(line, false), 		4, 6);
	SFWEPDEF_FUNC( WDEF_TK_AddWeaponNoPack, 	SFWEPDEF_AddWeapon(line, true), 		4, 6);
	SFWEPDEF_FUNC( WDEF_TK_AddWeaponUp, 		SFWEPDEF_AddWeaponUpgrade(line), 		6, 6);
	SFWEPDEF_FUNC( WDEF_TK_ClearAllWeapons, 	SFWEPDEF_ClearAllWeapons(), 			1, 1);
	SFWEPDEF_FUNC( WDEF_TK_ReplaceWeapon, 		SFWEPDEF_ReplaceWeapon(line), 			6, 6);
	SFWEPDEF_FUNC( WDEF_TK_ReplaceWeaponPack, 	SFWEPDEF_ReplaceWeaponPack(line), 		6, 7);
	SFWEPDEF_FUNC( WDEF_TK_ReplaceWeaponUp, 	SFWEPDEF_ReplaceWeaponUpgrade(line), 	6, 6);
	SFWEPDEF_FUNC( WDEF_TK_AddAmmo, 			SFWEPDEF_AddAmmo(line), 				4, 8);
	SFWEPDEF_FUNC( WDEF_TK_ClearAllAmmo, 		SFWEPDEF_ClearAllAmmo(), 				1, 1);
	SFWEPDEF_FUNC( WDEF_TK_StarterItem, 		SFWEPDEF_StarterItem(line),				3, 4);
	SFWEPDEF_FUNC( WDEF_TK_StarterWeapon, 		SFWEPDEF_StarterWeapon(line), 			3, 3);
	SFWEPDEF_FUNC( WDEF_TK_ClearClasses, 		SFWEPDEF_ClearClasses(), 				1, 1);

	log(s:"fl_WDir: Invalid command: '", s:op, s:"'", s:", line=", s:line);
}

function void SFWEPDEF_ClearClasses (void){
	// Clear all starter classes and their items
	StarterItemT sc;
	for(int c = 0; c < fp_StarterClassCount; c++) {
		fp_StarterClassNames[c] = "";
		for(int i = 0; i < FP_STARTER_ITEMS_PER_CLASS; i++) {
			memcpy(fp_StarterItems[c][i], sc);
		}
		fp_StarterItemStack[c] = 0;
	}
	fp_StarterClassCount = 0;
}

function void SFWEPDEF_ClearAllWeapons(void) {
	int i, j, k;
	WeaponPackT wp;

	for(i = 0; i < FP_WEP_PACKS; i++){
		/*
		for(j = 0; j < 10; j++){
			fp_WpPacks[i][j] = 0;
		}*/
		memcpy(fp_WpPacks[i], wp);
	}
	for(i = 0; i < FP_WEAPONS; i++){
		Weapon_Clear(i);
	}

	for(i = 0; i < WEAPON_SET_CATEGORIES; i++){
		for(j = 0; j < 50; j++){
			fp_WpCategory[i][j] = 0;
		}
		fp_WpCategoryStack[i] = 0;
	}

	fp_WeaponPackStack = 0;
	fp_WeaponStack = 0;
}

function void Weapon_Clear(int weapon_id){
	int j;
	WeaponT w;
	memcpy(fp_Wpns[weapon_id], w);
	WeaponUpgrades_Clear(weapon_id);
}

function void WeaponUpgrades_Clear(int weapon_id){
	int j;
	//fp_Wpns[weapon_id][FP_WDIR_WEAPON_UPSNUM] = 0;
	WeaponUpgradeT wup;
	for(j = 0; j < FP_WEPUPS; j++){
		memcpy(fp_WpUps[weapon_id][j], wup);
	}
}

/*  Adds an ammo definition  */
function void SFWEPDEF_AddAmmo(int line) {
	str ammo_name = LumpRead_NextArg(line, LUMPR_VALUE_STR);
	int base_max_amount = LumpRead_NextArg(line, LUMPR_VALUE_INT);
	int pb_extra_ammo = LumpRead_NextArg(line, LUMPR_VALUE_INT);
	int dispenser_give = LumpRead_NextArgDefault(line, LUMPR_VALUE_INT, 1);
	int supply_spawn = LumpRead_NextArgDefault(line, LUMPR_VALUE_STR, 0);
	int max_cap = LumpRead_NextArgDefault(line, LUMPR_VALUE_INT, 0);
	bool dont_expand = LumpRead_NextArgDefault(line, LUMPR_VALUE_BOOL, false);

	int idx = fp_AmmoDefStack;
	if(idx < FP_AMMODEFS) {
		fp_AmmoList[idx].name = ammo_name;
		fp_AmmoList[idx].base_max_amount = base_max_amount;
		fp_AmmoList[idx].pb_extra_ammo = pb_extra_ammo;
		fp_AmmoList[idx].dispenser_give = dispenser_give;
		fp_AmmoList[idx].supply_spawn = supply_spawn;
		fp_AmmoList[idx].max_cap = max_cap;
		fp_AmmoList[idx].not_expandable = dont_expand;
		fp_AmmoDefStack++;
	} else {
		log(s:"AddAmmo: Maximum ammo definitions reached (", d:FP_AMMODEFS, s:").");
	}
}

script "SFPlus_BCS_ArrDemo" (void)
{
	// create array with 12 elements
	int[]? testArr = bcs::arrNew(1);

	log(d:(lengthof(testArr)), s:" elements created.");

	foreach ( let int index, number; testArr ) {
      log( s: "testArr[", d: index, s: "] == ", d: number );
    }

	// resize array, growing by 1
	testArr = bcs::arrResize(testArr, lengthof(testArr) + 1);
	testArr[lengthof(testArr) - 1] = 42;
	/*
	log(s:"Array Resized");
	foreach ( let int index, number; testArr ) {
	  log( s: "testArr[", d: index, s: "] == ", d: number );
	}
	*/
}


/*  Clears all defined ammo  */
function void SFWEPDEF_ClearAllAmmo(void) {
	AmmoT reset;
	for(int i = 0; i < FP_AMMODEFS; i++) {
		memcpy(fp_AmmoList[i], reset);
	}
	fp_AmmoDefStack = 0;
}

function void AmmoTakeLeftOvers (str ammo){
	if(getAmmoCapacity(ammo) < CheckInventory(ammo)){
		TakeInventory(ammo, CheckInventory(ammo) - getAmmoCapacity(ammo));
	}
}

script "Ammo_UpdateCaps" (void) {
	int p_back = CheckInventory("p_Backpack");
	int g_back = (CheckInventory("g_Backpack") || CheckInventory("Cheat_Ammo"));
	int ammo_rune = CheckInventory("RuneAmmunition");

	for (int i = 0; i < fp_AmmoDefStack; i++) {
		str ammo_name = fp_AmmoList[i].name;
		int base_max_amount = fp_AmmoList[i].base_max_amount;
		int max_cap = fp_AmmoList[i].max_cap;
		bool dont_expand = fp_AmmoList[i].not_expandable;
		int pb_extra_ammo = fp_AmmoList[i].pb_extra_ammo;
		int cap = base_max_amount;
		if (!dont_expand) {
			cap = base_max_amount + (base_max_amount * g_back + pb_extra_ammo * p_back);
			if (ammo_rune) {
				cap = cap * 3 / 2; // Multiply by 1.5 if ammo_rune is active
			}
		} else continue; // TODO: Remove this Anti-crash line, after a Zandro update
		// If max_cap is not zero, limit cap to max_cap
		if (max_cap != 0 && cap > max_cap) {
			cap = max_cap;
		}
		SetAmmoCapacity(ammo_name, cap);
		AmmoTakeLeftOvers(ammo_name);
	}
}

script "Ammo_ResetCaps" (void) {
	TakeInventory("p_Backpack", 1); // Reset token.
	TakeInventory("g_Backpack", 1); // Reset token.

	for (int i = 0; i < fp_AmmoDefStack; i++) {
		
		// TODO: Remove this condition, after updating Zandronum.
		if(fp_AmmoList[i].not_expandable) 
			continue;
			
		str ammo_name = fp_AmmoList[i].name;
		int base_max_amount = fp_AmmoList[i].base_max_amount;
		SetAmmoCapacity(ammo_name, base_max_amount);
	}
}

script "SFPlus_WDir_GiveStarterItems" (void)
{
	// Clear inventory first
	ClearInventory();

	// Get player's class name
	str currentPlayerClass = GetActorClass(CONST_TID_PLAYER + PlayerNumber());

	// Find matching class index
	int classIdx = -1;
	for (int i = 0; i < fp_StarterClassCount; i++)
	{
		if (StrEquals(fp_StarterClassNames[i], currentPlayerClass))
		{
			classIdx = i;
			break;
		}
	}

	// If found, give starter items
	if (classIdx != -1)
	{
		for (int j = 0; j < fp_StarterItemStack[classIdx]; j++)
		{
			str item = fp_StarterItems[classIdx][j].item;
			int amount = fp_StarterItems[classIdx][j].amount;
			bool equipWeapon = fp_StarterItems[classIdx][j].equipweapon;
			if (item != "")
			{
				GiveInventory(item, amount);
				if (equipWeapon)
				{
					delay(1);
					SetWeapon(item); // If it is a weapon, set it as the current weapon
				}
			}
		}
	}
}

/*	Adds a weapon pack	*/
function void SFWEPDEF_AddWeaponPack (int line) {
	int cat = LumpRead_NextArg (line, LUMPR_VALUE_INT);
	if(fp_WpCategoryStack[cat] >= FP_WEP_PACKS_PER_CATEGORY){
		log(s:"(FL_WDIR.acs) Can't add more weapon packs in this category, ", d:FP_WEP_PACKS_PER_CATEGORY, s:" packs Limit reached.");
		return;
	}
	int wpid = fp_WeaponPackStack;

	fp_WpCategory[cat][fp_WpCategoryStack[cat]] = wpid;
	fp_WpCategoryStack[cat]++;
	WeaponPackT wp;
	wp.category = cat;
	wp.langstring = LumpRead_NextArg (line, LUMPR_VALUE_STR);

	str lang_weppack = wp.langstring;
	wp.name = SFWEPDEF_WepLanguageValue (lang_weppack, FP_LANGUAGEPROP_NAME);
	wp.description = SFWEPDEF_WepLanguageValue (lang_weppack, FP_LANGUAGEPROP_DESC);
	wp.price = LumpRead_NextArg (line, LUMPR_VALUE_CVAR);
	wp.mon_pressure = LumpRead_NextArg (line, LUMPR_VALUE_INT);
	wp.forclass = LumpRead_NextArgDefault (line, LUMPR_VALUE_STR, 0);
	memcpy(fp_WpPacks[wpid], wp);
	fp_WeaponPackStack++;
}

/*	Adds a weapon  */
function void SFWEPDEF_AddWeapon (int line, bool nopack) {
	int weppack_id = -1;
	if(!nopack){
		weppack_id = WeaponPack_Find(LumpRead_NextArg(line, LUMPR_VALUE_STR));
	}
	int wepid = fp_WeaponStack;
	WeaponT wep;
	wep.actor = LumpRead_NextArg (line, LUMPR_VALUE_STR);
	str lang_wep = LumpRead_NextArg (line, LUMPR_VALUE_STR);
	wep.langstring = lang_wep;
	wep.pack_id = weppack_id;

	if(weppack_id >= 0) {
		// If the weapon is part of a pack, we need to get the language string from the pack.
		//str lang_weppack = fp_WpPacks[weapon_pack_id][FP_WDIR_WPPACK_LANGSTRING];
		str lang_weppack = fp_WpPacks[weppack_id].langstring;
		lang_wep = strparam(s:lang_weppack, s:"_", s:lang_wep);
		wep.langstring = lang_wep;
	}

	wep.name = SFWEPDEF_WepLanguageValue (lang_wep, FP_LANGUAGEPROP_NAME);
	wep.description = SFWEPDEF_WepLanguageValue (lang_wep, FP_LANGUAGEPROP_DESC);
	wep.icon = SFWEPDEF_WepLanguageValue (lang_wep, FP_LANGUAGEPROP_ICON);
	wep.price = 0;
	if(weppack_id >= 0) 
		wep.price = LumpRead_NextArg (line, LUMPR_VALUE_CVAR);

	memcpy(fp_Wpns[wepid], wep);
	fp_WeaponStack++;
}

/*	Adds a weapon upgrade	*/
function void SFWEPDEF_AddWeaponUpgrade(int line){
	str base_weapon_actor = LumpRead_NextArg(line, LUMPR_VALUE_STR);
	int weapon_id = Weapon_Find(base_weapon_actor);
	int uptype = LumpRead_NextArg (line, LUMPR_VALUE_INT);
	if(weapon_id == -1){
		log(s:"(FL_WDIR.acs) Base weapon '", s:base_weapon_actor, s:"' not found, cannot add Type ", d:uptype, s:" upgrade.");
		return;
	}
	int wepup_count = fp_Wpns[weapon_id].upsnum;
	if(wepup_count >= FP_WEPUPS){
		log(s:"(FL_WDIR.acs) Can't add more weapon upgrades for this weapon, ", d:FP_WEPUPS, s:" weapons Limit reached.");
		return;
	}
	str lang_wep = fp_Wpns[weapon_id].langstring;
	

	fp_WpUps[weapon_id][wepup_count].up_type = uptype;
	fp_WpUps[weapon_id][wepup_count].actor = LumpRead_NextArg (line, LUMPR_VALUE_STR);
	fp_WpUps[weapon_id][wepup_count].name = SFWEPDEF_WepLangVal_Upgrade (lang_wep, uptype, FP_LANGUAGEPROP_NAME);
	fp_WpUps[weapon_id][wepup_count].description = SFWEPDEF_WepLangVal_Upgrade (lang_wep, uptype, FP_LANGUAGEPROP_DESC);
	fp_WpUps[weapon_id][wepup_count].ups_price = LumpRead_NextArg (line, LUMPR_VALUE_CVAR);
	fp_WpUps[weapon_id][wepup_count].rcy_price = LumpRead_NextArg (line, LUMPR_VALUE_CVAR);
	fp_Wpns[weapon_id].upsnum++;
}

/*	Replaces a weapon pack	*/
function void SFWEPDEF_ReplaceWeaponPack(int line) {
	int i = 0;
	str pack_to_replace = LumpRead_NextArg(line, LUMPR_VALUE_STR);
	str pack_replacer = LumpRead_NextArg(line, LUMPR_VALUE_STR);
	int new_category = LumpRead_NextArg(line, LUMPR_VALUE_INT);
	int pack_to_replace_id = WeaponPack_Find(pack_to_replace);

	if(pack_to_replace_id == -1){
		log(s:"(FL_WDIR.acs) Replaced weapon pack '", s:pack_to_replace, s:"' not found. Failed to replace with: '", s:pack_replacer, s:"'.");
		return;
	}

	file_wpid = pack_to_replace_id;

	for(i = 0; i < FP_WEAPONS && pack_to_replace_id != -1; i++){
		if(Weapon_GetWepPack(i) == pack_to_replace_id){
			Weapon_Clear(i);
		}
	}
	WeaponPackT wp;
	wp.category = new_category > WEAPON_SET_HIDDEN ? new_category : fp_WpPacks[file_wpid].category;
	wp.langstring = pack_replacer;
	wp.name = SFWEPDEF_WepLanguageValue(pack_replacer, FP_LANGUAGEPROP_NAME);
	wp.description = SFWEPDEF_WepLanguageValue(pack_replacer, FP_LANGUAGEPROP_DESC);
	wp.price = LumpRead_NextArg(line, LUMPR_VALUE_CVAR);
	wp.mon_pressure = LumpRead_NextArg(line, LUMPR_VALUE_INT);
	wp.forclass = LumpRead_NextArgDefault (line, LUMPR_VALUE_STR, 0);
	memcpy(fp_WpPacks[file_wpid], wp);
}

/*	Replaces a weapon	*/
function void SFWEPDEF_ReplaceWeapon(int line) {
	int i = 0;
	str weapon_to_replace = LumpRead_NextArg (line, LUMPR_VALUE_STR);
	str weapon_replacer = LumpRead_NextArg (line, LUMPR_VALUE_STR);
	int wepid = Weapon_Find(weapon_to_replace);

	if(wepid == -1){
		log(s:"(FL_WDIR.acs) Replaced weapon '", s:weapon_to_replace, s:"' not found. Failed to replace with: '", s:weapon_replacer, s:"'.");
		return;
	}
	int wep_pack_id = Weapon_GetWepPack(wepid);
	WeaponUpgrades_Clear(wepid);

	fp_Wpns[wepid].pack_id = wep_pack_id;
	fp_Wpns[wepid].actor = weapon_replacer;
	str lang_wep = LumpRead_NextArg (line, LUMPR_VALUE_STR);
	fp_Wpns[wepid].langstring = lang_wep;

	if(wep_pack_id >= 0) {
		// If the weapon is part of a pack, we need to get the language string from the pack.
		str lang_weppack = fp_WpPacks[wep_pack_id].langstring;
		lang_wep = strparam(s:lang_weppack, s:"_", s:lang_wep);
		fp_Wpns[wepid].langstring = lang_wep;
	} 

	fp_Wpns[wepid].name = SFWEPDEF_WepLanguageValue (lang_wep, FP_LANGUAGEPROP_NAME);
	fp_Wpns[wepid].description = SFWEPDEF_WepLanguageValue (lang_wep, FP_LANGUAGEPROP_DESC);
	fp_Wpns[wepid].icon = SFWEPDEF_WepLanguageValue (lang_wep, FP_LANGUAGEPROP_ICON);
	
	if(wep_pack_id >= 0) 
		fp_Wpns[wepid].price = LumpRead_NextArg (line, LUMPR_VALUE_CVAR);
	
	fp_Wpns[wepid].forclass = LumpRead_NextArg (line, LUMPR_VALUE_STR);
}

function void SFWEPDEF_ReplaceWeaponUpgrade(int line) {
	int i, j = 0;
	str parent_weapon = LumpRead_NextArg (line, LUMPR_VALUE_STR);
	str wepup_to_replace = LumpRead_NextArg (line, LUMPR_VALUE_INT);
	str wepup_actor_replacer = LumpRead_NextArg (line, LUMPR_VALUE_STR);
	
	int weapon_parent_id = Weapon_Find(parent_weapon);
	int wepup_to_replace_id = WeaponUpgrade_Find(weapon_parent_id, wepup_to_replace);

	if(weapon_parent_id == -1){
		log(s:"(FL_WDIR.acs) Parent weapon '", s:parent_weapon, s:"' not found. Failed to replace weapon upgrade.");
		return;
	}

	if(wepup_to_replace_id == -1){
		log(s:"(FL_WDIR.acs) Replaced weapon upgrade Type '", d:wepup_to_replace, s:"' not found in weapon '", s:parent_weapon, s:"'. Failed to replace with: '", s:wepup_actor_replacer, s:"'.");
		return;
	}

	str lang_wep = fp_Wpns[weapon_parent_id].langstring;
	int uptype = wepup_to_replace;

	fp_WpUps[weapon_parent_id][wepup_to_replace_id].up_type = uptype;
	fp_WpUps[weapon_parent_id][wepup_to_replace_id].actor = wepup_actor_replacer;
	fp_WpUps[weapon_parent_id][wepup_to_replace_id].name = SFWEPDEF_WepLangVal_Upgrade (lang_wep, uptype, FP_LANGUAGEPROP_NAME);
	fp_WpUps[weapon_parent_id][wepup_to_replace_id].description = SFWEPDEF_WepLangVal_Upgrade (lang_wep, uptype, FP_LANGUAGEPROP_DESC);
	fp_WpUps[weapon_parent_id][wepup_to_replace_id].ups_price = LumpRead_NextArg (line, LUMPR_VALUE_CVAR);
	fp_WpUps[weapon_parent_id][wepup_to_replace_id].rcy_price = LumpRead_NextArg (line, LUMPR_VALUE_CVAR);
}

function void SFWEPDEF_StarterItem(int line){
	str classPlayer = LumpRead_NextArg(line, LUMPR_VALUE_STR);
	str inventoryItem = LumpRead_NextArg(line, LUMPR_VALUE_STR);
	int amount = LumpRead_NextArgDefault(line, LUMPR_VALUE_INT, 1);

	int classIdx = StarterClass_FindOrAdd(classPlayer);
	if(classIdx == -1) {
		log(s:"StarterItem: Maximum player classes reached.");
		return;
	}
	StarterItem_Add(classIdx, classPlayer, inventoryItem, amount, false);
}

function void SFWEPDEF_StarterWeapon(int line){
	str classPlayer = LumpRead_NextArg(line, LUMPR_VALUE_STR);
	str inventoryItem = LumpRead_NextArg(line, LUMPR_VALUE_STR);

	int classIdx = StarterClass_FindOrAdd(classPlayer);
	if(classIdx == -1) {
		log(s:"StarterWeapon: Maximum player classes reached.");
		return;
	}
	StarterItem_Add(classIdx, classPlayer, inventoryItem, 1, true);
}

function str SFWEPDEF_WepLangVal_Upgrade (str string, int up_type, str prop){
	return SFWEPDEF_WepLanguageValue (strparam(s:string, s:"_UP", d:up_type), prop);
}

function str SFWEPDEF_WepLanguageValue (str string, str prop){
	return strparam(l:strparam(s:"WEPS_", s:string, s:"_", s:prop));
}

function int GiveActorReplacedInventory(int tid, str item, int amt)
{
    int tempTid = UniqueTID();
    int ret = SpawnForced(item, GetActorX(tid), GetActorY(tid), GetActorZ(tid), tempTid, 0);
    if(ret)
    {
        GiveActorInventory(tid, GetActorClass(tempTid), amt);
        Thing_Remove(tempTid);
    }
    return ret;
}

function str Weapon_GetSameKind(int wid){
	if (CheckInventory(Weapon_GetActor(wid))) return Weapon_GetActor(wid);
	if (CheckInventory(WeaponUpgrade_getActor(wid, 0))) return WeaponUpgrade_getActor(wid, 0);
	if (CheckInventory(WeaponUpgrade_getActor(wid, 1))) return WeaponUpgrade_getActor(wid, 1);
	return 0;
}

function void Weapon_Swap(str this, str withThis){
	TakeInventory(this,1);
	GiveInventory(withThis,1);
	setWeapon(withThis);
}

function void Weapon_Upgrade(int wid, int type){
	//printbold(n:(playernumber() + 1), s:" Upgrades ", s:Weapon_GetActor(wid), s:" to: ", s:WeaponUpgrade_getActor(wid, type-1));
	Weapon_Swap(Weapon_GetActor(wid),WeaponUpgrade_getActor(wid, type));
}

function void WeaponPack_Give(int wp){
	SetActivatorToPlayer(PlayerNumber());
	for (int i = fp_WeaponStack; i >= 0 ; i--){
		if(Weapon_getWepPack(i) == wp){
			str weapon = Weapon_getActor(i);
			//giveinventory(weapon, 1);
			GiveActorReplacedInventory(ActivatorTID(), weapon, 1);
			setweapon(weapon);
		}
	}
	WPTrack_Increase(wp);
}


function void Weapon_Recycle(int wid, int upg, int play){
	str whatweapon;
	int rcyprice;
	if(upg != -1){
		whatweapon = WeaponUpgrade_GetActor(wid, upg);
		rcyprice = WeaponUpgrade_getRcyPrice(wid, upg);
	}else{ 
		whatweapon = Weapon_getActor(wid);
		rcyprice = Weapon_getPrice(wid);
	}
	
	SetActivatorToPlayer(play);
	TakeInventory(whatWeapon, 1);
	sf_AddCredits(rcyprice, play);
}




// Definers.
///////////////////////////////////////////////////////////////////////////////

function bool Weapon_IsNotUpgraded(int asThis){
	int ihavebaseweapon = checkinventory(Weapon_GetActor(asThis));
	int ihaveanyupgrade = false;
	for(int i = 0; i < Weapon_getUpsNum(asThis) && !ihaveanyupgrade; i++){
		ihaveanyupgrade = checkinventory(WeaponUpgrade_getActor(asThis, i));
	}
	return ihavebaseweapon && !ihaveanyupgrade;
}

function bool Weapon_OwnedSameKind(int asThis){
	bool iGotItUpgraded = false;
	for(int i = 0; i < Weapon_getUpsNum(asThis) && !iGotItUpgraded; i++){
		iGotItUpgraded = CheckInventory(WeaponUpgrade_getActor(asThis, i));
	}
	return checkinventory(Weapon_GetActor(asThis)) || iGotItUpgraded;
}

function int Weapon_SaveLastUsed(void){
	int res = -1;
	for(int i = 0; i < fp_weaponStack; i++){
		if(Weapon_OwnedSameKind(i)){
			res = i;
			break;
		}
	}
	//log(s:"Weapon style save: ", s:Weapon_GetActor(res));
	return res;
}

function void Weapon_LoadLastUsed(int which){
	
	SetWeapon(Weapon_GetActor(which));
	SetWeapon(WeaponUpgrade_GetActor(which, 0));
	SetWeapon(WeaponUpgrade_GetActor(which, 1));
	for(int i = 0; i < Weapon_getUpsNum(which); i++){
		SetWeapon(WeaponUpgrade_GetActor(which, i));
	}
	//log(s:"Weapon style load: ", s:Weapon_GetActor(which));
	
}

function void WPTrack_Increase(int wp){
	int cat = WeaponPack_GetCat(wp);
	fp_WPTrack[wp] = fp_WPTrack[wp] + WeaponPack_GetScoreGain(wp);
}

function void WPTrack_Decrease(int wp){
	int cat = WeaponPack_GetCat(wp);
	fp_WPTrack[wp] = fp_WPTrack[wp] - WeaponPack_GetScoreGain(wp);
}

function int WPTrack_GetScore(void){
	int i, c;
	int score = 0;
	for(i = 0; i < fp_WeaponPAckStack; i++){
		score = score + fp_WPTrack[i];
	}
	
	int players = 0;
	for(i = 0; i < 64; i++){
		if(PlayerInGame(i)) players++;
		else break;
	}
	if(!players) return 0;
	return score / players;
}

function int UpTrack_GetScore(void){
	int i;
	int score = fp_UpTrack;
	int players = 0;
	
	for(i = 0; i < 64; i++){
		if(PlayerInGame(i)) players++;
		else break;
	}
	if(!players) return 0;
	return score / players;
}

function str WeaponUpgrade_GetActor(int wpb, int uid){ return fp_WpUps[wpb][uid].actor; }
function str WeaponUpgrade_GetName( int wpb, int uid){ return fp_WpUps[wpb][uid].name; }
function int WeaponUpgrade_GetDesc(int wpb, int uid){  return fp_WpUps[wpb][uid].description; }
function int WeaponUpgrade_GetUpgPrice(int wpb, int uid){ return fp_WpUps[wpb][uid].ups_price; }
function int WeaponUpgrade_GetRcyPrice(int wpb, int uid){ return fp_WpUps[wpb][uid].rcy_price; }
function int WeaponUpgrade_GetUpType(int wpb, int uid){ return fp_WpUps[wpb][uid].up_type; }
function int WeaponUpgrade_Find(int weapon_parent_id, int up_type) {
	//log(s:"Weapon ups for weapon #", d:weapon_parent_id, s:": ", d:Weapon_getUpsNum(weapon_parent_id));
	for(int i = 0; i < Weapon_getUpsNum(weapon_parent_id); i++){
		log(d:i);
		if(up_type == WeaponUpgrade_GetUpType(weapon_parent_id, i)){
			//log(d:i, s:", That's the target");
			return i;
		}
	}
	return -1;
}

function str WeaponPack_GetLangString(int wid){ return fp_WpPacks[wid].langstring; }
function str WeaponPack_GetName(int wid){  		return fp_WpPacks[wid].name; }
function int WeaponPack_GetCat(int wid){  		return fp_WpPacks[wid].category; }
function int WeaponPack_GetScoreGain(int wid){  return fp_WpPacks[wid].mon_pressure; }
function str WeaponPack_GetDesc(int wid){  		return fp_WpPacks[wid].description; }
function int WeaponPack_GetPrice(int wid){ 		return fp_WpPacks[wid].price; }
function int WeaponPack_GetForClass(int wid){ 	return fp_WpPacks[wid].forclass; }

function bool WeaponPack_IsEquipableForPClass(int wid){ 
	int forplayerclass = WeaponPack_GetForClass(wid);
	str playclass = GetActorClass(CONST_TID_PLAYER + PlayerNumber());
	str char_everyone = "#E";
	str char_notClass = "$";
	// #E = Everyone can use this weapon pack.
	// $PlayerClass = Not for this class (In conjunction with the @ character.)

	//log(s:"[WeaponPack_IsEquipableForPClass] forplayerclass: '", s:forplayerclass, s:"', playerclass: '", s:playclass, s:"'");
	//log(s:"forplayerclass: '", s:forplayerclass, s:"'");
	if(forplayerclass == 0 || StrIEquals(forplayerclass, "0")) return true;
	
	// Check if the player class name is contained in the forplayerclass string
	// Also check that it does not contain a '$' before the player class name.
	if(StrContainsI(strparam(s:char_notClass, s:forplayerclass), strparam(s:char_notClass, s:playclass))){
		//log(s:"Hey!, This guy does'nt use this pack!");
		return false;
	}

	return StrContainsI(forplayerclass, playclass) || StrContainsI(forplayerclass, char_everyone);
}
function bool WeaponPack_isOwned(int wid){
	bool res = false;
		for (int i = 0; (i < fp_WeaponStack) && !res; i++){
			if(Weapon_getWepPack(i) == wid){
				res = Weapon_OwnedSameKind(i);
			}
		}
	return res;
}
function int WeaponPack_Find(str langstring) {
	for(int i = 0; i < FP_WEP_PACKS; i++){
		if(StrEquals(langstring, WeaponPack_GetLangString(i))){
			return i;
		}
	}
	return -1;
}

function str Weapon_GetActor(int wid)		{ return fp_Wpns[wid].actor; }
function str Weapon_GetName(int wid)		{ return fp_Wpns[wid].name; }
function int Weapon_GetWepPack(int wid)		{ return fp_Wpns[wid].pack_id; }
function int Weapon_GetCat(int wid){
	return WeaponPack_GetCat(Weapon_GetWepPack(wid));
}
function int Weapon_getUpsNum(int wid)		{ return fp_Wpns[wid].upsnum; }
function int Weapon_GetDesc(int wid)		{ return fp_Wpns[wid].description; }
function int Weapon_GetPrice(int wid)		{ return fp_Wpns[wid].price; }
function int Weapon_GetForClass(int wid)	{ return fp_Wpns[wid].forclass; }
function int Weapon_IsEquipableForPClass(int wid){  
	int forplayerclass = Weapon_GetForClass(wid);
	str playclass = GetActorClass(CONST_TID_PLAYER + PlayerNumber());
	str char_everyone = "#E";
	str char_notClass = "$";
	// #E = Everyone can use this weapon pack.
	// $PlayerClass = Not for this class (In conjunction with the @ character.)

	if(forplayerclass == 0 || StrIEquals(forplayerclass, "0")) return true;
	
	// Check if the player class name is contained in the forplayerclass string
	// Also check that it does not contain a '$' before the player class name.
	if(StrContainsI(strparam(s:char_notClass, s:forplayerclass), strparam(s:char_notClass, s:playclass))){
		//log(s:"Hey!, This guy does'nt use this pack!");
		return false;
	}

	return StrContainsI(forplayerclass, playclass) || StrContainsI(forplayerclass, char_everyone);
}
function int Weapon_Find(str actor_name) {
	for (int i = 0; i < fp_WeaponStack; i++) {
		if (StrEquals(actor_name, Weapon_GetActor(i))) {
			return i;
		}
	}
	return -1;
}