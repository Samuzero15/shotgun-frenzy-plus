
#define RWPAGE_TAG 0
#define RWPAGE_PANEL_WEAPONS RWPAGE_TAG+1
#define RWPAGE_VAR_WEPSTACK RWPAGE_PANEL_WEAPONS+1
#define RWPAGE_VAR_CURRPAGE RWPAGE_VAR_WEPSTACK+1
#define RWPAGE_STRBUTTON_NEXTPAGE RWPAGE_VAR_CURRPAGE+1
#define RWPAGE_STRBUTTON_PREVPAGE RWPAGE_STRBUTTON_NEXTPAGE+1
#define RWPAGE_LABEL_CURRPAGE RWPAGE_STRBUTTON_PREVPAGE+1
#define RWPAGE_LABEL_WEAPONRCY RWPAGE_LABEL_CURRPAGE+1
#define RWPAGE_LABEL_NORCY1 RWPAGE_LABEL_WEAPONRCY+1
#define RWPAGE_LABEL_NORCY2 RWPAGE_LABEL_NORCY1+1
#define RWPAGE_WEAPON_STRBTN 0
#define RWPAGE_WEAPON_LABEL  1

#define RWPAGE_ITEMSPERPAGE 15

int rcywpn_vars[RWPAGE_LABEL_NORCY2+1];
int rcywpn_wepsell[RWPAGE_ITEMSPERPAGE][2];

function void RcyWpnPage_Update(int pageTag){
	setPageTitle(pageTag, "Recycle Weapons", CR_RED);

	if(cursor_hover[STCURSOR_HOVER_COMP] == -1 && !Prompt_GetState())
		Label_SetText(rcywpn_vars[RWPAGE_LABEL_WEAPONRCY], "");
	if(SamuTerminal_isOnSamePage(pageTag)){
		
		RcyWpnPage_UpdateRcyBtns();
	}
}

function void RcyWpnPage_UpdateRcyBtns(void){
	int stack = 0;
	int page_items = RWPAGE_ITEMSPERPAGE;
	rcywpn_vars[RWPAGE_VAR_WEPSTACK] = 0;

	int page_min_limit = rcywpn_vars[RWPAGE_VAR_CURRPAGE]*RWPAGE_ITEMSPERPAGE;
	int page_max_limit = (rcywpn_vars[RWPAGE_VAR_CURRPAGE]+1)*RWPAGE_ITEMSPERPAGE;

	int i = 0;
	for(i = 0; i < fp_WeaponStack; i++){
		if(Weapon_getWepPack(i) != -1 && Weapon_getWepPack(i) != -2 && Weapon_OwnedSameKind(i)){
			rcywpn_vars[RWPAGE_VAR_WEPSTACK]++;

			if(!(rcywpn_vars[RWPAGE_VAR_WEPSTACK] >= page_min_limit 
				&& rcywpn_vars[RWPAGE_VAR_WEPSTACK] <= page_max_limit)) continue;

			if(stack < RWPAGE_ITEMSPERPAGE) RcyWpnPage_UpdateButton(stack, i);
			stack++;
		}
	}
	bool can_i_show = stack > 0;
	Panel_SetShow(rcywpn_vars[RWPAGE_PANEL_WEAPONS], can_i_show);
	StrButton_SetShow  (rcywpn_vars[RWPAGE_STRBUTTON_PREVPAGE], can_i_show); 
	StrButton_SetShow  (rcywpn_vars[RWPAGE_STRBUTTON_NEXTPAGE], can_i_show); 
	Label_SetShow(rcywpn_vars[RWPAGE_LABEL_CURRPAGE], can_i_show);

	Label_SetShow(rcywpn_vars[RWPAGE_LABEL_NORCY1], !can_i_show);
	Label_SetShow(rcywpn_vars[RWPAGE_LABEL_NORCY2], !can_i_show);
	if(!can_i_show) return;

	int last_page = rcywpn_vars[RWPAGE_VAR_WEPSTACK]/RWPAGE_ITEMSPERPAGE;
	int sb;
	int lbl;
	if(stack < page_items){
		for(i = stack; i < page_items; i++){
			sb = rcywpn_wepsell[i][RWPAGE_WEAPON_STRBTN];
			lbl = rcywpn_wepsell[i][RWPAGE_WEAPON_LABEL];
			StrButton_SetShow(sb, false);
			Label_SetShow(lbl, false);
		}
	}
	if(rcywpn_vars[RWPAGE_VAR_CURRPAGE] > last_page) rcywpn_vars[RWPAGE_VAR_CURRPAGE] = last_page;
	StrButton_SetExeArgs  (rcywpn_vars[RWPAGE_STRBUTTON_PREVPAGE], -1, last_page, 0); 
	StrButton_SetExeArgs  (rcywpn_vars[RWPAGE_STRBUTTON_NEXTPAGE], 1, last_page, 0); 
	StrButton_SetShow  (rcywpn_vars[RWPAGE_STRBUTTON_PREVPAGE], true);
	StrButton_SetShow  (rcywpn_vars[RWPAGE_STRBUTTON_NEXTPAGE], true);
	
	Label_SetText	(rcywpn_vars[RWPAGE_LABEL_CURRPAGE], 
		strparam(d:rcywpn_vars[RWPAGE_VAR_CURRPAGE]+1, s:"/", d:last_page+1));
	
		
	if(rcywpn_vars[RWPAGE_VAR_CURRPAGE] == 0)
		StrButton_SetShow  (rcywpn_vars[RWPAGE_STRBUTTON_PREVPAGE], false); 
	
	if(rcywpn_vars[RWPAGE_VAR_CURRPAGE] == last_page)
		StrButton_SetShow  (rcywpn_vars[RWPAGE_STRBUTTON_NEXTPAGE], false); 

}

function void RcyWpnPage_UpdateButton(int slot, int wpn){
	int sb = 0, lbl = 0; 
	sb = rcywpn_wepsell[slot][RWPAGE_WEAPON_STRBTN];
	lbl = rcywpn_wepsell[slot][RWPAGE_WEAPON_LABEL];
	int color_cat = term_catStyle[Weapon_GetCat(wpn)][TERM_CATSTYLE_COLOR];
	StrButton_SetShow(sb, true);
	//StrButton_SetTag(sb, rcywpn_vars[RWPAGE_TAG]);
	int charcap = 10;
	str weapon_name = Weapon_GetName(wpn);
	int price = Weapon_GetPrice(wpn);
	int up_recycle = -1;

	for(int i = 0; i < Weapon_getUpsNum(wpn); i++){
		if(CheckInventory(WeaponUpgrade_getActor(wpn, i))){
			price = WeaponUpgrade_getRcyPrice(wpn, i);
			up_recycle = i;
		} 
	}

	str upgrade_str = "No Upg";
	if(up_recycle != -1) upgrade_str = strparam(s:"Type ", d:up_recycle+1);
	
	if(StrLen(weapon_name) > charcap) weapon_name = StrParam(s:StrLeft(weapon_name, charcap), s:"...");
	StrButton_SetText(sb, weapon_name);
	StrButton_SetColor(sb, color_cat);
	StrButton_SetExeArgs(sb, wpn, up_recycle, 0);
	StrButton_SetExeHoverArgs(sb, wpn, up_recycle, 0);

	Label_SetText(lbl, strparam(d:price, s:" $ - ", s:upgrade_str));
	Label_SetShow(lbl, true);
	Label_SetColor(lbl, color_cat);
}

// I'm Doing this from scratch.
function void RcyWpnPage_Init(int pageTag){
	rcywpn_vars[RWPAGE_TAG] = pageTag;

	int i = Panel_Add(65.0, 60.0, 62, 25);
	Panel_SetTag(i, rcywpn_vars[RWPAGE_TAG]);
	Panel_SetAnchors(i, STPANEL_ANCHOR_TOP, STPANEL_ANCHOR_LEFT);
	Panel_SetColumns(i, 3);
	Panel_SetSpacing(i, 175.0, 36.0);
	Panel_SetAnchorOffset(i, 8.0, 0.0);
	rcywpn_vars[RWPAGE_PANEL_WEAPONS] = i;

	RcyWpnPage_MakeRWButtons(rcywpn_vars[RWPAGE_PANEL_WEAPONS]);
	int base_x_navs = 250.0;
	int base_y_navs = 290.0;
	int btn_dist_navs = 64.0;

	i = StrButton_AddMid   (base_x_navs, base_y_navs, "<");
	StrButton_SetExecute(i, STEXE_MOVERC);
	StrButton_setTag    (i, rcywpn_vars[RWPAGE_TAG]);
	rcywpn_vars[RWPAGE_STRBUTTON_PREVPAGE] = i;

	i = StrButton_DupMid  (i, base_x_navs + btn_dist_navs*2, base_y_navs);
	StrButton_SetText   (i, ">");
	rcywpn_vars[RWPAGE_STRBUTTON_NEXTPAGE] = i;

	i = Label_Add(base_x_navs + btn_dist_navs, base_y_navs);
	Label_setFont(i,"DBIGFONT");
	Label_setText(i, "n / m");
	Label_setTag (i, rcywpn_vars[RWPAGE_TAG]);
	Label_setAligns(i, HUD_ALIGN_CENTER, HUD_ALIGN_CENTER);
	rcywpn_vars[RWPAGE_LABEL_CURRPAGE] = i;

	i = Label_Dup(i, 90.0, 330.0);
	Label_setText(i, "Weapon Name Here");
	Label_setAligns(i, HUD_ALIGN_TOP, HUD_ALIGN_LEFT);
	rcywpn_vars[RWPAGE_LABEL_WEAPONRCY] = i;

	i = Label_Dup(rcywpn_vars[RWPAGE_LABEL_CURRPAGE], Vars_Mid_Wid(), Vars_Mid_Hei() - 30.0);
	Label_SetText(i, langstring("SAMUT_ARMSTERM_NOACCESS_RCY_TITLE", 0));
	rcywpn_vars[RWPAGE_LABEL_NORCY1] = i;

	i = Label_Dup(i, Vars_Mid_Wid(), Vars_Mid_Hei());
	Label_setFont(i,"SMALFONT");
	Label_SetText(i, langstring("SAMUT_ARMSTERM_NOACCESS_RCY_LABEL", 0));
	rcywpn_vars[RWPAGE_LABEL_NORCY2] = i;
}

function void RcyWpnPage_MakeRWButtons(int panel){
	int i = 0;
	int sb = 0, lbl = 0;
	for(int j = 0; j < RWPAGE_ITEMSPERPAGE; j++){
		//RcyWpnPage_MakeRWButton(rcywpn_vars[RWPAGE_PANEL_WEAPONS], j);
		if(j == 0){
			sb = StrButton_AddToPanelPos ("_________", panel, j);
			StrButton_SetExecute(sb, STEXE_ASKRC);
			StrButton_SetExeHover(sb, STEXE_LABELRC);
			//StrButton_SetShow(sb, true);
			rcywpn_wepsell[j][RWPAGE_WEAPON_STRBTN] = sb;
		
			lbl = Label_AddToPanelPosOff (panel, j, 75.0, 25.0);
			Label_SetText(lbl, strparam(s:"price"));
			Label_SetFont(lbl, "SMALLFNT");
			rcywpn_wepsell[j][RWPAGE_WEAPON_LABEL] = lbl;
			continue;
		}
		i = StrButton_DupToPanelPos(sb, panel, j);
		rcywpn_wepsell[j][RWPAGE_WEAPON_STRBTN] = i;

		i = Label_DupToPanelPosOff (lbl, panel, j, 75.0, 25.0);
		rcywpn_wepsell[j][RWPAGE_WEAPON_LABEL] = i;
	}
	
}