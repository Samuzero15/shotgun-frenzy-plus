/**
*
*	<samut_comps.acs>
*	Here it declares all of the components for the terminal.
*	This is the part where you can customize your menu stuff!
*
*/

#include "samut_comps_ibtns.acs"
#include "samut_comps_sbtns.acs"
#include "samut_comps_lbls.acs"
#include "samut_comps_imgs.acs"
#include "samut_comps_toast.acs"
#include "samut_comps_prompt.acs"
#include "samut_comps_cursor.acs"
#include "samut_comps_panels.acs"
#include "samut_comps_numinput.acs"

function void SamuTerm_InitComps(void){
	Components_Clear();
	pagetags[STTAGS_NOW] = pagetags[STTAGS_ALPHA];
	Cursor_Initialize(32.0, 32.0);
	
	initTitle();
	Toaster_Initialize();
	Prompt_Initialize();
	
	//Custom shiet.
	SamuTerm_InitPages();
}

function void Components_Clear(void){
	RectsDelete(0);
	Vars_ClearCompCounts();
	Vars_ClearTmps();
	Grid_ClearAll();
	Card_ClearAll();
	pagetags[STTAGS_NOW] = 0;
	pagetags[STTAGS_LAST] = 0;
}

function void Component_Save(int comprect){
	for(int i = 0; i < ACSUTILS_RECTLIB_USERVARS; i++){
		comp_propsave[i] = RectGetUserData(comprect, i);
	}
}

function int Component_GetRect(int type, int id){
	int res;
	switch(type) {// Hud Id is overriden. Fix that with a redefining.
		case STCOMP_IBTN:	res = ImgButton_GetRect(id); break;
		case STCOMP_SBTN:	res = StrButton_GetRect(id); break;
		case STCOMP_IMG:	res = Image_GetRect(id); break;
		case STCOMP_LBL:	res = Label_GetRect(id); break;
	}
	return res;
}

function void Component_Load(int id, int rect){
	int type = 0;
	for(int i = 0; i < ACSUTILS_RECTLIB_USERVARS; i++){
		SetRectUserData(rect, i, comp_propsave[i]);
		if(i == 0) type = comp_propsave[i];
	}

	switch(type) {// Hud Id is overriden. Fix that with a redefining.
		case STCOMP_IBTN:	ImgButton_setHudId(id, ST_HID_IBTN + id); break;
		case STCOMP_SBTN:	StrButton_setHudId(id, ST_HID_SBTNF + id); StrButton_setBackHudId(id, ST_HID_SBTNB + id); break;
		case STCOMP_IMG:	Image_setHudId	(id, ST_HID_IMG + id); break;
		case STCOMP_LBL:	Label_setHudId	(id, ST_HID_LBL + id); break;
	}
}

function int Component_Allocate(int comptype, int x, int y, int w, int h){
	int id = comp_count[comptype];
	if(id < ST_NCOMPS){
		int rect = RectCreate(x,y,w,h);
		SetRectUserData(rect, ST_CPROP0, comptype); // Remember the type of component here.
		switch(comptype){
			case STCOMP_IBTN:	ImgButton_setRect(id, rect); break;
			case STCOMP_SBTN:	StrButton_setRect(id, rect); break;
			case STCOMP_IMG:	Image_setRect	(id, rect); break;
			case STCOMP_LBL:	Label_setRect	(id, rect); break;
		}
		//if(comptype == STCOMP_IBTN) print(s:"Id: ", d:id, s:" for image button added.");
		comp_count[comptype]++;
	}else{
		print(s:"Too many components!");
		id = -1;
	}
	//print(d:id);
	
	return id;
}

function void Component_PrintCount(void){
	str type_str;
	int rect_count;
	log(s:"Component cap: \cg", d:ST_NCOMPS);
	for (int i = 0; i < STVARS_COMPCOUNT; i++){
		switch(i){
			case 0: type_str = "IMG BUTTON"; break;
			case 1: type_str = "LABELS"; break;
			case 2: type_str = "IMAGES"; break;
			case 3: type_str = "STR BUTTON"; break;
			case 4: type_str = "PANEL"; break;
		}
		log(s:type_str, s:", \cd", d:comp_count[i]);
	}
	log(s:"TOAST:", s:", \cd", d:1);
	log(s:"CURSOR:", s:", \cd", d:1);
	log(s:"Rects used: \cf", d:numrects, s:" / ", d:ACSUTILS_RECTLIB_SAVEDSTATES);
	if(rect_count > ACSUTILS_RECTLIB_SAVEDSTATES){
		log(s:"\cgACSRect overflow! Expand the \cf'ACSUTILS_RECTLIB_SAVEDSTATES'\cg Constant in \cfacsrect.acs\cg.");
	}
}

function void initTitle(void){

	sfpcomps[SF_SFCOMPS_TITLE] = Label_Add(320.0,40.0); 
	Label_setText(sfpcomps[SF_SFCOMPS_TITLE],"Title");
	Label_setFont(sfpcomps[SF_SFCOMPS_TITLE],"DBIGFONT");
	Label_setColor(sfpcomps[SF_SFCOMPS_TITLE],CR_WHITE);
	Label_setTag(sfpcomps[SF_SFCOMPS_TITLE], -1);
	Label_setShow(sfpcomps[SF_SFCOMPS_TITLE], false);
}

function void setTitleText(str title){
	Label_setText(sfpcomps[SF_SFCOMPS_TITLE], title);
}

function void setTitleColor(int color){
	Label_setColor(sfpcomps[SF_SFCOMPS_TITLE], color);
}
