/**
*
* 	<samut_input.acs> 
* 	Input module!
*	Here is where the "click" gets analized. You don't usually, touch this.
*
**/

function void SamuTerm_Input(int play){
	if(CheckInventory("SamuTInv_LockInput")){
		hudmessage(s:"Waiting..."; HUDMSG_PLAIN, 0, 0, 0.5, 0.5, 1.0);
		return;
	}
	if(PlayerKeyDown(play, FIRE1) && PlayerKeyPressed(play, FIRE1)){
		if(!GetPromptState())
			CheckClick();
		else CheckPromptClick();
	}
	
	if(!GetPromptState())
		CheckHover();
	else CheckPromptHover();
}

function void CheckPromptClick(void){
	int rect, i = 0;
	bool inputDone = false;
	
	inputDone = ExecuteOnClick_IBtn(prompt_ok);
	if(!inputDone) inputDone = ExecuteOnClick_IBtn(prompt_yes); 
	if(!inputDone) inputDone = ExecuteOnClick_IBtn(prompt_no);
	
	if(!inputDone){
		localAmbientSound("SamuTerm/Error",127);
	}
}

function bool ExecuteOnClick_IBtn(int i){
	int sucess = false;
	int rect =  ImgButton_getRect(i);
	if(RectIsClicked(rect,FIRE1) && ImgButton_getShow(i) != ST_Disabled){
		SamuTerm_CallOpExecute 
			(ImgButton_getExecute(i), ImgButton_getExeArg1(i),  
			ImgButton_getExeArg2(i),  ImgButton_getExeArg3(i));
		localAmbientSound("SamuTerm/Select",127);
		sucess = true;
	}
	return sucess;
	
}

function bool ExecuteOnHover_IBtn(int i){
	int sucess = false;
	int rect =  ImgButton_getRect(i);
	if(ImgButton_getShow(i) != ST_Disabled){
		SamuTerm_CallOpExecute 
			(ImgButton_getExeHover(i), ImgButton_getHoverArg1(i),  
			ImgButton_getHoverArg2(i),  ImgButton_getHoverArg3(i));
		//localAmbientSound("SamuTerm/Select",127);
		sucess = true;
	}
	return sucess;
	
}

function bool ExecuteOnClick_sBtn(int i){
	int sucess = false;
	int rect = StrButton_getRect(i);
	if(RectIsClicked(rect,FIRE1) && StrButton_getShow(i) != ST_Disabled){
		SamuTerm_CallOpExecute 
			(StrButton_getExecute(i), StrButton_getExeArg1(i),  
			StrButton_getExeArg2(i),  StrButton_getExeArg3(i));
		//log(s:"You clicked a string button!");
		localAmbientSound("SamuTerm/Select",127);
		sucess = true;
	}
	return sucess;
	
}

function void CheckClick(void){
	int rect, i = 0;
	bool inputDone = false;
	for(i = 0; i < comp_count[STCOMP_IBTN] && !inputDone; i++){
		inputDone = ExecuteOnClick_IBtn(i);
	}
	for(i = 0; i < comp_count[STCOMP_SBTN] && !inputDone; i++){
		inputDone = ExecuteOnClick_SBtn(i);
	}
	//log(s:"Click!");
}

function void CheckHover(void){
	int rect;
	bool inputDone = false;
	
	bool nowHover = false;
	lasthover = comphover;
	comphover = -1;
	//log(s:"No Change. ", d:nowHover);
	
	for(int j = 0; j < comp_count[STCOMP_IBTN] && !inputDone; j++){
		rect = ImgButton_getRect(j);
		if(RectIsHovered(rect) && ImgButton_getShow(j) != ST_Disabled){
			ExecuteOnHover_IBtn(j);
			comphover = j;
			inputDone = true;
			break;
		}
	}
	for(int i = 0; i < comp_count[STCOMP_SBTN] && !inputDone; i++){
		rect = StrButton_getRect(i);
		if(RectIsHovered(rect) && StrButton_getShow(i) != ST_Disabled){
			comphover = i;
			inputDone = true;
			break;
		}
	}
	if(lasthover != comphover && (comphover != -1))
		localAmbientSound("SamuTerm/Hover",127);
	//log(s:"Click!");
}

function void CheckPromptHover(void){
	int rect;
	bool inputDone = false, nowhover;
	
	
	//log(s:"No Change. ", d:nowHover);
	inputDone = IsHovering_ibtn(prompt_ok);
	if(!inputDone) inputDone = IsHovering_ibtn(prompt_yes); 
	if(!inputDone) inputDone = IsHovering_ibtn(prompt_no);
	
	nowhover = inputDone;
	
	if(nowHover && !onceHover){
		localAmbientSound("SamuTerm/Hover",127);
		oncehover = true;
	}else if (!nowHover && onceHover) 
		oncehover = false;
	//log(s:"Click!");
}

function bool IsHovering_ibtn(int i){
	bool res = false;
	int rect = ImgButton_getRect(i);
	if(RectIsHovered(rect) && ImgButton_getShow(i) != ST_Disabled){
		res = true;
	}
	return res;
}

script "SamuTerm_FreePlayer" (int play) NET{
	unlockPlayer(play);
	takeActorInventory(1000+play,"SamuTInv_UsingTerminal", 1);
}