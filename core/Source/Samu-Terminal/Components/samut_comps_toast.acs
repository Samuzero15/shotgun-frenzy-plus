
/**
	A toast works like a label, but it's more used to show quick messages.
	It's your notification string from your android/ios.
	Except its ported to Doom!
*/

#include "samut_cgnsf_toast.acs"

function void Toaster_Initialize (void){
	int rect = RectCreate(320.0,440.0,0.0,0.0);
	Toast_SetRect(rect);
	Toaster_SetProps("I am a Toast!", CR_BRICK);
}

function void Toaster_ShowToast(str msg, str color){
	Toaster_SetProps(msg, color);
	ShowToast();
}

function void Toaster_ShowToastLog(str msg, str color){
	Toaster_SetProps(msg, color);
	Toast_SetLogMessage(true);
	ShowToast();
	Toast_SetLogMessage(false);
}

function void Toaster_SetProps (str string, int color){
	Toast_SetString(string);
	Toaster_Setup (string);
	Toast_SetColor(color);
}

function void Toaster_Setup (str string){
	int w = 0;
	int h = 0;
	
	int charw = 0;
	int charh = 0;
	int chars = 0;
	str btnstring = "A";
	string = StripColorCodes(string);
	for(int i = 0; i < StrLen(string); i++){
		int c = GetChar(string, i);
		if(c == '\n'){
			charh += 8;
			chars = 0;
		}else chars += 8;
		if(charw < chars) charw = chars;
	}
	if(charw > 2){
		w = (charw/8)-1;
		if(charw%8 != 0) w++;
	}
	h = (charh/8);
	if(charh%8 != 0) h++;

	if(w != 0){
		for(int j = 0; j < w; j++){
			btnstring = StrAdd(btnstring, "B");
		}
	}
	btnstring = StrAdd(btnstring, "C");
	
	if(w != 0 && h != 0){
		for(int y = 0; y < h; y++){
		btnstring = StrAdd(btnstring, "\nD");
			for(int x = 0; x < w; x++){
				btnstring = StrAdd(btnstring, "E");
			}
		btnstring = StrAdd(btnstring, "F");
		}
	}
	
	btnstring = StrAdd(btnstring, "\nG");
	if(w != 0){
		for(int k = 0; k < w; k++){
			btnstring = StrAdd(btnstring, "H");
		}
	}

	btnstring = StrAdd(btnstring, "I");
	
	Toast_SetBack(btnstring);
	
}
