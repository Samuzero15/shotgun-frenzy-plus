

/**
	A prompt. I ask you something, You answer, Simple.
*/

#include "samut_cgnsf_prompt.acs"

function void Prompt_Initialize (void){
	
	int w = 30, h = 20;
	int fw = 16.0 + (8.0*w);
	int fh = 16.0 + (8.0*h);
	
	int posx = FixedDiv(640.0, 2.0)-FixedDiv(fw, 2.0);
	int posy = FixedDiv(480.0, 2.0)-FixedDiv(fh, 2.0);
	
	int rect = RectCreate(posx,posy, fw, fh);
	setPromptRect(rect);
	
	Prompt_SetProperties ("A question",CR_GRAY,w,h, false);
	setPromptTag(200);
	
	//log(s:" X: ", f:RectX2(rect), s:", Y: ", f:RectY2(rect));
	
	prompt_ok = ImgButton_Add_P(
		0.0,0.0, 32.0, 32.0, ST_COMPALIGN_South, rect); 
		
	ImgButton_SetProperties(prompt_ok, "BTNOK", "Okay",false, STEXE_OKAY);
	ImgButton_setHudID(prompt_ok,ST_HID_PROMPT_BTNOK);
	ImgButton_setShow(prompt_ok, false);

	prompt_yes = ImgButton_Add_P(
		-32.0,0.0, 32.0, 32.0, ST_COMPALIGN_South, rect); 
		
	ImgButton_SetProperties(prompt_yes, "BTNYES", "Accept",false, STEXE_YES);
	ImgButton_setHudID(prompt_yes, ST_HID_PROMPT_BTNYES);
	
	
	prompt_no = ImgButton_Add_P(
		32.0,0.0, 32.0, 32.0, ST_COMPALIGN_South, rect); 
	ImgButton_SetProperties(prompt_no, "BTNNO", "Decline",false, STEXE_NO);
	ImgButton_setHudID(prompt_no,ST_HID_PROMPT_BTNNO);
	
	prompt_label = Label_Add_P 
		(0.0,0.0, ST_COMPALIGN_North, rect);
	Label_SetText(prompt_label,"This is a small message from the prompt.");
	Label_SetColor(prompt_label, getPromptColor());
	Label_SetFont(prompt_label,"CONFONT");
	Label_SetShow(prompt_label, true);
	Label_setHudID(prompt_label,ST_HID_PROMPT_LBL);
	Label_setTag(prompt_label,-9999);
	
	Prompt_ResetOptions();
	
	Prompt_SetOptions(0);
	setPromptState(false);
}

function void Prompt_SetOptions(int style)
{
	switch(style){
		case 1: // Yes or not answer.
			ImgButton_setTag(prompt_yes,getPromptTag());
			ImgButton_setTag(prompt_no,getPromptTag());
			ImgButton_setTag(prompt_ok,-9999);
		break;
		default: // Accept answer.
			ImgButton_setTag(prompt_yes,-9999);
			ImgButton_setTag(prompt_no,-9999);
			ImgButton_setTag(prompt_ok,getPromptTag());
		break;
	}
}

function void Prompt_ResetOptions(void)
{
	ImgButton_setShow(prompt_yes, false);
	ImgButton_setShow(prompt_no, false);
	ImgButton_setShow(prompt_ok, false);
	ImgButton_setTag(prompt_yes,-9999);
	ImgButton_setTag(prompt_no,-9999);
	ImgButton_setTag(prompt_ok,-9999);
}

function void Prompt_Resize(int w, int h){
	int fw = 16.0 + (8.0*w);
	int fh = 16.0 + (8.0*h);
	
	int posx = FixedDiv(640.0, 2.0)-FixedDiv(fw, 2.0);
	int posy = FixedDiv(480.0, 2.0)-FixedDiv(fh, 2.0);
	
	//print(s:"Before reposition: ",f:getPromptX());
	
	SetRectPosition(getPromptRect(), posX, posY);
	SetRectSize(getPromptRect(), fw, fh);
	
	//print(s:"After reposition: ",f:getPromptX());
	StrButton_GenerateBack(w, h);
	setPromptBack(tmp[2]);
	AdjustImgButton_P(prompt_yes,-32.0, 0.0, ST_COMPALIGN_South, getPromptRect());
	AdjustImgButton_P(prompt_no,32.0, 0.0, ST_COMPALIGN_South, getPromptRect());
	AdjustImgButton_P(prompt_ok, 0.0, 0.0, ST_COMPALIGN_South, getPromptRect());
	AdjustLabel_P(prompt_label, 0.0, 0.0, ST_COMPALIGN_North, getPromptRect());
}

function void Prompt_Ask(str title, str msg, str color){
	Prompt_ResetOptions();
	setPromptTitle(title);
	setPromptColor(color);
	setPromptExecute_YesAction(0);
	setPromptExecute_NoAction(0);
	
	Label_setText(prompt_label, msg);
	Label_setColor(prompt_label, color);
	Prompt_SetOptions(1);
	
	// In case i was'nt clear enough.
	ImgButton_setParent(prompt_ok, getPromptRect());
	ImgButton_setParent(prompt_yes, getPromptRect());
	ImgButton_setParent(prompt_no, getPromptRect());
	
	localAmbientSound("SamuTerm/HopIn",127);
	setPromptState(true);
}

function void Prompt_ExecuteOnTrue(int cmd, int arg1, int arg2, int arg3){
	setPromptExecute_YesAction(cmd);
	setPromptExeArgs_YesAction(arg1,arg2,arg3);
}

function void Prompt_ExecuteOnFalse(int cmd, int arg1, int arg2, int arg3){
	setPromptExecute_NoAction(cmd);
	setPromptExeArgs_NoAction(arg1,arg2,arg3);
}

function void Prompt_Notify(str title, str msg, str color){
	Prompt_ResetOptions();
	setPromptTitle(title);
	setPromptColor(color);
	
	Label_setText(prompt_label, msg);
	Label_setColor(prompt_label, color);
	Prompt_SetOptions(0);
	
	setPromptState(true);
}

function void Prompt_SetProperties (str title,int color,int w,int h, bool state){

		StrButton_generateBack(w, h);
		setPromptTitle(title);
		setPromptState(state);
		setPromptWindowId(1);
		setPromptValue(0);
		setPromptColor(color);
		setPromptBack(tmp[2]);
}