
#include "samut_cgnsf_panels.acs"

function int Panel_Add(int x, int y, int wx8, int hx8){
	int i = panel_count;
	
	int fw = 16.0 + (8.0*wx8);
	int fh = 16.0 + (8.0*hx8);
	
	//print(s:StrSfPfFixed("Width: ", fw, " pixels"), s:" ", s:StrSfPfFixed("Height: ", fh, " pixels"));
	
	if(i < ST_NCOMPS){
		int rect = RectCreate(x,y,fw,fh);
		Panel_setRect(i,rect);
		Panel_SetDefaultProps(i);
		Panel_GenerateBack(i);

		panel_count++;
	}else print(s:"No more panels to add!");
	
	return i;
}

function void Panel_AddCompOff(int p, int rect, int offx, int offy){ // Add some offset
	Panel_AddCompEX(p, rect, -1, offx, offy, false);
}

function void Panel_AddCompPos(int p, int rect, int pos){ // Pick a specific position
	Panel_AddCompEX(p, rect, pos, 0.0, 0.0, false);
}

function void Panel_AddComp(int p, int rect){ // Simple
	Panel_AddCompEX(p, rect, -1, 0.0, 0.0, false);
}

function void Panel_AddCompEX(int p, int rect, int numb_pos, int offx, int offy,bool absolute){
	// The full function
	//log(s:"Panel number of components: ", d:Panel_GetNumberOfComps(p));
	int count = numb_pos <= -1 ? (Panel_GetNumberOfComps(p) - Panel_GetNumberOfCompsAbs(p)) : numb_pos;
	int pos[2];
	pos[0] = Panel_getClipX1(p);
	pos[1] = Panel_getClipY1(p); 
	int spacing[2];
	spacing[0] = Panel_GetSpacingX(p);
	spacing[1] = Panel_GetSpacingY(p); 
	int columns = Panel_GetColumns(p);
	int anchors[2];
	anchors[0] = Panel_getAnchorX(p);
	anchors[1] = Panel_getAnchorY(p); 
	
	/*log(s:"Virtual size: (", f:virtual_size[0], s:", ", f:virtual_size[1], s:")");
	log(s:"Spacing Anchored: (", f:spacing_anchored[0], s:", ", f:spacing_anchored[1], s:")");
	log(s:"Component base position Anchored: (", f:comp_pos[0], s:", ", f:comp_pos[1], s:")");
	*/

	SetRectPosition(rect, 
		pos[0] + (absolute ? offx : spacing[0]*(count%columns) + offx), 
		pos[1] + (absolute ? offx : spacing[1]*(count/(columns)) + offy));
	Panel_AddCompNumber(p);
	if(absolute) Panel_AddCompAbsNumber(p);
}

function void Panel_SetDefaultProps(int i){
	Panel_ResetCompNumber(i);
	Panel_ResetCompAbsNumber(i);
	Panel_setColor(i,CR_WHITE);
	Panel_setTag(i,0);
	Panel_setHudId(i, ST_HID_PAN_BACKG + i);
	Panel_SetScrollAmount(i, 1.0, 1.0);
	Panel_SetVirtualSize(i, 0.0, 0.0);
	Panel_SetColumns(i, 1);
	Panel_SetTitleAnchors(i, STPANEL_ANCHOR_CENTER, STPANEL_ANCHOR_TOP);
	Panel_SetAnchors(i, STPANEL_ANCHOR_LEFT, STPANEL_ANCHOR_TOP);
	Panel_SetClipRect(i, -1, -1, -1, -1);
}

function void Panel_MakeScrollButton(int i, int id, int x, int y, int scrollx, int scrolly, int img){
	int btn = ImgButton_Add(x, y, 8.0, 8.0);
	ImgButton_setExecute(btn, STEXE_PANEL_SCROLL);
	ImgButton_setArgs(btn, i, scrollx, scrolly);
	ImgButton_setShow(btn, true);
	ImgButton_setImage(btn, img);
	ImgButton_setHudId(btn, ST_HID_PAN_FOREG + btn);
	panels_scrollbuttons[i][id] = btn;
}

function bool Panel_ImgBtnIsClicked(int panel, int ibtn, int key){
	return Panel_ButtonIsClicked(panel, Imgbutton_GetX1Offseted(ibtn), Imgbutton_GetY1Offseted(ibtn), 
		Imgbutton_GetX2Offseted(ibtn), Imgbutton_GetY2Offseted(ibtn), key);
}

function bool Panel_ImgBtnIsHovered(int panel, int ibtn)
{
	return Panel_ButtonIsClicked(panel, Imgbutton_GetX1Offseted(ibtn), Imgbutton_GetY1Offseted(ibtn), 
		Imgbutton_GetX2Offseted(ibtn), Imgbutton_GetY2Offseted(ibtn), -1);
}

function bool Panel_StrBtnIsClicked(int panel, int ibtn, int key){
	return Panel_ButtonIsClicked(panel, Strbutton_GetX1Offseted(ibtn), Strbutton_GetY1Offseted(ibtn), 
		Strbutton_GetX2Offseted(ibtn), Strbutton_GetY2Offseted(ibtn), key);
}

function bool Panel_StrBtnIsHovered(int panel, int ibtn)
{
	return Panel_ButtonIsClicked(panel, Strbutton_GetX1Offseted(ibtn), Strbutton_GetY1Offseted(ibtn), 
		Strbutton_GetX2Offseted(ibtn), Strbutton_GetY2Offseted(ibtn), -1);
}

function bool Panel_ButtonIsClicked(int panel, int btnx1, int btny1, int btnx2, int btny2, int key)
{
	int cursor[2] = {CursorX(), CursorY()};
	int pan_clip[4] = { // Get the clipping points of the panel.
		Panel_GetClipX1(panel),
		Panel_GetClipY1(panel),
		Panel_GetClipX2(panel),
		Panel_GetClipY2(panel)
	};
	bool res;
	bool intersects = !(pan_clip[0] >= btnx2 ||btnx1 >= pan_clip[2] || pan_clip[1] >= btny2 || btny1 >= pan_clip[3]);
	//log(s:"Img Btn Offset: ( ", f:btnx1, s:", ", f:btny1, s:", ", f:btnx2, s:", ", f:btny2, s:" ) Intersercts? ", d:intersects);
	if(intersects){
		int clip_rect[4] = {
		max(pan_clip[0], btnx1),
		max(pan_clip[1], btny1),
		min(pan_clip[2], btnx2),
		min(pan_clip[3], btny2)
		};
		//log(s:" Intersected rectangle: ", f:abs(clip_rect[0] - clip_rect[2]), s:"x", f:abs(clip_rect[1] - clip_rect[3]));
		bool cursor_inside = cursor[0] > clip_rect[0] && cursor[0] < clip_rect[2] && cursor[1] > clip_rect[1] && cursor[1] < clip_rect[3];
		//log(s:" Cursor: ( ", f:cursor[0], s:",", f:cursor[1], s:") \n Cursor Inside? ", d:cursor_inside);
		if(key == -1){
			res = cursor_inside;
			/*if(res){
				
			}*/
		}else {
			res = cursor_inside && KeyPressed(key);
		}
	}else{
		res = false;
	}
	return res;
}

function void Panel_UpdateScrollButtons(int pan){
	int btn_up = panels_scrollbuttons[pan][0];
	int btn_down = panels_scrollbuttons[pan][1];
	int btn_left = panels_scrollbuttons[pan][2];
	int btn_right = panels_scrollbuttons[pan][3];
	int pan_show = Panel_getShow(pan);

	ImgButton_setShow(btn_up, pan_show);
	ImgButton_setShow(btn_down, pan_show);
	ImgButton_setShow(btn_left, pan_show);
	ImgButton_setShow(btn_right, pan_show);
	if(!pan_show) return;
	/*
	
	*/
	int pan_x = Panel_getX(pan);
	int pan_y = Panel_getY(pan);
	int pan_h = Panel_getHeight(pan);
	int pan_w = Panel_getWidth(pan);
	ImgButton_setPosition(btn_up, pan_x, pan_y+8.0);
	ImgButton_setPosition(btn_down, pan_x, pan_y+(pan_h-16.0));
	ImgButton_setPosition(btn_left, pan_x+8.0, pan_y+(pan_h-8.0));
	ImgButton_setPosition(btn_right, pan_x+(pan_w-16.0), pan_y+(pan_h-8.0));

	int scrollx = Panel_GetScrollX(pan);
	int scrolly = Panel_GetScrollY(pan);
	int virt_w = Panel_GetVirtualWidth(pan);
	int virt_h = Panel_GetVirtualHeight(pan);
	int virt_x = Panel_GetVirtualOffsetX(pan);
	int virt_y = Panel_GetVirtualOffsetY(pan);
	ImgButton_setArgs(btn_up, pan, 0, scrolly);
	ImgButton_setShow(btn_up, virt_y == 0 ? false : true);
	ImgButton_setArgs(btn_down, pan, 0, -scrolly);
	ImgButton_setShow(btn_down, virt_y == -virt_h ? false : true);
	ImgButton_setArgs(btn_left, pan, scrollx, 0);
	ImgButton_setShow(btn_left, virt_x == 0 ? false : true);
	ImgButton_setArgs(btn_right, pan, -scrollx, 0);
	ImgButton_setShow(btn_right, virt_x == -virt_w ? false : true);
}

function str Panel_GenerateBack(int i){
	str btnstring = "A";
	int xpan = Panel_GetX(i);
	int ypan = Panel_GetY(i);
	//log(s:"Panel size: (",f:Panel_GetWidth(i), s:", ",f:Panel_GetHeight(i), s:")");
	int w = FixedDiv(Panel_GetWidth(i)-16.0, 8.0)>>16;
	int h = FixedDiv(Panel_GetHeight(i)-16.0, 8.0)>>16;
	//print(s:"w: ", d:w, s:", h: ", d:h);
	int xbtn = xpan;
	int ybtn = xpan;
	int xscroll = Panel_GetScrollX(i);
	int yscroll = Panel_GetScrollY(i);
	
	if(w != 0){
		for(int j = 0; j < w; j++){
			btnstring = StrAdd(btnstring, "B");
		}
	}
	btnstring = StrAdd(btnstring, "C");
	ybtn += 8.0;
	
	if(w != 0 && h != 0){
		for(int y = 0; y < h; y++){
		if	(y == 0) {
			//btnstring = StrAdd(btnstring, "\nJ");
			log(s:"Btn Pos: (",f:xbtn, s:", ",f:ybtn, s:")");
			Panel_MakeScrollButton(i, 0, xbtn, ybtn, 0, yscroll, "WP11");
		}   
		else if	(y == h-1){
			//btnstring = StrAdd(btnstring, "\nK");
			ybtn += 8.0*(h-1);
			log(s:"Btn Pos: (",f:xbtn, s:", ",f:ybtn, s:")");
			Panel_MakeScrollButton(i, 1, xbtn, ybtn,  0, -yscroll, "WP10");
		}
		btnstring = StrAdd(btnstring, "\nD");
			for(int x = 0; x < w; x++){
				btnstring = StrAdd(btnstring, "E");
			}
		btnstring = StrAdd(btnstring, "F");
		}
	}
	
	btnstring = StrAdd(btnstring, "\nG");
	ybtn += 8.0;
	if(w != 0){
		
		for(int k = 0; k < w; k++){
			if (k == 0){
				//btnstring = StrAdd(btnstring, "L");
				xbtn += 8.0;
				log(s:"Btn Pos: (",f:xbtn, s:", ",f:ybtn, s:")");
				Panel_MakeScrollButton(i, 2, xbtn, ybtn, xscroll, 0, "WP12");
			} 
			else if (k == w-1){
				//btnstring = StrAdd(btnstring, "M");
				xbtn += 8.0*(w-1);
				log(s:"Btn Pos: (",f:xbtn, s:", ",f:ybtn, s:")");
				Panel_MakeScrollButton(i, 3, xbtn, ybtn, -xscroll, 0, "WP13");
			}
			btnstring = StrAdd(btnstring, "H");
		}
	}
	btnstring = StrAdd(btnstring, "I");
	
	tmp[2] = btnstring;
	Panel_setBack(i, btnstring);
	Panel_UpdateScrollButtons(i);
	return btnstring;
}