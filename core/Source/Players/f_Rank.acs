///////////////////////////////////////////////////
//      SHOTGUN FRENZY
//   a mod by Wad'a'Holic

// f_Rank.acs
// rank and exp management
#library "f_Rank"

#import "f_IntDB.acs"
#import "f_Tip.acs"
#import "fl_techs.acs"
#include "zcommon.acs"
#include "acsutils.acs"

int rank_titles[20] = {
    "Rookie",  
    "Private", 
    "Specialist", 
    "Corporal", 
    "Sergeant", 
    "Lieutenant", 
    "Captain",
    "Major",
    "Colonel", 
    "Elite",
    "Private Elite",
    "Specialist Elite",
    "Corporal Elite",
    "Sergeant Elite", 
    "Lieutenant Elite",  
    "Captain Elite", 
    "Major Elite", 
    "Colonel Elite", 
    "Commander", 
    "Ultimate Commander",  
};

Script "SFPlus_DamagePerExperience" (int type, int arg1, int arg2) EVENT
{
    if(GetCVar("sfp_damageperexp") && type != GAMEEVENT_ACTOR_DAMAGED && PlayerCount() >= 3){terminate;}

    int play;
    SetActivator(0, AAPTR_Damage_Source);
		play = PlayerNumber();
	if(play == -1) terminate;
        int healthnow = GetActorProperty(0, APROP_HEALTH) - arg1;
        int damagedealt = arg1;
    if(healthnow < 0) damagedealt = arg1 - healthnow;

    int expbydmg = Rank_ExpByDmgFormula(play);
    p_DmgtoExp[play] += damagedealt;

    if(p_DmgtoExp[play] >= expbydmg){
        Rank_GrantEXP(play, 1);
        p_DmgtoExp[play] = 0;
    }
    /*
    log(s:"(DmgxExp) Effective damage= ", d:damagedealt);
    log(s:"(DmgxExp) Damage x Experience limit= ", d:Rank_ExpByDmgFormula(play));
    log(s:"(DmgxExp) Damaged caused by player= ", d:p_DmgtoExp[play]);
    */
}

// Results given by formula because float numbers are limited.
// Formula is: 100*sqrt(e^(rank + ksmilestone))

int rank_expbydmg[30] = {
    100, 164, 271, 448, 738,
    1218, 2008, 3311, 5459, 9001,
    14841, 24469, 40342, 66514, 109663, 
    180804, 298095, 491476, 810308, 1335972,
    2202646, 3631550, 5987414, 9871577, 16275479,
    26833728, 44241339, 72941636, 120260428, 198275926
};

function int Rank_ExpByDmgFormula(int play){
    return rank_expbydmg[p_Rank[play] + p_StreakLevel[play]];
}

function void Rank_GrantEXP(int who, int amount) {
	int exp_boost; // Hm...
    if(Tech_IsDone(TECH_MARCOM_VETMER) && p_Rank[playernumber()] < 4) amount *= 3;
    else if (Tech_IsDone(TECH_MARCOM_VETMER) && p_Rank[playernumber()] < 7) amount *= 2;
	p_Exp[who] += amount;

	if (p_Exp[who] > 0)
		for (int i = 0; i < INTDB_NUM_RANKS; i++){
            if (p_RankExp[i] <= p_Exp[who] && p_RankExp[i] >= p_RankExp[p_Rank[who]+1]){
                p_IncreaseRank();
                break;
            }
        }
		  
}

function void p_IncreaseRank (void)
{
    int play = playernumber();
    if (p_Rank[play] < INTDB_NUM_RANKS-2){
    // increase rank and give upgrade points
    int exp_cost = p_RankExp[p_Rank[play]+1];
    p_Rank[play] ++;
    p_Exp_Points[play] ++;
    p_Exp[play] = p_Exp[play] - exp_cost;
    
    SetCustomPlayerValue("RankCol", play, p_Rank[play]+1);
    ACS_NamedExecuteAlways("SFPlus_UpdateSaves", 0);

    // message
    //f_CenterTip("You have been promoted!");
    Spawn("RankUpFx", GetActorX(0), GetActorY(0), GetActorZ(0));
    
    sethudsize(800, 600, 0);
    setfont("MED01");
    hudmessage(s:"a"; 3, 0, cr_white, 0.1, 0.1, 3.0);
    setfont("BIGFONT");
    hudmessage(s:"You have been promoted!"; 2, 5, cr_Blue, 400.0, 464.1, 3.0);
    
    // play sound
    //localambientsound("p/promotion", 125);
    
    f_TipShow("PLAYER", "", strparam(s:"Has been promoted to ", s:rank_titles[p_Rank[playernumber()]] ,s:"!"), 10);

    switch(p_Rank[playernumber()])
    {
        case 1:
            HudMsg_Rank("+200 Credits\n+1 Upgrade point");
            p_Credits[playernumber()] += 200;
            break;
        case 2:
            HudMsg_Rank("+400 Credits\n+1 Upgrade point");
            p_Credits[playernumber()] += 400;
            break;
        case 3:
            HudMsg_Rank("+900 Credits\n+1 Upgrade point");
            p_Credits[playernumber()] += 900;
            break;
        case 4:
            p_IncreaseRank_Grant(1);
            HudMsg_Rank("+Dual Pistols\n+1600 Credits\n+1 Upgrade point");
            p_Credits[playernumber()] += 1600;
            break;
        case 5:
            HudMsg_Rank("+2500 Credits\n+1 Upgrade point");
            p_Credits[playernumber()] += 2500;
            break;
        case 6:
            HudMsg_Rank("+3600 Credits\n+1 Upgrade point");
            p_Credits[playernumber()] += 3600;
            break;
        case 7:
            p_IncreaseRank_Grant(2);
            HudMsg_Rank("+SMG Pistol\n+4900 Credits\n+1 Upgrade point");
            p_Credits[playernumber()] += 4900;
            break;
        case 8:
            HudMsg_Rank("+6400 Credits\n+1 Upgrade point");
            p_Credits[playernumber()] += 6400;
            break;
        case 9:
            HudMsg_Rank("+8100 Credits\n+1 Upgrade point");
            p_EliteMode[playernumber()] = true;
            f_CenterTip("\cjElite \cfAccess Granted!");
            p_Credits[playernumber()] += 8100;
            break;
        }
    }
}

function void HudMsg_Rank(str string){
    setfont("SMALLFNT");
    hudmessage(s:string; 2, 6, cr_Blue, 400.0, 480.1, 6.0);
}

function void p_IncreaseRank_Grant (int l_Type)
{
    switch(l_Type)
    {
        case 1:
			takeinventory("Beretta", 1);
            giveinventory("Beretta_Dual", 1);
            f_CenterTip("You have been awarded with\nDual Pistols!");
            break;
        case 2:
			takeinventory("Beretta_Dual", 1);
            giveinventory("Beretta_SMG", 1);
            f_CenterTip("You have been awarded with\na SMG");
            break;
    }
}  
