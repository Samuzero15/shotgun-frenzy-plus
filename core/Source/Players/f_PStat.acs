///////////////////////////////////////////////////
//      SHOTGUN FRENZY
//   a mod by Wad'a'Holic

// f_PStat.acs
// player status and respawning
#library "f_PStat"

#import "f_IntDB.acs"
#import "f_Tip.acs"
#import "f_Cred.acs"
#import "fl_Price.acs"
#import "f_PMenP.acs"
#import "fl_techs.acs"
#import "fl_wdir.acs"
#import "fp_credd.acs"
#import "fp_cmdevt.acs"

#include "zcommon.acs"
#include "acsutils.acs"
#include "samu_tools.acs"

str Stats_PlayerNames[32];

// player dies
script 52 DEATH
{

if (g_Status == 5){
	thing_hate(109, 104, 4);
	thing_hate(110, 104, 4);
	thing_hate(111, 104, 4);
	thing_hate(112, 104, 4);
	thing_hate(113, 104, 4);
	thing_hate(114, 104, 4);
}
else{
	//log(s:"Hating on sector: ", d:g_Status);
	thing_hate(109, 100+g_Status, 4);
	thing_hate(110, 100+g_Status, 4);
	thing_hate(111, 100+g_Status, 4);
	thing_hate(112, 100+g_Status, 4);
	thing_hate(113, 100+g_Status, 4);
	thing_hate(114, 100+g_Status, 4);
}

    // unmorph incase the player is in a tank
    //Unmorphactor(0, 1);
    hudmessage(s:""; 0, 201, 0, 400.0, 0.1, 1.0);
    
    // check tank status
	if(ACS_ExecuteWithResult(781, 0))
		{
			acs_executealways(309, 0); // Oh shit they got me.
		}
	else{
	// [Samu] Here, a saving weapon feature.
		int lastWeapon = Weapon_SaveLastUsed();
		p_LastWeap[playernumber()] = lastWeapon;
	}
    
    // increase death counter
    p_TotalPlayerDeaths ++;
    
    // display message
    if(PlayerCount() < 3){
		   Notify_Player_Death();
    }
	
	// a nice pretty message for the loser to make them think on the bright side
	// [Samu] Hm... I got a better idea.
	/*
	sethudsize(s_ScreenSize[0], s_ScreenSize[1], s_ScreenSize[2]);
	setfont("BCOUNT");
	hudmessage(s:"a"; 2, 180, CR_Gold, 0.1, 0.1, 5.0);
	//setfont("BIGFONT");
	//hudmessage(s:"You've been killed!"; 1, 178, CR_Red, 570.0, 370.1, 5.0);
	setfont("CONFONT");
	hudmessage(s:"You have been killed!\nThe bodycount sits at ", d:getlevelinfo(LEVELINFO_KILLED_MONSTERS), s:" dead monsters!\nTotal player deaths is sitting at ", d:p_TotalPlayerDeaths; 2, 179, CR_Gold, 580.0, 420.1, 1.0);
	*/

}

Script "CheckClientMute" OPEN CLIENTSIDE
{
    RequestScriptPuke(607,0,0,0);
}

Script 607 (void) NET
{
    //Check to see if they need to be muted
    int mutetime = GetCVar("sf_mutetime");
    if (mutetime) {
        str mute = strparam(s:"ignore_idx ", d:playernumber());
        mute = strparam(s:mute, s:" ");
        mute = strparam(s:mute, d:mutetime);
        ConsoleCommand(mute);
    }
}

// player enters game
script 53 ENTER
{
    //Clear potential bugs
    setactorproperty(0, APROP_SPEED, 1.0);
    setactorproperty(0, APROP_Health, 100);
	setactorproperty(0, APROP_DamageFactor, 1.0);
    setplayerproperty(0, 0, PROP_TOTALLYFROZEN);
    ACS_ExecuteAlways(106,0,3,1,1);
    p_MenuSelection_Active[playernumber()] = 0;
    
    
    // give credits and post global tip
    // mmm: bonus from level time

    //t3h: This is too aggressive -- players are speccing / rejoining because
    //     they earn more credits over time than by playing.
    //     So, let's add a "corrective" console variable
    //     This way, if the cvar is unset, we behave as always. But it can be made positive, to increase the rate, or negative, to decrease the rate.

    //t3h: Trying again. Just adding the cvar wasn't enough. Now, just don't give money unless they're really new (no frags)
    //     Also, there's a bug where sometimes players don't start at rank 0.

    // clear any previous items
	TakeInventory("PlayerKilledDemon", 0xFFFFFFF);
	TakeInventory("P_Backpack", 0xFFFFFFF);
	// Just in case
	ACS_NamedExecuteAlways("PersonalBP_Reset", 0);
    clearinventory();

    // give the player his/her boomstick and beretta
    giveinventory("Beretta", 1);
    giveinventory("Boomstick", 1);
    setweapon("Boomstick");

    int creditmult = GetCVar("sf_creditcorrection");
	
	int play = playernumber();
    p_Credits[play] = cond(g_Status != 0,
							f_Startcash + (10 + creditmult) * Timer() / 35,
							f_Startcash);
    if (p_Credits[play] > 20000)
    {
      p_Credits[play] = 20000;
    }
	p_dropCredits_Starter[play] = p_Credits[play];
	
    p_Rank[play] = 0;
    p_Exp_Points[play] = 0;
    p_Exp[play] = 0;
    p_KickSlot[play] = 0;
    p_TankSlot[play] = 0;
	p_StreakLevel[play] = 0;
	p_Mechtimer[play] = -2;
	
    Notify_Player_Enter();
	
	//Save the name for disconnect cases.
	Stats_PlayerNames[playernumber()] = strparam(n:0);
	
    // perform respawn actions
    acs_executealways(54, 0, 0, 0, 0);
}

// player respawns
script 54 RESPAWN
{
	
    // give players the 'player' tid
    thing_changetid(0, 990+PlayerNumber());
	// Set the max health upon spawn.
	
	setactorproperty(0, APROP_SpawnHealth, getActorProperty(0, AProp_Health));
	
	ACS_NamedExecute("Auto-Use Items", 0);
    p_Streak[playernumber()] = 0; // Reset the kill-streak.
    
    // take boomer cannon incase it glitched
    takeinventory("BoomerCannon", 1);
	
	// [Samu] In case of tank past life.
	takeinventory("TankGun", 1);
	takeinventory("TankGunAdvanced", 1);
	
	TakeInventory("BasicArmor", 0xFFFFFFF);
  
    
    // give inventory on respawn
	delay(5);
	ACS_Execute(56,0,p_LastWeap[playernumber()],0,0);
    delay(10);
	// Recharge prometeo protocol.
	if(CheckInventory("PlayUp_Prometeo_Used"))
		takeinventory("PlayUp_Prometeo_Used", 1);
	if(CheckInventory("PlayUp_SpawnShield")) 
		giveinventory("PlayUp_SpawnShield_Activate", 1);
		
    acs_execute(106, 0, 1, 1, 0);
    delay(15);
    //t3hdoom: Originally didn't give any ammo if there's no armory. Let's give them ammo on spawn regardless
    acs_execute(106, 0, 3, 1, 0);
	
	//[Samu] If you die, the teleport script will not activate the spawnshield effet.
	// So to counter that, I should place it here.
	if(CheckInventory("PlayUp_SpawnShield")){
		TakeInventory("PlayUp_SpawnShield_Power", 1);
		delay(1);
		giveInventory("PlayUp_SpawnShield_Activate", 1);
	}
}

script "SFPlus_NameUpdate" ENTER {
	// If this boi decides to change the name.
	while(PlayerInGame(PlayerNumber())){
		if(StrCmp(Stats_PlayerNames[Playernumber()], strparam(n:0)) != 0){
			Stats_PlayerNames[Playernumber()] = strparam(n:0);
		}
		delay(1);
	}
}

script 57 (int num) DISCONNECT 
{
	//PrintBold(d:ActivatorTID());
	//PrintBold(d:ACS_ExecuteWithResult(599,0,0,0));
	//PrintBold(d:ThingCountName("TurretHeadBase", 0));
	//PrintBold(d:ThingCountName("TurretFlame_Head", 0));
	
	// Clean things leftovers by player.
	Thing_Damage(990+num, 90000000, MOD_UNKNOWN);
	Thing_Remove(990+num);
	
	Thing_Damage(11000+num, 90000000, MOD_UNKNOWN);
	Thing_Remove(11000+num);
	
	Thing_Damage(10000+num, 90000000, MOD_UNKNOWN);
	Thing_Remove(10000+num);
	
	Thing_Damage(15000+num, 90000000, MOD_UNKNOWN);
	Thing_Remove(15000+num);
	
	Thing_Damage(30000+num, 90000000, MOD_UNKNOWN);
	Thing_Remove(30000+num);
	// Unfreeze the player, to avoid bugs.
	SetPlayerProperty(0, 0, PROP_TOTALLYFROZEN);
	SetPlayerProperty(0, 0, PROP_FROZEN);
	//log(s:"Last commander: ", n:c_Commander+1);
	if(c_Commander == num){
		SetPlayerProperty(0, 1, PROP_TOTALLYFROZEN);
		SetPlayerProperty(0, 0, PROP_TOTALLYFROZEN);
		// [Samu] Bug: Spectating from commander will cause a freeze on the player.
		Thing_Remove(3568); // Remove the spawn blocker at least.
		c_Commander = 99;
	}
	
	if (Stats_PlayerNames[num] != 0){ // Check if the player has joined to the game at least once.
		 Notify_Player_Leave(Stats_PlayerNames[num]);
	}
	Stats_PlayerNames[num] = 0;
}

script 599 (void)
{
	int count = 0;
	count = count + ThingCountName("TurretBullet_Head", 0);
	count = count + ThingCountName("TurretFlame_Head", 0);
	count = count + ThingCountName("TurretPlasma_Head", 0);
	count = count + ThingCountName("TurretRocket_Head", 0);
	count = count + ThingCountName("TurretTesla_Head", 0);
	SetResultValue(count);
}

script 56 (int weapon) //Set weapon to last (separated this script from the respawn one because it's going to be executed through Decorate too)
{
	if(weapon == 0) weapon = p_LastWeap[playernumber()]; //If executed from Decorate

	Weapon_LoadLastUsed(weapon);

}

int auto_research[20][2];
int stack = 0;
//int acs_level = 0;
function void AutoResearch_Add(int event_id, int kills_needed){
	auto_research[stack][0] = event_id;
	auto_research[stack][1] = kills_needed;
	stack++;
}

function void AutoResearch_DoIt(int level){
	CmdEvent_Execute(auto_research[level][0], 0);
}

function bool AutoResearch_Check(int level){
	return getlevelinfo(LEVELINFO_KILLED_MONSTERS) > auto_research[level][1];
}
/*
script "SFPlus_AutoResearchLevel" (int lvl){
	acs_level = lvl;
}*/

script 249 OPEN
{
	delay(35*3);
	CmdEvent_Initialize();
	AutoResearch_Add(CMDEVT_REWARD_HEALTH1, 100);
	AutoResearch_Add(CMDEVT_REWARD_STIMPK, 	250);
	AutoResearch_Add(CMDEVT_REWARD_ARMOR, 	500);
	AutoResearch_Add(CMDEVT_REWARD_AMMOBP, 	800);
	AutoResearch_Add(CMDEVT_REWARD_HEALTH2, 1200);
	AutoResearch_Add(CMDEVT_REWARD_BOOMRN, 	1700);
	AutoResearch_Add(CMDEVT_REWARD_PUMP1, 	2300);
	AutoResearch_Add(CMDEVT_REWARD_SPEEDB, 	3000);
	AutoResearch_Add(CMDEVT_REWARD_RESISB, 	3800);
	AutoResearch_Add(CMDEVT_REWARD_REGENB, 	4700);
	AutoResearch_Add(CMDEVT_REWARD_PUMP2, 	5600);
			
	// make sure it is not a commander gametype
	if (f_UseScoreSystem == 1){
		int l_CurrentLevel;
		//int acs_last = acs_level;
		while(1){
			// display
			setfont("CONFONT");
			sethudsize(s_ScreenSize[0], s_ScreenSize[1], s_ScreenSize[2]);
			hudmessage(s:"Team score: ",d:getlevelinfo(LEVELINFO_KILLED_MONSTERS); 1, 256, CR_TAN, 400.0, 20.1, 7.0);
			/*
			if(acs_level != acs_last){ // Debuggy
				l_currentlevel = acs_level;
				acs_last = acs_level;
			}*/
			
			if(AutoResearch_Check(l_CurrentLevel)){
				AutoResearch_DoIt(l_CurrentLevel);
				l_CurrentLevel++;
			}
			
			if(l_CurrentLevel == stack) break;
			
			
			delay(35*5);
		}
	}
}


// remove if wrong gametype
script 769 (void){
	SetResultValue(f_Gametype);
}

// [Samu] Check if i am on a tank.
script 781 (void){
	SetResultValue(checkinventory("GotTank") || ((CheckInventory("TankGun") || CheckInventory("TankGunAdvanced")) && p_TankSlot[playernumber()] == 2));
}
