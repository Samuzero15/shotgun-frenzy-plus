///////////////////////////////////////////////////
//      SHOTGUN FRENZY
//   a mod by Wad'a'Holic

// f_StokP.acs
// health and ammo stock pad setup

#library "f_StokP"

#import "f_IntDB.acs"
#import "fl_techs.acs"
#import "f_Tip.acs"

#include "zcommon.acs"
#include "acsutils.acs"
#include "samu_tools.acs"

// a player walks onto a stock pad
script 106 (int l_Type, int l_Give, int l_DisableFlash)
{//Healing and stocking pads
	
	if(l_type > 0) ACS_NamedExecute("SFPlus_ShowUpgrades", 0); // All the reminders wrapped up

    if ((l_Type == 1 && Tech_IsDone(TECH_GENLAB)) || (l_Type == 4))
    {
		
		if(p_TankSlot[playernumber()] == 2) //Preventing exploding tanks
		{
			Print(s:"Exit the tank (Mech Factory or hold Use for 1 second) to heal it!");
			terminate;
		}

		int hp = getActorProperty(0, APROP_Health);
		bool bodyEnhance = CheckInventory("PlayUp_BodyEnhancement");
		bool health1 = Tech_IsDone(TECH_GENLAB_HEALTH1);
		bool health2 = Tech_IsDone(TECH_GENLAB_HEALTH2);
		bool adrenaline = Tech_IsDone(TECH_GENLAB_SPEEDB);
		bool resister = Tech_IsDone(TECH_GENLAB_RESISB);
		bool armorTech = Tech_IsDone(TECH_GENLAB_ARMOR);
		
		int healthboost = 20*health1 + 20*health2 + 60*bodyEnhance;
		int speedboost = 0.3*adrenaline + 0.2*bodyEnhance;
		int defenseboost = 0.3*resister + 0.2*bodyEnhance;
		
		if (l_DisableFlash == 0){
		 f_CenterTip("Health Restocked!");
		 fadeto(0, 0, 255, 0.5, 0.0);
		 localambientsound("p/heal", 128);
		}		
		
		// check for genetics lab updates
		if (l_Give > 0)
		{
			if (armorTech){
				// [Samu] Preventing Armor Overrides.
				if(CheckInventory("BasicArmor") == 0) {takeInventory("Flag_BlueArmor", 1);}
				
				if(CheckInventory("BasicArmor") < 100 && !CheckInventory("Flag_BlueArmor")){
					giveinventory("SteelPlatingArmor", 1);
				}
				// steel plating
			}
			//if (Tech_IsDone(TECH_GENLAB_RESISB))        giveinventory("Upgrade_Resister", 1);       // resister unit
			if (Tech_IsDone(TECH_GENLAB_REGENB))        giveinventory("Upgrade_Asorber", 1);  // asorber unit
			if (CheckInventory("PlayUp_Prometeo")) 
				ACS_NamedExecuteAlways("SFPlus_PrometeoScript", 0);
			
			
			/*
			log(s:"============================");
			log(s:"Before...\n",
			s:"MaxHP: ", d:getActorProperty(0,APROP_SpawnHealth), s:"\n",
			s:"Speed Factor: ", f:getActorProperty(0,APROP_Speed),s:"\n",
			s:"Damage Factor:", f:getActorProperty(0,APROP_DamageFactor));
			*/
			
			// Do all calculations depending on the researches and unit upgrades.
			//Apply da techs!
			setactorproperty(0, APROP_SPEED, 1.0 + speedboost);  		
			
			setactorproperty(0, APROP_SpawnHealth, 100 + healthboost);
			giveinventory("Health", 100 + healthboost);
			SetInventory("HUD_MaxHp", 1 + 1*health1 + 1*health2 + 3*bodyEnhance);
			
			setactorproperty(0, APROP_DamageFactor,1.0 - defenseboost);
		}
		
		delay(2);
		if (l_DisableFlash == 0)		fadeto(0, 0, 255, 0.0, 0.2);
    }
    
    // ammo stock pad
    if ((l_Type == 2 && Tech_IsDone(TECH_ARMORY)) || (l_Type == 3))
    {
		
		if(p_TankSlot[playernumber()] == 2) //Preventing exploding tanks
		{
			Print(s:"You can't use these in a tank!");
			terminate;
		}
	
		//[Samu] *sigh* It's time to refactor deez sheet.
			//Bullets
			if(l_Give > 0){
			 if (Tech_IsDone(TECH_ARMORY_STIMPK))
				GiveInventory("Inventory_Stimpack", 10);
			 if (Tech_IsDone(TECH_ARMORY_BOOMRN)) 
				giveinventory("Upgrade_Boomshot", 1);   // boomshot rounds
             if (Tech_IsDone(TECH_ARMORY_AMMOBP)) 
				giveinventory("Backpack", 1);           // backpack
			
			 if(checkInventory("p_backpack")){ // Grants it some more ammo.
				ACS_NamedExecuteAlways("PersonalBP_Expand", 0);
			 }
			 delay(1);
			 Stock_FillAmmo(1);
			 
			}
			
            if (l_DisableFlash == 0){
			 f_CenterTip("Ammo Restocked!");
             fadeto(0, 255, 0, 0.5, 0.0);
             localambientsound("p/deploy", 128);
			}
            
            delay(2);
            if (l_DisableFlash == 0)		fadeto(0, 255, 0, 0.0, 0.2);
    }
	
	//[Samu] A customized refresh-me-up.
	if(l_Type == -1){
		// Do Heal and Stock the player, but do not give the latest techs.
		
		giveinventory("Health", 100 + healthboost);
		
		Stock_FillAmmo(1);
			 
		if (l_DisableFlash == 0){
			 f_CenterTip("You feel good as new!");
             fadeto(255, 255, 255, 0.5, 0.0);
			 localambientsound("p/heal", 128);
             localambientsound("p/deploy", 128);
			}
		delay(2);
		if (l_DisableFlash == 0)	fadeto(255, 255, 255, 0.0, 0.2);
	}
	
	//Ammo dispenser's stocker.
	// A nerfed version of the dispenser, 
	// just to prevent the full stocking on anywhere of the map.
	if(l_type == -2){
		if(p_TankSlot[playernumber()] == 2) //Preventing exploding tanks
		{
			Print(s:"You can't use these in a tank!");
			terminate;
		}
		Stock_FillAmmo(0);
			 
		if (l_DisableFlash == 0){
			 f_SmallTip ("Reloading...", cr_orange, 0);
             fadeto(0, 255, 0, 0.3, 0.0);
			 localambientsound("g/reloading", 64);
			}
		delay(2);
		if (l_DisableFlash == 0)	fadeto(0, 255, 0, 0.0, 0.1);
	}
	// Health dispenser's stocker.
	// A nerfed version of the dispenser, 
	// just to prevent the full healing on anywhere of the map.
	if(l_type == -3){
		if(p_TankSlot[playernumber()] == 2) //Preventing exploding tanks
		{
			Print(s:"You can't use these in a tank!");
			terminate;
		}
		// [Samu] Expansive re-stock and armor repair upgrades for the players.
		int health_give = 10 + 15*CheckInventory("PlayUp_ExpansiveRestock");
		int armor = Checkinventory("BasicArmor");
		int armor_max = 100 + 100*CheckInventory("Flag_BlueArmor");
		int armor_left = armor_max - armor;
		int armor_give = 5 + 15*CheckInventory("PlayUp_ArmorRepair") + 30*CheckInventory("PlayUp_ExpansiveRestock");
		giveinventory("Health", health_give);
			
		if(armor == 0 && armorTech && CheckInventory("PlayUp_ArmorRepair")){
			takeInventory("Flag_BlueArmor", 1);
			GiveInventory("SteelPlatingArmor", 1);
			f_SmallTip ("Green Armor Granted!", cr_green, 3);
		}
		else if (armor < armor_max){
			if(armor_left < armor_give) armor_give = armor_left;
			giveinventory("ArmorBonus", armor_give);
			f_SmallTip ("Repairing...", cr_green, 3);
		}
		
		
		if (l_DisableFlash == 0){
			 f_SmallTip ("Healing...", cr_cyan, 1);
             fadeto(0, 0, 255, 0.3, 0.0);
			 localambientsound("g/healing", 64);
			}
		delay(2);
		if (l_DisableFlash == 0)	fadeto(0, 0, 255, 0.0, 0.1);
	}
	
}  
#Define STOCK_AMMOTYPES 14
int stock_ammo_stack;
int stock_ammo[STOCK_AMMOTYPES][2];

function void Stock_DefineAmmo(str name, int disp_give){
	stock_ammo[stock_ammo_stack][0] = name;
	stock_ammo[stock_ammo_stack][1] = disp_give;
	stock_ammo_stack++;
}

function void Stock_FillAmmo(bool full){
	int i;
	stock_ammo_stack = 0;
	Stock_DefineAmmo("Clip", 				10);
	Stock_DefineAmmo("ClipPistol",			10);
	Stock_DefineAmmo("Shell",				2);
	Stock_DefineAmmo("RocketAmmo",			2);
	Stock_DefineAmmo("GLauncher_Ammo",		2);
	Stock_DefineAmmo("Flinger_Ammo",		2); 
	Stock_DefineAmmo("Cell",				20); 
	Stock_DefineAmmo("Dualshot_Ammo",		15); 
	Stock_DefineAmmo("HeavyBullets",		40); 
	Stock_DefineAmmo("Gasoline",			20);
	Stock_DefineAmmo("Supplies_Health",		0); 
	Stock_DefineAmmo("Supplies_Ammo",		0); 
	Stock_DefineAmmo("Supplies_Health_Big",	0); 
	Stock_DefineAmmo("Supplies_Ammo_big",	0);
	
	if(full){
		for(i = 0; i < STOCK_AMMOTYPES; i++){
			GiveMaxInventory(stock_ammo[i][0]);
		}
	}
	else{
		// [Samu] Expansive re-stock upgrades for the players.
		int extra_ammo = CheckInventory("PlayUp_ExpansiveRestock");
		
		for(i = 0; i < STOCK_AMMOTYPES; i++){
			GiveInventory(stock_ammo[i][0], stock_ammo[i][1]*(1 + extra_ammo));
			//print(d:(stock_ammo[i][1]));
		}
	}
}

script "SFPlus_ShowUpgrades" (void) CLIENTSIDE{
	int s = 0;
	int c = 0;
	
	c = CR_CYAN;
	if (Tech_IsDoneC(TECH_GENLAB_HEALTH2))
		s = Stock_RemindUpgrade(s, c,true, "+ HUMAN GENETICS P2");
	else if (Tech_IsDoneC(TECH_GENLAB_HEALTH1)) 
		s = Stock_RemindUpgrade(s, c,true, "+ HUMAN GENETICS P1");
		
	s = Stock_RemindUpgrade(s, c,Tech_IsDoneC(TECH_GENLAB_SPEEDB), "+ Adrenaline");
	s = Stock_RemindUpgrade(s, c,Tech_IsDoneC(TECH_GENLAB_ARMOR), "+ Steel Plating");
	s = Stock_RemindUpgrade(s, c,Tech_IsDoneC(TECH_GENLAB_RESISB), "+ Resister Unit");
	s = Stock_RemindUpgrade(s, c,Tech_IsDoneC(TECH_GENLAB_REGENB), "+ Absorber Unit");
	
	c = CR_BLUE;
	s++;
	s = Stock_RemindUpgrade(s, c,CheckInventory("PlayUp_BodyEnhancement"), 	"+ Body Enhancement");
	s = Stock_RemindUpgrade(s, c,CheckInventory("PlayUp_ExpansiveRestock"), "+ Expansive Restock");
	s = Stock_RemindUpgrade(s, c,CheckInventory("PlayUp_ArmorRepair"), 		"+ Armor Repair");
	s = Stock_RemindUpgrade(s, c,CheckInventory("PlayUp_PowUpUpgrade"), 	"+ Powerup Upgrade");
	s = Stock_RemindUpgrade(s, c,CheckInventory("PlayUp_MechSync"), 		"+ Mech Syncronize");
	s = Stock_RemindUpgrade(s, c,CheckInventory("PlayUp_SpawnShield"), 		"+ Spawn Shield");
	s = Stock_RemindUpgrade(s, c,CheckInventory("PlayUp_HealCoins"), 		"+ Healing Coins");
	s = Stock_RemindUpgrade(s, c,CheckInventory("PlayUp_Prometeo"), 		"+ Prometeo Protocol");
	s = Stock_RemindUpgrade(s, c,CheckInventory("PlayUp_RuneSync"), 		"+ Rune Syncronize");

	c = CR_GREEN;
	s++;
	if (Tech_IsDoneC(TECH_ARMORY_ARMS2)) {
		s = Stock_RemindUpgrade(s, c,true, "+ Arms P2");
	}
	else if (Tech_IsDoneC(TECH_ARMORY_ARMS1)) {
		s = Stock_RemindUpgrade(s, c,true, "+ Arms P1");
	}
	s = Stock_RemindUpgrade(s, c,Tech_IsDoneC(TECH_ARMORY_BOOMRN), "+ Boomshot Rounds");
	s = Stock_RemindUpgrade(s, c,Tech_IsDoneC(TECH_ARMORY_AMMOBP), "+ Ammo Backpack");
	c = CR_DARKGREEN;
	s = Stock_RemindUpgrade(s, c,Tech_IsDoneC(TECH_ARMORY_STIMPK), "+ 10 Stimpacks");
	s = Stock_RemindUpgrade(s, c,
		CheckInventory("P_Backpack"), "+ Personal Backpack");
	
	c = CR_GOLD;
	s++;
	s = Stock_RemindUpgrade(s, c, Tech_IsDoneC(TECH_REFINE_PUMP1), "+ Processing Phase 1");
	s = Stock_RemindUpgrade(s, c, Tech_IsDoneC(TECH_REFINE_PUMP2), "+ Processing Phase 2");
}

function int Stock_RemindUpgrade(int show_stack, int show_color, bool condition, str string){
	if(condition){
		int offset = show_stack * 10.0;
		int x = 600.1;
		int y = 100.1 + offset;
		int shineFlags = HUDMSG_FADEINOUT | HUDMSG_AddBlend;
		
		sethudsize(800, 600, s_ScreenSize[2]);
		setfont("CONFONT");
		hudmessage(s:string; shineFlags, 0, show_color, x, y, 0.2,0.15,0.15);
		hudmessage(s:string; HUDMSG_FADEOUT, 2020 + show_stack, show_color, x, y, 3.0, 0.5);
		return show_stack+1;
	}
	else {
		setfont("CONFONT");
		hudmessage(s:""; 0, 2020 + show_stack, show_color, 600.1, 100.1 + offset, 2.0);
	}
	return show_stack;
}

script "SFPlus_PrometeoScript" (void)
{
// Prometeo Script, Saves your life once (recharged at respawning).
// In addition, grants you a short invul and a x2 Damage multipler.
// If you use a berserk here, you could get a x6 Damage multipler!
// Even better, you could get a x12 Damage multipler if you upgrade the powerups!
// This shit is overpowered when used on the right time!
	if(CheckInventory("PlayUp_Prometeo_Used") ) Terminate;
	if(!CheckInventory("PlayUp_Prometeo_Ready")){
		GiveInventory("PlayUp_Prometeo_Ready", 1);
	} else terminate;
	SetPlayerProperty(0, 1, PROP_BUDDHA);
	while(GetActorProperty(0, APROP_Health) > 1){
		// Wait until the player is pinned down
		delay(1);
	} 
	SetPlayerProperty(0, 0, PROP_BUDDHA);
	GiveInventory("PlayUp_Prometeo_Used", 1);
	TakeInventory("PlayUp_Prometeo_Ready", 1);
	giveinventory("PlayUp_Prometeo_Activate", 1);
}