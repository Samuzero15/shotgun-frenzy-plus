///////////////////////////////////////////////////
//      SHOTGUN FRENZY
//   a mod by Wad'a'Holic

// f_StokP.acs
// health and ammo stock pad setup

#library "f_StokP"

#import "f_IntDB.acs"
#import "fl_techs.acs"
#import "f_Tip.acs"
#import "fl_Wdir.acs"
#import "fl_StUpDir.acs"
#import "fl_MupDir.acs"
#import "fl_PupDir.acs"

#include "zcommon.acs"
#include "acsutils.acs"
#include "samu_tools.acs"

#libdefine RESTOCK_HEALTHPAD 1
#libdefine RESTOCK_AMMOPAD 2
#libdefine RESTOCK_HEALTH 4
#libdefine RESTOCK_AMMO 3
#libdefine RESTOCK_HEALTHDISP -3
#libdefine RESTOCK_AMMODISP -2
#libdefine RESTOCK_REFRESH -1
#libdefine RESTOCK_AMMOSPHERE -4

// a player walks onto a stock pad
script 106 (int l_Type, int l_Give, int l_DisableFlash)
{//Healing and stocking pads
	
	if(l_type > 0) ACS_NamedExecute("SFPlus_ShowUpgrades", 0); // All the reminders wrapped up
	
	int hp = getActorProperty(0, APROP_Health);
	bool bodyEnhance = PlayUp_CheckInv("PlayUp_BodyEnhancement");
	bool health1 = Tech_IsDone(TECH_GENLAB_HEALTH1);
	bool health2 = Tech_IsDone(TECH_GENLAB_HEALTH2);
	bool adrenaline = Tech_IsDone(TECH_GENLAB_SPEEDB);
	bool resister = Tech_IsDone(TECH_GENLAB_RESISB);
	bool armorTech = Tech_IsDone(TECH_GENLAB_ARMOR);
    if ((l_Type == RESTOCK_HEALTHPAD && Tech_IsDone(TECH_GENLAB)) || (l_Type == RESTOCK_HEALTH))
    {
		
		if(p_TankSlot[playernumber()] == 2) //Preventing exploding tanks
		{
			Print(s:"Exit the tank (Mech Factory or hold Use for 1 second) to heal it!");
			terminate;
		}

		int healthboost = 20*health1 + 20*health2 + 60*bodyEnhance;
		int speedboost = 0.3*adrenaline + 0.2*bodyEnhance;
		int defenseboost = 0.3*resister + 0.2*bodyEnhance;
		
		if (l_DisableFlash == 0){
		 f_CenterTip("Health Restocked!");
		 fadeto(0, 0, 255, 0.5, 0.0);
		 localambientsound("p/heal", 128);
		}		
		
		// check for genetics lab updates
		if (l_Give > 0)
		{
			if (armorTech){
				// [Samu] Preventing Armor Overrides.
				if(CheckInventory("BasicArmor") == 0) {takeInventory("Flag_BlueArmor", 1);takeInventory("Flag_RedArmor", 1);}
				
				if(CheckInventory("BasicArmor") < 100 && !(CheckInventory("Flag_BlueArmor") || CheckInventory("Flag_RedArmor"))){
					giveinventory("SteelPlatingArmor_Pickup", 1);
				}else if (CheckInventory("BasicArmor") >= 100 && !(CheckInventory("Flag_BlueArmor") || CheckInventory("Flag_RedArmor"))){
					ACS_NamedExecuteAlways("Armor_ReplaceShardsWithArmor", 0, 0);
				}
				// log(s:"Basic armor: ", d:CheckInventory("BasicArmor"));
				// steel plating
			}
			if (Tech_IsDone(TECH_GENLAB_REGENB))        giveinventory("Upgrade_Asorber", 1);  // asorber unit
			
			
			/*
			log(s:"============================");
			log(s:"Before...\n",
			s:"MaxHP: ", d:getActorProperty(0,APROP_SpawnHealth), s:"\n",
			s:"Speed Factor: ", f:getActorProperty(0,APROP_Speed),s:"\n",
			s:"Damage Factor:", f:getActorProperty(0,APROP_DamageFactor));
			*/

			int stimul = StimUp_CheckInv("StimUp_Stimulant");
			int coffie = StimUp_CheckInv("StimUp_Caffeine");
			
			// Do all calculations depending on the researches and unit upgrades.
			//Apply da techs!
			setactorproperty(0, APROP_SPEED, 1.0 + speedboost + 0.1*coffie*CheckInventory("StimUp_CaffeinePower"));  		
			
			
			setactorproperty(0, APROP_SpawnHealth, 
				100 + healthboost + 20*stimul*CheckInventory("StimUp_StimulantPower") + 100*CheckInventory("RuneHealth"));
			giveinventory("Health", 
				100 + healthboost + 20*stimul*CheckInventory("StimUp_StimulantPower") + 100*CheckInventory("RuneHealth"));
			
			
			SetInventory("HUD_MaxHp", 1 + 1*health1 + 1*health2 + 3*bodyEnhance);
			
			setactorproperty(0, APROP_DamageFactor,1.0 - defenseboost);
		}
		
		delay(2);
		if (l_DisableFlash == 0)		fadeto(0, 0, 255, 0.0, 0.2);
    }
    
    // ammo stock pad
    if ((l_Type == RESTOCK_AMMOPAD && Tech_IsDone(TECH_ARMORY)) || (l_Type == RESTOCK_AMMO))
    {
		
		if(p_TankSlot[playernumber()] == 2) //Preventing exploding tanks
		{
			Print(s:"You can't use these in a tank!");
			terminate;
		}
	
		//[Samu] *sigh* It's time to refactor deez sheet.
			//Bullets
			if(l_Give > 0){
			 if (Tech_IsDone(TECH_ARMORY_GNADES))
				GiveInventory("HandGrenade_Ammo", 5);
			 if (Tech_IsDone(TECH_GENLAB_STIMPK))
				SetInventory("Inventory_Stimpack", 5 + StimUp_CheckInv("StimUp_Amount"));
			 if (Tech_IsDone(TECH_ARMORY_BOOMRN)) 
				giveinventory("Upgrade_Boomshot", 1);   // boomshot rounds
             if (Tech_IsDone(TECH_ARMORY_AMMOBP)) 
				giveinventory("G_Backpack", 1);           // backpack
			
			 ACS_NamedExecuteAlways("Ammo_UpdateCaps", 0);
			 
			 delay(1);
			 Stock_FillAmmo(1);
			 delay(1);
			 Stock_FillAmmo(1);
			}
			
            if (l_DisableFlash == 0){
			 f_CenterTip("Ammo Restocked!");
             fadeto(0, 255, 0, 0.5, 0.0);
             localambientsound("p/deploy", 128);
			}
            
            delay(2);
            if (l_DisableFlash == 0)		fadeto(0, 255, 0, 0.0, 0.2);
    }
	
	//[Samu] A customized refresh-me-up.
	if(l_Type == RESTOCK_REFRESH){
		// Do Heal and Stock the player, but do not give the latest techs.
		
		giveinventory("Health", 100 + healthboost);
		
		Stock_FillAmmo(1);
			 
		if (l_DisableFlash == 0){
			 f_CenterTip("You feel good as new!");
             fadeto(255, 255, 255, 0.5, 0.0);
			 localambientsound("p/heal", 128);
             localambientsound("p/deploy", 128);
			}
		delay(2);
		if (l_DisableFlash == 0)	fadeto(255, 255, 255, 0.0, 0.2);
	}
	
	//Ammo dispenser's stocker.
	// A nerfed version of the dispenser, 
	// just to prevent the full stocking on anywhere of the map.
	if(l_type == RESTOCK_AMMODISP){
		if(p_TankSlot[playernumber()] == 2) //Preventing exploding tanks
		{
			Print(s:"You can't use these in a tank!");
			terminate;
		}
		Stock_FillAmmo(0);
			 
		if (l_DisableFlash == 0){
			 f_SmallTip ("Reloading...", cr_orange, 0);
             fadeto(0, 255, 0, 0.3, 0.0);
			 localambientsound("g/reloading", 64);
			}
		delay(2);
		if (l_DisableFlash == 0)	fadeto(0, 255, 0, 0.0, 0.1);
	}
	// Health dispenser's stocker.
	// A nerfed version of the dispenser, 
	// just to prevent the full healing on anywhere of the map.
	if(l_type == RESTOCK_HEALTHDISP){
		if(p_TankSlot[playernumber()] == 2) //Preventing exploding tanks
		{
			Print(s:"You can't use these in a tank!");
			terminate;
		}
		// [Samu] Expansive re-stock and armor repair upgrades for the players.
		int health_give = 10 + 15*PlayUp_CheckInv("PlayUp_ExpansiveRestock");
		int armor = Checkinventory("BasicArmor");
		int armor_max = 100 + 100*CheckInventory("Flag_BlueArmor") + 200*CheckInventory("Flag_RedArmor");
		int armor_left = armor_max - armor;
		int armor_give = 5 + 15*PlayUp_CheckInv("PlayUp_ArmorRepair") + 30*PlayUp_CheckInv("PlayUp_ExpansiveRestock");
		giveinventory("Health", health_give);
			
		if(armor == 0 && armorTech && PlayUp_CheckInv("PlayUp_ArmorRepair")){
			takeInventory("Flag_RedArmor", 1);
			takeInventory("Flag_BlueArmor", 1);
			GiveInventory("SteelPlatingArmor", 1);
			f_SmallTip ("Green Armor Granted!", cr_green, 3);
		}
		else if (armor < armor_max){
			if(armor_left < armor_give) armor_give = armor_left;
			giveinventory("ArmorBonus", armor_give);
			f_SmallTip ("Repairing...", cr_green, 3);
		}
		
		
		if (l_DisableFlash == 0){
			 f_SmallTip ("Healing...", cr_cyan, 1);
             fadeto(0, 0, 255, 0.3, 0.0);
			 localambientsound("g/healing", 64);
			}
		delay(2);
		if (l_DisableFlash == 0)	fadeto(0, 0, 255, 0.0, 0.1);
	}
	
	// Ammosphere's Restock.
	// Does not updates your techs.
	if(l_type == RESTOCK_AMMOSPHERE){
		if(p_TankSlot[playernumber()] == 2) //Preventing exploding tanks
		{
			Print(s:"You can't use these in a tank!");
			terminate;
		}
		Stock_FillAmmo(1);
			 
		if (l_DisableFlash == 0){
			 f_CenterTip("Ammo Restocked!");
             fadeto(0, 255, 0, 0.3, 0.0);
			 localambientsound("g/reloading", 64);
			}
		delay(2);
		if (l_DisableFlash == 0)	fadeto(0, 255, 0, 0.0, 0.1);
	}
}  

function void Stock_FillAmmo(bool full){
	int i = 0;
	str ammo_name = "";
	int ammo_dispensergive = 0;
	if(full){
		for (i = 0; i < fp_AmmoDefStack; i++) {
			ammo_name = fp_AmmoList[i][FP_AMMODEF_NAME];
			GiveMaxInventory(ammo_name);
		}
	}
	else{
		// [Samu] Expansive re-stock upgrades for the players.
		int extra_ammo = (1 + PlayUp_CheckInv("PlayUp_ExpansiveRestock"));
		for (i = 0; i < fp_AmmoDefStack; i++) {
			ammo_name = fp_AmmoList[i][FP_AMMODEF_NAME];
			ammo_dispensergive = fp_AmmoList[i][FP_AMMODEF_DISPENSERGIVE];
			GiveInventory(ammo_name, ammo_dispensergive*extra_ammo);
		}
	}
}

script "SFPlus_ShowUpgrades" (void) CLIENTSIDE{
	int c = 0;

	for (int j = 0; j < 30; j++){
		Stock_RemindUpgrade(j, c, true, "");
	}
	if(!GetUserCVar(ConsolePlayerNumber(), "sfp_stockreminder")) terminate;

	int s = 0;
	str uplvl;

	if(GetUserCVar(ConsolePlayerNumber(), "sfp_sr_techs")){

		if (Tech_IsDoneC(TECH_GENLAB_HEALTH1) || Tech_IsDoneC(TECH_GENLAB_HEALTH2) ||
			Tech_IsDoneC(TECH_GENLAB_SPEEDB) || Tech_IsDoneC(TECH_GENLAB_ARMOR) ||
			Tech_IsDoneC(TECH_GENLAB_RESISB) || Tech_IsDoneC(TECH_GENLAB_REGENB) ||
			Tech_IsDoneC(TECH_GENLAB_STIMPK))
		{
			c = CR_CYAN;
			s = Stock_RemindUpgrade(s, c,Tech_IsDoneC(TECH_GENLAB_HEALTH1) || Tech_IsDoneC(TECH_GENLAB_HEALTH2),
				strparam(s:"+ HUMAN GENETICS ", s:Tech_IsDoneC(TECH_GENLAB_HEALTH2) ? ("P2") : ("P1")));	
			s = Stock_RemindUpgrade(s, c,Tech_IsDoneC(TECH_GENLAB_SPEEDB), "+ Adrenaline");
			s = Stock_RemindUpgrade(s, c,Tech_IsDoneC(TECH_GENLAB_ARMOR), "+ Steel Plating");
			s = Stock_RemindUpgrade(s, c,Tech_IsDoneC(TECH_GENLAB_RESISB), "+ Resister Unit");
			s = Stock_RemindUpgrade(s, c,Tech_IsDoneC(TECH_GENLAB_REGENB), "+ Absorber Unit");
			c = CR_LIGHTBLUE;
			s = Stock_RemindUpgrade(s, c,Tech_IsDoneC(TECH_GENLAB_STIMPK), "+ 10 Stimpacks");
			s = Stock_RemindUpgrade(s, c, true, "");
		}

		if (Tech_IsDoneC(TECH_ARMORY_ARMS2) ||
			Tech_IsDoneC(TECH_ARMORY_ARMS1) ||
			Tech_IsDoneC(TECH_ARMORY_BOOMRN) ||
			Tech_IsDoneC(TECH_ARMORY_AMMOBP) ||
			Tech_IsDoneC(TECH_ARMORY_GNADES) ||
			CheckInventory("P_Backpack"))
		{
			c = CR_GREEN;
			s = Stock_RemindUpgrade(s, c, Tech_IsDoneC(TECH_ARMORY_ARMS1) || Tech_IsDoneC(TECH_ARMORY_ARMS2), 
				strparam(s:"+ Arms ", s:Tech_IsDoneC(TECH_ARMORY_ARMS2) ? ("P2") : ("P1")));
			s = Stock_RemindUpgrade(s, c, Tech_IsDoneC(TECH_ARMORY_BOOMRN), "+ Boomshot Rounds");
			s = Stock_RemindUpgrade(s, c, Tech_IsDoneC(TECH_ARMORY_AMMOBP), "+ Ammo Backpack");
			c = CR_DARKGREEN;
			s = Stock_RemindUpgrade(s, c, Tech_IsDoneC(TECH_ARMORY_GNADES), "+ Grenades");
			s = Stock_RemindUpgrade(s, c, CheckInventory("P_Backpack"), "+ Personal Backpack");
			s = Stock_RemindUpgrade(s, c, true, "");
		}
		
		if (Tech_IsDoneC(TECH_REFINE_PUMP1) || Tech_IsDoneC(TECH_REFINE_PUMP2) || Tech_IsDoneC(TECH_REFINE_COINX5))
		{
			c = CR_GOLD;
			s = Stock_RemindUpgrade(s, c, Tech_IsDoneC(TECH_REFINE_PUMP1), "+ Processing Phase 1");
			s = Stock_RemindUpgrade(s, c, Tech_IsDoneC(TECH_REFINE_PUMP2), "+ Processing Phase 2");
			s = Stock_RemindUpgrade(s, c, Tech_IsDoneC(TECH_REFINE_COINX5), "+ Compensation");
		}

		for (int i = 0; i < 20; i++){
			Stock_RemindUpgrade(s+i, c, true, "");
		}

		delay(35*3 + 17);
	}
	s = 0, c = 0;

	if(GetUserCVar(ConsolePlayerNumber(), "sfp_sr_playups")){
		if (PlayUp_CheckInv("PlayUp_BodyEnhancement") || 
			PlayUp_CheckInv("PlayUp_ExpansiveRestock") || 
			PlayUp_CheckInv("PlayUp_ArmorRepair") ||
			PlayUp_CheckInv("PlayUp_PowUpUpgrade_Defense") || 
			PlayUp_CheckInv("PlayUp_PowUpUpgrade_Attack") || 
			PlayUp_CheckInv("PlayUp_MechSync") ||
			PlayUp_CheckInv("PlayUp_SpawnShield") || 
			PlayUp_CheckInv("PlayUp_HealCoins") || 
			PlayUp_CheckInv("PlayUp_Prometeo") ||
			PlayUp_CheckInv("PlayUp_RuneSync"))
		{
			c = CR_BLUE;
			s = Stock_RemindUpgrade(s, c,true, 	"// Player Ups. //");
			s = Stock_RemindUpgrade(s, c,true, "");
			s = Stock_RemindUpgrade(s, c,PlayUp_CheckInv("PlayUp_BodyEnhancement"), 	"+ Body Enhancement");
			s = Stock_RemindUpgrade(s, c,PlayUp_CheckInv("PlayUp_ExpansiveRestock"), "+ Expansive Restock");
			s = Stock_RemindUpgrade(s, c,PlayUp_CheckInv("PlayUp_ArmorRepair"), 		"+ Armor Repair");
			s = Stock_RemindUpgrade(s, c,PlayUp_CheckInv("PlayUp_PowUpUpgrade_Defense"), 	"+ Def. Powerup");
			s = Stock_RemindUpgrade(s, c,PlayUp_CheckInv("PlayUp_PowUpUpgrade_Attack"), 	"+ Off. Powerup");
			s = Stock_RemindUpgrade(s, c,PlayUp_CheckInv("PlayUp_MechSync"), 		"+ Mech Syncronize");
			s = Stock_RemindUpgrade(s, c,PlayUp_CheckInv("PlayUp_SpawnShield"), 		"+ Spawn Shield");
			s = Stock_RemindUpgrade(s, c,PlayUp_CheckInv("PlayUp_HealCoins"), 		"+ Healing Coins");
			s = Stock_RemindUpgrade(s, c,PlayUp_CheckInv("PlayUp_Prometeo"), 		"+ Prometeo Protocol");
			s = Stock_RemindUpgrade(s, c,PlayUp_CheckInv("PlayUp_RuneSync"), 		"+ Rune Syncronize");
			s = Stock_RemindUpgrade(s, c,true, "");
		}
	}

	if(GetUserCVar(ConsolePlayerNumber(), "sfp_sr_stimups")){
		if (StimUp_CheckInv("StimUp_ExtraDosis") || StimUp_CheckInv("StimUp_Amount") ||
			StimUp_CheckInv("StimUp_Overhealth") || StimUp_CheckInv("StimUp_HardShell") ||
			StimUp_CheckInv("StimUp_Cicatrizate") || StimUp_CheckInv("StimUp_Caffeine") ||
			StimUp_CheckInv("StimUp_Stimulant"))
		{
			c = CR_CREAM;
			s = Stock_RemindUpgrade(s, c,true, 	"// Stimpack Ups. //");
			s = Stock_RemindUpgrade(s, c,true, "");
			uplvl = strparam(s:"+ Extra Dosis Lv.", d:StimUp_CheckInv("StimUp_ExtraDosis"));
			s = Stock_RemindUpgrade(s, c, StimUp_CheckInv("StimUp_ExtraDosis"), uplvl);
			uplvl = strparam(s:"+ ", d:StimUp_CheckInv("StimUp_Amount"), s:" Stimps. More");
			s = Stock_RemindUpgrade(s, c, StimUp_CheckInv("StimUp_Amount"), uplvl);
			uplvl = strparam(s:"+ ", d:(10*StimUp_CheckInv("StimUp_Overhealth")), s:"% Overheal");
			s = Stock_RemindUpgrade(s, c, StimUp_CheckInv("StimUp_Overhealth"), uplvl);
			uplvl = strparam(s:"+ 50% A.Repair Lv", d:(StimUp_CheckInv("StimUp_HardShell")));
			s = Stock_RemindUpgrade(s, c, StimUp_CheckInv("StimUp_HardShell"), uplvl);
			uplvl = strparam(s:"+ Regen. Lv", d:(StimUp_CheckInv("StimUp_Cicatrizate")));
			s = Stock_RemindUpgrade(s, c, StimUp_CheckInv("StimUp_Cicatrizate"), uplvl);
			uplvl = strparam(s:"+ ", d:(10*StimUp_CheckInv("StimUp_Caffeine")), s:"% Speed Up");
			s = Stock_RemindUpgrade(s, c, StimUp_CheckInv("StimUp_Caffeine"), uplvl);
			uplvl = strparam(s:"+ ", d:(20*StimUp_CheckInv("StimUp_Stimulant")), s:" Max HP");
			s = Stock_RemindUpgrade(s, c, StimUp_CheckInv("StimUp_Stimulant"), uplvl);
			s = Stock_RemindUpgrade(s, c,true, "");
		}
	}
	
	if(GetUserCVar(ConsolePlayerNumber(), "sfp_sr_mechups")){
		if (MechUp_CheckInv("Mechup_Nails") ||
		MechUp_CheckInv("Mechup_Rockets") ||
		MechUp_CheckInv("Mechup_Armor") ||
		MechUp_CheckInv("Mechup_Speed") ||
		MechUp_CheckInv("Mechup_AutoRepair") ||
		MechUp_CheckInv("Mechup_LifeSave") ||
		MechUp_CheckInv("Mechup_SelfDestruct") ||
		MechUp_CheckInv("Mechup_Efficiency"))
		{
			c = CR_ORANGE;
			s = Stock_RemindUpgrade(s, c,true, 	"// Mech Ups. //");
			s = Stock_RemindUpgrade(s, c,true, "");
			uplvl = strparam(s:"+ Nails Lv. ", d:MechUp_CheckInv("Mechup_Nails"));
			s = Stock_RemindUpgrade(s, c, MechUp_CheckInv("Mechup_Nails"), uplvl);
			uplvl = strparam(s:"+ Rockets Lv. ", d:MechUp_CheckInv("Mechup_Rockets"));
			s = Stock_RemindUpgrade(s, c, MechUp_CheckInv("Mechup_Rockets"), uplvl);
			uplvl = strparam(s:"+ Armor Lv. ", d:MechUp_CheckInv("Mechup_Armor"));
			s = Stock_RemindUpgrade(s, c, MechUp_CheckInv("Mechup_Armor"), uplvl);
			uplvl = strparam(s:"+ Manuver Lv. ", d:MechUp_CheckInv("Mechup_Speed"));
			s = Stock_RemindUpgrade(s, c, MechUp_CheckInv("Mechup_Speed"), uplvl);
			uplvl = strparam(s:"+ Auto Repair Lv. ", d:MechUp_CheckInv("Mechup_AutoRepair"));
			s = Stock_RemindUpgrade(s, c, MechUp_CheckInv("Mechup_AutoRepair"), uplvl);
			s = Stock_RemindUpgrade(s, c,MechUp_CheckInv("Mechup_LifeSave"), 		"+ Life Saver");
			s = Stock_RemindUpgrade(s, c,MechUp_CheckInv("Mechup_SelfDestruct"), 	"+ Self Destruct");
			s = Stock_RemindUpgrade(s, c,MechUp_CheckInv("Mechup_Efficiency"), 		"+ Efficiency");
			s = Stock_RemindUpgrade(s, c,true, "");
		}
	}

	delay(17);
}

function int Stock_RemindUpgrade(int show_stack, int show_color, bool condition, str string){
	if(condition){
		int offset = show_stack * 10.0;
		int x = 600.1;
		int y = 100.1 + offset;
		int shineFlags = HUDMSG_FADEINOUT | HUDMSG_AddBlend;
		
		sethudsize(800, 600, s_ScreenSize[2]);
		setfont("SMALLFNT");
		hudmessage(s:string; shineFlags, 0, show_color, x, y, 0.2,0.15,0.15);
		hudmessage(s:string; HUDMSG_FADEOUT, 2020 + show_stack, show_color, x, y, 3.0, 0.5);
		return show_stack+1;
	}
	else {
		setfont("SMALLFNT");
		hudmessage(s:""; 0, 2020 + show_stack, show_color, 600.1, 100.1 + offset, 2.0);
	}
	return show_stack;
}

script "SFPlus_PrometeoScript" (void)
{
// Prometeo Script, Saves your life once (recharged at respawning).
// In addition, grants you a short invul and a x2 Damage multipler.
// If you use a berserk here, you could get a x6 Damage multipler!
// Even better, you could get a x12 Damage multipler if you upgrade the powerups!
// This shit is overpowered when used on the right time!
	if(CheckInventory("PlayUp_Prometeo_Used") ) Terminate;
	if(!CheckInventory("PlayUp_Prometeo_Ready")){
		GiveInventory("PlayUp_Prometeo_Ready", 1);
	} else terminate;
	SetPlayerProperty(0, 1, PROP_BUDDHA);
	while(GetActorProperty(0, APROP_Health) > 1){
		// Wait until the player is pinned down
		delay(1);
	} 
	SetPlayerProperty(0, 0, PROP_BUDDHA);
	setactorproperty(0, APROP_SpawnHealth, 50);
	GiveInventory("PlayUp_Prometeo_Used", 1);
	TakeInventory("PlayUp_Prometeo_Ready", 1);
	giveinventory("PlayUp_Prometeo_Activate", 1);
}