///////////////////////////////////////////////////
//      SHOTGUN FRENZY
//   a mod by Wad'a'Holic

// fc_DemonicLab.acs
// structure scripts
#library "fc_DLab"

#import "f_IntDB.acs"
#import "f_Tip.acs"
#import "fc_Const.acs"
#import "fc_CommB.acs"
#import "fl_techs.acs"
#import "fp_ptdir.acs"

#include "zcommon.acs"
#include "acsutils.acs"
#include "samu_tools.acs"
// build
script 400 (void)
{
	SetActivator(0); //Preventing a major disaster
    // construction
    c_Construct (201, 176, 128, 128, "RROCK03", "FLOOR7_2", "");
    c_Construct (202, 192, 128, 128, "RROCK03", "FLOOR7_2", "");
    c_Construct (203, 201, 132, 128, "GRND", "FLOOR7_2", "");
    c_Construct (204, 224, 128, 128, "FLOOR4_8", "FLOOR7_2", "");
    c_Construct (205, 176, 24, 256, "GRND", "CEIL5_2", "");
    c_Construct (206, 144, 24, 360, "GRND", "CEIL5_2", "");
    c_Construct (207, 224, 24, 264, "FLOOR4_8", "SHAWN2", "");
    c_Construct (208, 255, 16, 136, "GRND", "CEIL5_1", "");
    c_Construct (209, 224, 8, 72, "GRND", "CEIL5_2", "");
    c_Construct (210, 208, 128, 136, "CEIL5_1", "CEIL5_1", "");
    c_Construct (211, 176, 128, 136, "CEIL5_1", "CEIL5_1", "");
    c_Construct (212, 208, 128, 72, "CEIL5_1", "CEIL5_2", "");
    c_Construct (213, 255, 384, 0, "CEIL5_2", "", "");
    c_Construct (214, 255, 128, 128, "CEIL5_2", "CEIL5_2", "");
    c_Construct (215, 255, 144, 160, "CEIL5_2", "CEIL5_2", "");
    
    // summon turrets
    spawnspot("TurretBase", 154, 0, 0);
    
    // set techflag
	Tech_Set(TECH_MARCOM, TECHF_DONE);
    
    // constructed message
    //f_TipShow ("BUTTON", "Marine Command", "Construction Complete!", 1);
    
    // check bridge
    delay(35 * 2);
    if (Tech_isDone(TECH_MARCOM) && Tech_isDone(TECH_MECHFA) &&
		c_BridgeStatus_4 == 0 && f_Gametype != 5)
    {
        c_BridgeStatus_4 = 1;
        floor_raisetonearest(343, 125);
    }
    
    // morph
    //f_TipShow ("BUTTON", "Marine Core", "Attack functions now online...", 1);
    //morphactor(104, "", "AttackBattery", 99999999999);
}

#define FP_MCOM_CPULSE_CRIPPLE	"SFPLus_CripplePulse"
#define FP_MCOM_CPULSE_HEAL		"SFPLus_HealPulse"
#define FP_MCOM_CPULSE_SMITE	"SFPLus_SmitePulse"
#define FP_MCOM_CPULSE_REPAIR	"SFPLus_RepairPulse"
#define FP_MCOM_CPULSE_HALT		"SFPLus_HaltPulse"
#define FP_MCOM_CPULSE_WARCRY		"SFPLus_WarcryPulse"
#define FP_MCOM_CPULSE_SHOCK		"SFPLus_ShockPulse"

script "SFPlus_CoreRecharge" (int delay_time){	
	int slot = Notify_QueueFreeSpace();
	ACS_NamedExecuteAlways("SFPlus_ProgressScript",0, FP_PTID_CPULSE, delay_time, slot);
	do{ delay(1); }while (p_TipProcess[slot]); // Wait for it.
	
	Tech_Set(TECH_MARCOM_CPULSE, TECHF_DONE);
}

// Need the shock and war pulses
// Shock: all the demons will be stunned for a while.
// War: All players gain x2.5 Dmg Up and Fast fire. 

script "SFPlus_CorePulse" (int pulse_cmd)
{	// A Generalized script for all of the core pulses.
	if(Tech_isLock(TECH_MARCOM_CPULSE)){
		Notify_Message("Core pulse failed",
		"The core pulse is still charging!", "INFO", 3);
		terminate;
	}
	SetActivator(0); //Preventing a major disaster
    ambientsound("cv/corepulse", 125);
    
    // disable use of core weapons
	Tech_SetDone(TECH_MARCOM_CPULSE, TECHF_LOCK);
	
	str pulse_name;
	int execute;
	int charge_delay;
	switch(pulse_cmd){
		case 1: // Buffs players with some extra 
			pulse_name = "HEAL"; 
			execute = FP_MCOM_CPULSE_HEAL; 
			charge_delay = 15; 
		break;
		case 2: // Harms all demons with 500 dmg.
			pulse_name = "CRIPPLE"; 
			execute = FP_MCOM_CPULSE_CRIPPLE; 
			charge_delay = 20; 
		break;
		case 3: // Stops monster spawning for a minute.
			pulse_name = "HALT"; 
			execute = FP_MCOM_CPULSE_HALT; 
			charge_delay = 25; 
		break;
		case 4: // Harms all demons within the risked sector with 3000 dmg.
			pulse_name = "SMITE"; 
			execute = FP_MCOM_CPULSE_SMITE; 
			charge_delay = 30; 
		break;
		case 5: // Instantly repairs the risked core.
			pulse_name = "REPAIR"; 
			execute = FP_MCOM_CPULSE_REPAIR; 
			charge_delay = 30; 
		break;
		case 6: // Stops monster spawning for a minute.
			pulse_name = "WARCRY"; 
			execute = FP_MCOM_CPULSE_WARCRY; 
			charge_delay = 30; 
		break;
		case 7: // Stops monster spawning for a minute.
			pulse_name = "SHOCK"; 
			execute = FP_MCOM_CPULSE_SHOCK; 
			charge_delay = 25; 
		break;
	}
	int startup_delay = 5-2*Tech_isDone(TECH_MARCOM_COPTIM);
	charge_delay = charge_delay - ((floor(charge_delay * 0.25))>> 16)*Tech_isDone(TECH_MARCOM_COPTIM);
	Notify_Message(strparam(s:pulse_name, s:" PULSE ACTIVATED"),
		strparam(s:"Activating in ",d:startup_delay,s:" seconds..."), "INFO", 2);
    delay(35 * (startup_delay));
	acs_execute(126, 0, 1, 0, 0);
    ambientsound("x1/taser", 125);
	//Effects
	
	acs_NamedExecuteWait(execute, 0);
	
	delay(35 * 2);
    //f_TipShow ("BUTTON", "", "", 19);
	ACS_NamedExecuteAlways("SFPlus_CoreRecharge",0, charge_delay);
}

// core pulse: Cripple
// Harms all demons with 500 of damage.
script FP_MCOM_CPULSE_CRIPPLE (void)
{
	giveActorInventory(110, "FPMToken_NoCount", 1);
	giveActorInventory(111, "FPMToken_NoCount", 1);
	giveActorInventory(112, "FPMToken_NoCount", 1);
	giveActorInventory(113, "FPMToken_NoCount", 1);
	giveActorInventory(114, "FPMToken_NoCount", 1);
	// Temporaly disable the credit earning of these monsters.
	int damage = 500 * (1+2*Tech_isDone(TECH_MARCOM_CBOOST));
	delay(2);
	thing_damage(110, damage, "NoCount");
	thing_damage(111, damage, "NoCount");
	thing_damage(112, damage, "NoCount");
	thing_damage(113, damage, "NoCount");
	thing_damage(114, damage, "NoCount");
	delay(2);
	// Re-activate the credit earning.
	takeActorInventory(110, "FPMToken_NoCount", 1);
	takeActorInventory(111, "FPMToken_NoCount", 1);
	takeActorInventory(112, "FPMToken_NoCount", 1);
	takeActorInventory(113, "FPMToken_NoCount", 1);
	takeActorInventory(114, "FPMToken_NoCount", 1);
}

// core pulse: Smite (NEW!)
// Harms any demon inside of the risked sector with 3000 hp.
script FP_MCOM_CPULSE_SMITE (void)
{
	giveActorInventory(110 + g_Status, "FPMToken_NoCount", 1);
	// Temporaly disable the credit earning of these monsters.
	int damage = 3000 * (1+4*Tech_isDone(TECH_MARCOM_CBOOST));
	delay(2);
	thing_damage(110 + g_Status, damage,  "NoCount");
	delay(2);
	// Re-activate the credit earning.
	takeActorInventory(110 + g_Status, "FPMToken_NoCount", 1);
}

// halt pulse
// Halts the demon spawning
script FP_MCOM_CPULSE_HALT (void)
{
	c_HaltSpawning = 1;

	ProgTip_DefaultProps(-1);
	ProgTip_SetMsg		(-1, "HALT PULSE");
	ProgTip_SetEndMsg1	(-1, "HALT PULSE EXPIRED!");
	ProgTip_SetEndMsg2	(-1, "Hostiles are spawning again!");
	int time = 60 + 30*Tech_isDone(TECH_MARCOM_CBOOST);

	int slot = Notify_QueueFreeSpace();
	ACS_NamedExecuteAlways("SFPlus_TimerScript",0, -1, time, slot);
	do{ delay(1); }while (p_TipProcess[slot]); // Wait for it.

	// reactivate demons
	c_HaltSpawning = 0;
}

// heal pulse
script FP_MCOM_CPULSE_HEAL (int pslot)
{
	// Give an instant megasphere (Activated upon pickup), +30% resistance 20 seconds, and a 5 second short invul to all players.
	for(int i = 0; i < 32; i++)
	{
		if(PlayerInGame(i)){
			SetActivatorToPlayer(i);
			
			int megasphere_now = CheckInventory("Inventory_Megasphere");
			GiveInventory("Inventory_Megasphere", 1);
			UseInventory("Inventory_Megasphere");
			if(CheckInventory("Inventory_Megasphere") != megasphere_now){
				GiveInventory("Inventory_Megasphere", 1); // Check if it's on maximum, might not add the temporal megasphere, and if it does, refund it.
			}
			TakeInventory("CorePulse_Heal_Resistance", 1);
			TakeInventory("CorePulse_Heal_Invul", 1);

			GiveInventory("CorePulse_Heal_Resistance", 1+Tech_isDone(TECH_MARCOM_CBOOST));
			GiveInventory("CorePulse_Heal_Invul", 1+Tech_isDone(TECH_MARCOM_CBOOST));
			
			
			SetActivator(0);
		}
	}
}

script FP_MCOM_CPULSE_WARCRY (int pslot)
{
	// Give a x3 damage up, and fast fire for 20 seconds.
	for(int i = 0; i < 32; i++)
	{
		if(PlayerInGame(i)){
			SetActivatorToPlayer(i);
			
			TakeInventory("CorePulse_Warcry_RageUp", 1);
			TakeInventory("CorePulse_Warcry_DamageUp", 1);

			GiveInventory("CorePulse_Warcry_RageUp", 1+Tech_isDone(TECH_MARCOM_CBOOST));
			GiveInventory("CorePulse_Warcry_DamageUp", 1+Tech_isDone(TECH_MARCOM_CBOOST));
			
			
			SetActivator(0);
		}
	}
}

script FP_MCOM_CPULSE_SHOCK (int pslot)
{
	// Paralize your enemies for a while.
	// Eh, next update lol.
}

// core revive
script FP_MCOM_CPULSE_REPAIR (void)
{
	int extrahealth = 300*Tech_isDone(TECH_MARCOM_CBOOST);
	int core = 100 + g_Status;
	if ( core != 104){
		setactorproperty(core, APROP_Health, 750 + extrahealth);
	}       
	else {
		setactorproperty(core, APROP_Health, 1500 + extrahealth);

	}                            
}