///////////////////////////////////////////////////
//      SHOTGUN FRENZY
//   a mod by Wad'a'Holic

// fc_CommandButtons.acs
// script containing commander menu buttons
#library "fc_CommB"

#import "f_IntDB.acs"
#import "fl_Price.acs"
#import "f_Tip.acs"
#import "fl_techs.acs"
#import "fp_cmdevt.acs"
#include "zcommon.acs"
#include "samu_tools.acs"

//check if we're allowed to put turrets down again

function bool CMD_AreaCheck(int y){
	return 
		y < getactory(81) & g_Status == 1 ||
		y < getactory(82) & g_Status == 2 || 
		y < getactory(83) & g_Status == 3 || 
		y < getactory(84) & g_Status == 4;
}

function bool CMD_FloorCheck(int tid){
	return !(
	CheckActorFloorTexture(tid, "") || CheckActorFloorTexture(tid, "F_SKY1")
	
	);
}

// Spawn something in the indicated position, minding the area
function bool CMD_Spawn(str something, int tid){
	int angle = GetActorAngle(130);
	int r = 512.0;
	
	int x = GetActorX(130) + FixedMul(r,cos(angle));
	int y = GetActorY(130) + FixedMul(r,sin(angle));
	int z = GetSectorFloorZ(0,x>>16, y>>16);
	
	SpawnForced("DeploySpot", x , y, z, 500);
	int floorz = GetActorZ(500);
	bool free;
	int zoff = 0.0;
	for (int i = 0; i < 5 && !free; i++){ // Try spawning it. Give up after 5 attempts if you can't spawn it.
		free = Spawn("DeployBlocker", x, y, floorz + zoff, 500);
		zoff += 8.0;
	}
	int sucess = False;
	bool valid_area = CMD_AreaCheck(y);
	bool valid_floor = CMD_FloorCheck(500);
	if (free && valid_area && valid_floor){
		sucess = Spawn(something, x, y, floorz + fixedmul(2.0, zoff), tid);
	}
	if(sucess == false){
		if		(!valid_area) 	print (s:"Can't build in a lost sector!");
		else if (!valid_floor)  print (s:"Can't build over the liquids or void!");
		else if (!free) 		print (s:"The area is blocked or unstable for building!");
	}
	thing_Remove(500);
	return sucess;
}

// money bag temp disable
script 966 (int amount)
{
	str msg = strparam(d:amount, s:" credits for the lucky one!");
	
	//[Samu] Extended, for wealthier and bigger drops.
	if(c_credits < amount) terminate;
	

	if(Thingcount(T_NONE, 5550)) {
		
		Thing_Remove(5550);
		c_credits += c_MoneyDropped;
		// add desired message
        f_TipShow ("CM80", "Money Bag Re-Placed!", msg, 2);
	}else{
		f_TipShow ("CM80", "Money Bag Dropped!", msg, 2);
	}
	c_MoneyDropped = amount;
	c_credits -= amount;
	Thing_ChangeTID(ActivatorTID(), 5550);
    c_allow_moneybag = 0;
    delay(35 * 3);
    c_allow_moneybag = 1;
}

// events system

// Look-up fp_cmdevt.acs
script "SFPlus_CMDEvent_Init" OPEN {
	CmdEvent_Initialize();
}
// Look-up fp_cmdevt.acs
script "SFPLus_CMDEvent" (int which, int price) {
	CmdEvent_Execute(which, price);
}

script "SFPlus_CMDCleanup" (void){
	CmdEvent_Execute(CMDEVT_AIRSTK_CANCEL,0);
}