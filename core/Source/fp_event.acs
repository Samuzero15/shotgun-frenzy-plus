#library "fp_event"
#include "zcommon.acs"

script "SFP_Events" (int type, int arg1, int arg2) Event {
    switch(type){
        case GAMEEVENT_ACTOR_DAMAGED:
            int hpold = getActorProperty(0, APROP_HEALTH);
            int hpnow = hpold - arg1;
            if(playernumber() >= 0){
                SetResultValue(ACS_NamedExecuteWithResult("SFPlus_OnPlayerDamage", arg1, hpnow <= 0));
            }
        break;
        case GAMEEVENT_ACTOR_ARMORDAMAGED:
            if(checkinventory("RuneTemperance")){
                //SetActivatorToTarget(0);
                // Your armor takes double damage.
                if(GetArmorInfo(ARMORINFO_SAVEAMOUNT) > 0){
                    int dmg = 2 * ((arg1 * GetArmorInfo(ARMORINFO_SAVEPERCENT)) >> 16);
                    //log(s:"(fp_event) Save percent: ", f:GetArmorInfo(ARMORINFO_SAVEPERCENT));
                    //log(s:"(fp_event) Current Armor: ", d:armor);
                    //log(s:"(fp_event) Damage Took: ", d:arg1);
                    //log(s:"(fp_event) Damage Absorbed: ", d:((arg1 * GetArmorInfo(ARMORINFO_SAVEPERCENT)) >> 16));
                    
                    if(CheckInventory("BasicArmor") < dmg){
                        GiveInventory("TemperanceRune_BreakToken", 1);
                    }
                    SetResultValue(dmg);
                }
            }
        break;
    }    
}

script "SFPlus_ConstantEffects" (void) {
    int last_health = GetActorProperty(ActivatorTID(), APROP_HEALTH);
    int tics = 0;
    while(GetActorProperty(ActivatorTID(), APROP_HEALTH) > 0) {

        if(checkinventory("RuneHealth")){
            last_health = RuneEffect_ExtraHealth(last_health, 0.5); 
        }

        if(CheckInventory("RuneAmmunition")) {
            if(tics == 1 && random(0, 100) < 20){
                GiveInventory("AmmunitionRune_Activate", 1);
                //log(s:"Infinite ammo.");
            }
        }

        if(checkinventory("RuneSoul")){
            GiveInventory("SoulRune_Activate", 1);
        }else{
            GiveInventory("SoulRune_Reset", 1);
        }

        tics = (tics % 35) + 1;
        delay(1);
    }
    GiveInventory("SoulRune_Reset", 1);
}

function int RuneEffect_ExtraHealth(int last_health, int health_mult)
{
	int health = GetActorProperty(0, APROP_Health);
	int maxHealth = GetActorProperty(0, APROP_SpawnHealth);
	if(maxHealth == 0){ maxHealth = 100; /* default max */ }
	int left = maxHealth - health;
	if(left > 0 && last_health != health){
		int heal = health - last_health;
		if(heal >= left) heal = left; // Only gains, not loss
		// The difference between the health on the last tick and your current health is recived.
		GiveInventory("Health", (heal * health_mult) >> 16);
		//Print(s:"ExtraHealth: ", d:((heal * health_mult) >> 16));
	}
	last_health = GetActorProperty(0, APROP_Health);
	return last_health;
}

script "SFPlus_OnTeleport" (void) {
	// What happens to the teleported things?
	SetActivatorToTarget(0);
	delay(5);
	if(CheckActorClass(ActivatorTID(), "FrenzyPlayer")){
		// If a player was teleported, check if he got the spawn protection upgrade.
		if(CheckActorInventory(ActivatorTID(), "PlayUp_SpawnShield")){
			TakeActorInventory(ActivatorTID(), "PlayUp_SpawnShield_Power", 1);
			delay(1);
			giveActorinventory(ActivatorTID(),"PlayUp_SpawnShield_Activate", 1);
		}
	}
}

script "SFPlus_OnPlayerDamage" (int dmg, int fatal){
    int TotalDmg = dmg;
    if(fatal){
        if(checkinventory("RuneResurrection")){
            GiveInventory("ResurrectionRune_Activate", 1);
            TotalDmg = 0; // Deny effective damage.
        }
        
        if(CheckInventory("PlayUp_Prometeo") && !CheckInventory("PlayUp_Prometeo_Used")){
            // Activate Prometeo Protocol effect.
            setactorproperty(0, APROP_SpawnHealth, 50);
            setactorproperty(0, APROP_Health, 51);
            giveinventory("PlayUp_Prometeo_Activate", 1);
            TotalDmg = 1; // Just to call the pain state.
        }
    }
    else{
        if(checkinventory("RuneDefense")){
            GiveInventory("DefenseRune_Activate", 1);
        }

        if(checkinventory("RuneBlast")){
            GiveInventory("BlastRune_Activate", 1);
        }

        if(checkinventory("RuneTemperance")){
            if(checkinventory("BasicArmor") || checkinventory("MegasphereArmor")){
                LocalAmbientSound("runes/temperance/hit", 127);
                TotalDmg = 0; // Deny effective damage.
            }
            else if (CheckInventory("TemperanceRune_BreakToken")) {
                LocalAmbientSound("runes/temperance/break", 127);
                GiveInventory("TemperanceRune_GuardBreak", 1);
            }
        }
    }
    SetResultValue(TotalDmg);
}
script "SFPlus_OnMonsterKill" (void){
    if(checkinventory("RuneRampage")){
        GiveInventory("RampageRune_Activate", 1);
    }

    if(checkinventory("RuneSoul")){
        GiveInventory("SoulRune_Counter", 1);
    }
}

script "SFPlus_Dash" (void){
    int DASH_SPD = 40; // Speed defined by monster.
    int dashAngle = GetActorAngle(0);
    int speed_cap = (DASH_SPD * 10.0) >> 16;
    int factor_slide = 0.12;
    str str_speed = "";
    int a = "";
    int slidertime = 35;
    int i;
    int min_speed;
    for(i = 0; i < slidertime; i++)
    {
        if(GetActorZ(0) == GetActorFloorZ(0))
        {
            SetActorPosition(0, GetActorX(0), GetActorY(0), GetActorZ(0)+1.0, FALSE);
            SetActorVelocity(0, cos(dashAngle) * DASH_SPD, sin(dashAngle) * DASH_SPD, 0.0, FALSE, FALSE);
            //if(DASH_SPD < speed_cap) 
            if((DASH_SPD * factor_slide) < 1.0) min_speed = 1;
            else min_speed = (DASH_SPD * factor_slide >> 16);
            DASH_SPD = DASH_SPD - min_speed;// slow it down
            if(DASH_SPD < 0)SetActorVelocity(0, 0, 0, 0.0, FALSE, FALSE);
            a = strparam(s:" dash_speed: ", d:DASH_SPD, s:" \n");
            str_speed = strparam(s:str_speed, s:a);
            print(s:str_speed);
        }
        delay(4);
    }
}