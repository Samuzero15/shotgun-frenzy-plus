//This goes in frenzy_weapons. wad and it replaces the DW9 lump
ACTOR Grenade_Flinger
{
	Radius				16
	Height				8
	
	Speed				40
	Damage				80
	Gravity				1.0
	Mass				100
	bouncefactor                    0.8
	
	Obituary			"%0 was blowen to bits by %k's Flinger."

	DamageType			MarineFire2
	+NOBLOCKMAP
	+DROPOFF
	+MISSILE
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	+DOOMBOUNCE
	+FORCERADIUSDMG
	+NODAMAGETHRUST
	+MTHRUSPECIES
	bouncecount 16
	
	SeeSound "w9/fire1"
	DeathSound "Flinger/Grenade/Booms"
	
	States
	{
		Spawn:
			GRNS ABA 3 bright
			GRNS B 3 bright A_PlaySound("w9/fire2")
			goto Spawn
		Death:
			MISL B 8 bright A_Explode(75,256,0,0,16)
			MISL C 6 bright
			MISL D 4 bright
			stop
	}
}

ACTOR Grenade_Flinger_Exploder
{
	Radius				16
	Height				8
	
	Speed				40
	Damage				80
	Gravity				1.0
	Mass				100
	DamageType			MarineFire2
	
	+NOBLOCKMAP
	+DROPOFF
	+MISSILE
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	+DOOMBOUNCE
	+MTHRUSPECIES
	bouncecount 16
	
	SeeSound "w9/fire1"
	DeathSound "Flinger/Grenade/Booms"
	
	States
	{
		Spawn:
			GRNS ABA 3 bright
			GRNS B 3 bright A_PlaySound("w9/fire2")
			goto Spawn
		Death:
			MISL B 0 bright A_PlaySound("weapons/rocklx")
			MISL B 0 bright A_SpawnItemEx("Grenade_Debris_Spawner",0,0,0,0,0,0,0,SXF_TransferPointers)
			MISL C 6 bright
			MISL D 4 bright
			stop
	}
}

Actor Grenade_Debris_Spawner {
	radius 1
	height 1
	+NoBlockMap
	States
	{
		Spawn:
			TNT1 A 4
			TNT1 A 0 bright A_SpawnItemEx("Grenade_Flinger_Exploder_Debris", 0, 0, 10, random(-5,5), random(-5,5),random(3, 10), 0, SXF_TransferPointers)
			TNT1 A 0 bright A_SpawnItemEx("Grenade_Flinger_Exploder_Debris", 0, 0, 10, random(-5,5), random(-5,5),random(3, 10), 0, SXF_TransferPointers)
			TNT1 A 0 bright A_SpawnItemEx("Grenade_Flinger_Exploder_Debris", 0, 0, 10, random(-5,5), random(-5,5),random(3, 10), 0, SXF_TransferPointers)
			TNT1 A 0 bright A_SpawnItemEx("Grenade_Flinger_Exploder_Debris", 0, 0, 10, random(-5,5), random(-5,5),random(3, 10), 0, SXF_TransferPointers)
			TNT1 A 0 bright A_SpawnItemEx("Grenade_Flinger_Exploder_Debris", 0, 0, 10, random(-5,5), random(-5,5),random(3, 10), 0, SXF_TransferPointers)
			TNT1 A 0 bright A_SpawnItemEx("Grenade_Flinger_Exploder_Debris", 0, 0, 10, random(-5,5), random(-5,5),random(3, 10), 0, SXF_TransferPointers)
			TNT1 A 0 bright A_SpawnItemEx("Grenade_Flinger_Exploder_Debris", 0, 0, 10, random(-5,5), random(-5,5),random(3, 10), 0, SXF_TransferPointers)
			TNT1 A 0 bright A_SpawnItemEx("Grenade_Flinger_Exploder_Debris", 0, 0, 10, random(-5,5), random(-5,5),random(3, 10), 0, SXF_TransferPointers)
			TNT1 A 4
			TNT1 A 0 bright A_SpawnItemEx("Grenade_Flinger_Exploder_Debris", 0, 0, 10, random(-5,5), random(-5,5),random(3, 10), 0, SXF_TransferPointers)
			TNT1 A 0 bright A_SpawnItemEx("Grenade_Flinger_Exploder_Debris", 0, 0, 10, random(-5,5), random(-5,5),random(3, 10), 0, SXF_TransferPointers)
			TNT1 A 0 bright A_SpawnItemEx("Grenade_Flinger_Exploder_Debris", 0, 0, 10, random(-5,5), random(-5,5),random(3, 10), 0, SXF_TransferPointers)
			TNT1 A 0 bright A_SpawnItemEx("Grenade_Flinger_Exploder_Debris", 0, 0, 10, random(-5,5), random(-5,5),random(3, 10), 0, SXF_TransferPointers)
			TNT1 A 0 bright A_SpawnItemEx("Grenade_Flinger_Exploder_Debris", 0, 0, 10, random(-5,5), random(-5,5),random(3, 10), 0, SXF_TransferPointers)
			TNT1 A 0 bright A_SpawnItemEx("Grenade_Flinger_Exploder_Debris", 0, 0, 10, random(-5,5), random(-5,5),random(3, 10), 0, SXF_TransferPointers)
			TNT1 A 0 bright A_SpawnItemEx("Grenade_Flinger_Exploder_Debris", 0, 0, 10, random(-5,5), random(-5,5),random(3, 10), 0, SXF_TransferPointers)
			TNT1 A 0 bright A_SpawnItemEx("Grenade_Flinger_Exploder_Debris", 0, 0, 10, random(-5,5), random(-5,5),random(3, 10), 0, SXF_TransferPointers)
			stop
	}
}

ACTOR Grenade_Flinger_Exploder_Debris
{
	health 2
	radius 1
	height 1
	scale 0.5
	Mass				100
	Gravity				0.5
	
	+NoBlockMap
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	+DOOMBOUNCE
	+FORCERADIUSDMG
	+NODAMAGETHRUST
	+MTHRUSPECIES
	bouncecount 16
	
	ExplosionRadius			90		// reduced from 128 to 90
	ExplosionDamage			26		// reduced from 32 to 26
	DamageType			MarineFire2
	
	States
	{
		Spawn:
			GRNS AB 50
			MISL B 0 bright A_PlaySound("Flinger/Grenade/Booms")
			MISL B 0 A_Explode(25,96,0,0,16)
			MISL BCD 3
			stop
	}
}

ACTOR Grenade_NerveGas // Hm...
{
	Radius				16
	Height				8
	
	Speed				40
	Gravity				1.0
	Mass				100
	DamageType			MarineFire2
	
	Obituary			"%0 smell the ass of %k's gas nade."
	
	+NOBLOCKMAP
	+DROPOFF
	+MISSILE
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	+DOOMBOUNCE
	+MTHRUSPECIES
	+ThRUACTORS
	bouncecount 16
	
	SeeSound "w9/fire1"
	DeathSound "w9_detonate"
	
	States
	{
		Spawn:
			GRE1 A 3 bright a_playSound("Flinger/Grenade/NerveGas")
			TNT1 A 0 A_SpawnItemEX("NerveGas_HurtQuick")
			TNT1 A 0 a_SpawnItem("NerveGas_GFX")
			GRE1 B 3 bright
			TNT1 A 0 A_SpawnItemEX("NerveGas_HurtQuick")
			TNT1 A 0 a_SpawnItem("NerveGas_GFX")
			GRE1 A 3 bright
			TNT1 A 0 A_SpawnItemEX("NerveGas_HurtQuick")
			TNT1 A 0 a_SpawnItem("NerveGas_GFX")
			GRE1 B 3 bright A_PlaySound("w9/fire2")
			TNT1 A 0 A_SpawnItemEX("NerveGas_HurtQuick")
			TNT1 A 0 a_SpawnItem("NerveGas_GFX")
			
			GRE1 C 3 bright a_playSound("Flinger/Grenade/NerveGas")
			TNT1 A 0 A_SpawnItemEX("NerveGas_HurtQuick")
			TNT1 A 0 a_SpawnItem("NerveGas_GFX")
			GRE1 B 3 bright
			TNT1 A 0 A_SpawnItemEX("NerveGas_HurtQuick")
			TNT1 A 0 a_SpawnItem("NerveGas_GFX")
			GRE1 C 3 bright
			TNT1 A 0 A_SpawnItemEX("NerveGas_HurtQuick")
			TNT1 A 0 a_SpawnItem("NerveGas_GFX")
			GRE1 B 3 bright A_PlaySound("w9/fire2")
			TNT1 A 0 A_SpawnItemEX("NerveGas_HurtQuick")
			TNT1 A 0 a_SpawnItem("NerveGas_GFX")
			
			TNT1 A 0 A_ChangeFlag("ThruActors",false)
			goto Spawn
		Death:
			GRE1 A 3 bright 
			GRE1 C 2 bright A_PlaySound("w9/fire2")
			GRE1 A 3 bright 
			GRE1 C 2 bright A_PlaySound("w9/fire2")
			GRE1 A 3 bright 
			GRE1 AAD 3
			TNT1 A 0 A_Explode(30,64,0,0,64)
			GRNS A 0 A_SpawnItemEX("NerveGas_Hurt", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0)
			TNT1 A 0 a_playSound("Flinger/Grenade/NerveGas")
		GasPasser:
			TNT1 A 0 bright a_SpawnItem("NerveGas_GFX")
			GRE1 D 4 bright 
			TNT1 A 0 A_SetArg(1, args[1] + 1)
			TNT1 A 0 A_JumpIf(args[1] > 35, "IDontFeelSoGood")
			loop
		IDontFeelSoGood:
			GRE1 A 4 bright a_fadeout(0.1)
			loop
	}
}

Actor NerveGas_GFX
{
	states{
		Spawn:
		TNT1 A 1
		TNT1 A 0
		TNT1 A 0 bright a_SpawnItemEx("NerveGas_SteamUp",0,0,0,0,0,frandom(2.5,5.0))
		TNT1 A 0 bright A_SpawnItemEx("NerveGas_Fog", 0.0, 0.1, 16.0, frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(0.0, 2.0))
		stop
	}
}

//[Samu] Taken from alphahiatus dm!
// Thank you Himaura!
ACTOR NerveGas_Fog // Hm...
{
	Alpha 0.8
	RenderStyle Add
	Radius 2
	Height 2
	+NOBLOCKMAP
    +NOTELEPORT
    +DONTSPLASH
	+MISSILE
	+FORCEXYBILLBOARD
    +NOINTERACTION
	+NOGRAVITY
	+BOUNCEONCEILINGS
	+BOUNCEONWALLS
	+BOUNCEONFLOORS
	+THRUACTORS
	+MTHRUSPECIES
	Speed 10
	Scale 0.5
	
	States
	{
		Spawn:
			SMKG BCDEFGHIJKLM 4 bright 
			stop
	}
}

ACTOR NerveGas_SteamUp // Hm...
{

	Height 1
	Radius 1
	RenderStyle Add
	Scale 0.4
	Alpha 0.65
	+NOBLOCKMAP
    +NOTELEPORT
    +DONTSPLASH
	+MISSILE
	+FORCEXYBILLBOARD
    +NOINTERACTION
	+NOGRAVITY
	+BOUNCEONCEILINGS
	+BOUNCEONWALLS
	+BOUNCEONFLOORS
	+THRUACTORS
	+MTHRUSPECIES
	+ClientsideOnly
	Scale 0.5
	
	States
	{
		Spawn:
			STEM ABCDEFGHIJKL 1 bright 
			stop
	}
}

Actor NerveGas_Hurt
{
	//PoisonDamage 3, 15, 3
	+THRUACTORS
	+NOGRAVITY
	+NODAMAGETHRUST
	+DONTSPLASH
	+MTHRUSPECIES
	DamageType			MarineFire2
	Obituary "%o thought standing in %k's green gas cloud seemed smart."
	args 0, 0
	States
	{
	Spawn:
		TNT1 A 0 A_Explode(10, 32 + args[0], 0, true, 32 + args[0])
		TNT1 A 0 A_SetArg(0, args[0] + 2)
		TNT1 A 0 A_SetArg(1, args[1] + 1)
		TNT1 A 4 A_JumpIf(args[1] > 35, "Death")
		loop
	Death:
		TNT1 A 1
		Stop
	}
}

Actor NerveGas_HurtQuick : NerveGas_Hurt
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1 A_Explode(2, 32, 0, true, 32)
		TNT1 A 1 A_Explode(2, 48, 0, true, 48)
		TNT1 A 1 A_Explode(2, 64, 0, true, 64)
	Death:
		goto SUPER::DEATH
	}
}

ACTOR Grenade_MinePlacer
{
	Radius				16
	Height				8
	
	Speed				40
	Gravity				1.0
	Mass				100
	DamageType			MarineFire2
	
	Obituary			"%0 was blowen to bits by %k's Flinger."
	
	Projectile
	+NOBLOCKMAP
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	+DOOMBOUNCE
	+MTHRUSPECIES
	+UseBounceState
	+SeekerMissile
	+SCREENSEEKER
	+DONTFALL //Stops it from falling after sticking to targets.
    -NOGRAVITY
	bouncecount 16
	
	SeeSound "w9/fire1"
	DeathSound "w9_detonate"
	
	var int user_x;
	var int user_y;
	var int user_z;
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay Thing_ChangeTiD(0, 30000 + ACS_NamedExecuteWithResult("PlayerId"))
			TNT1 A 0 A_SeekerMissile(0, 1, SMF_LOOK | SMF_PRECISE, 256, 1)
			GRNS A 3 bright 
			TNT1 A 0 A_SeekerMissile(0, 1, SMF_LOOK | SMF_PRECISE, 256, 1)
			GRNS B 3 bright 
			TNT1 A 0 A_SeekerMissile(0, 1, SMF_LOOK | SMF_PRECISE, 256, 1)
			GRNS A 3 bright 
			TNT1 A 0 A_SeekerMissile(0, 1, SMF_LOOK | SMF_PRECISE, 256, 1)
			GRNS B 3 bright A_PlaySound("w9/fire2")
			goto Spawn
		StickToEnemy:
			TNT1 A 0 A_SetUserVar("user_x", random(-5, 5))
			TNT1 A 0 A_SetUserVar("user_y", random(-5, 5))
			TNT1 A 0 A_SetUserVar("user_z", random(0, 10))
			TNT1 A 0 A_PlaySound("w9/fire2")
		ImStuck:
			NULL A 0 A_Stop
			TNT1 A 0 Thing_ChangeTiD(0, 30000 + ACS_NamedExecuteWithResult("PlayerId"))
			GRNS D 1 Bright A_WARP(AAPTR_TRACER, 5.0 * user_x,5.0 * user_y,5.0 * user_z, 0, WARPF_NOCHECKPOSITION)
		goto ImStuck
		
		Death:
			NULL A 0 A_Stop
			GRNS D 4 bright A_SpawnItem ("Grenade_Mine" , 0, 0, 20.0, 0)
			stop
		XDeath:
			TNT1 A 0 A_Stop
			goto StickToEnemy
		Bounce.Wall:
			NULL A 0 A_Stop
			GRNS D 1 A_SpawnItem ("Grenade_Mine" , 0, 0, 0, 0)
			stop
		Bounce.Ceiling:
			NULL A 0 A_Stop
			GRNS D 1 A_SpawnItem ("Grenade_Mine" , 0, 0, 0, 0)
			stop
		
		Kaboom:
			GRNS D 1 bright A_SpawnItem("Grenade_Mine_GoBoom")
			stop
	}
}

ACTOR Grenade_Mine
{
	Game Doom
	Health 20
	Radius 5
	Height 34
	Speed 0
	+NOBLOOD
	+MTHRUSPECIES
	+noclip
	DeathSound "world/barrelx"
	Obituary "$OB_BARREL" // "%o went boom."
	ExplosionRadius			128
	ExplosionDamage			32
	DamageType			MarineFire2
	projectile
	States
	{
		Spawn:
			GRNS CD 6 Thing_ChangeTiD(0, 30000 + ACS_NamedExecuteWithResult("PlayerId"))
			goto Spawn
		Death:
			GRNS C 5 BRIGHT A_PlaySound("w9_detonate")
			GRNS C 5 BRIGHT A_Scream
			MISL B 1 BRIGHT A_SpawnItem("Grenade_Mine_GoBoom")
			Stop
	}
}

ACTOR Grenade_Mine_GoBoom
{
	health 2
	radius 1
	height 1
	scale 0.5
	Mass				100
	Gravity				0.5
	
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	+DOOMBOUNCE
	+FORCERADIUSDMG
	+MTHRUSPECIES
	+NODAMAGETHRUST
	bouncecount 16
	DamageType			MarineFire2
	
	States
	{
		Spawn:
			GRNS A 1
			MISL E 0 A_PlaySound("Flinger/Grenade/Booms")
			MISL E 0 A_Explode(60,192,0,0,96)
			MISL BCD 3
			stop
	}
}

ACTOR Grenade_GLauncher
{
	Radius				16
	Height				8
	
	Speed				40
	Damage				50
	Gravity				1.0
	Mass				100
	
	Obituary			"%0 was blowen to bits by %k's Grenade Launcher."
	
	DamageType			MarineFire
	
	+NOBLOCKMAP
	+DROPOFF
	+MISSILE
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	+DOOMBOUNCE
	+FORCERADIUSDMG
	+NODAMAGETHRUST
	+MTHRUSPECIES
	bouncecount 16
	
	SeeSound "w9/fire1"
	DeathSound "Flinger/Grenade/Booms"
	
	States
	{
		Spawn:
			GRNS ABA 3 bright
			GRNS B 3 bright A_PlaySound("w9/fire2")
			goto Spawn
		Death:
			MISL B 8 bright A_Explode(250,128,0,0,16)
			MISL C 6 bright
			MISL D 4 bright
			stop
	}
}

//////

//////

Actor Grenade_ShellBlast {
	Radius				16
	Height				8
	
	Speed				40
	Gravity				1.0
	Mass				100
	DamageType			MarineFire2
	Damage 30
	
	Obituary			"%o got a lot of lead by %k's shell grenade."
	
	Projectile
	+NOBLOCKMAP
	+DROPOFF
	+MISSILE
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	+DOOMBOUNCE
	-NOGRAVITY
	+DONTGIB
	bouncecount 16
	
	DeathSound "w2/fire"
	
	States
	{
		Spawn:
			GRE2 ABA 3 bright
			GRE2 B 3 bright A_PlaySound("w9/fire2")
			goto Spawn
		Death:
			
			PUFF B 1 A_Explode(30,64,0,0,16)
			TNT1 A 0 a_setScale(1.5, 1.5)
			PUFF B 1 A_SetArg(3, 0)
		RelaseThePeels:
			TNT1 A 0 A_Jumpif(args[3] >= 30, "ChangeTheWorld_MyFinalMessage_Goodbye")
			TNT1 A 0 A_SetArg(0, cos(frandom(0.0,360.0)) * (80) *  (2*random(0,1) - 1))
			TNT1 A 0 A_SetArg(1, sin(frandom(0.0,360.0)) * (80) *  (2*random(0,1) - 1))
			TNT1 A 0 A_SetArg(2, cos(frandom(0.0,360.0)) * (80) *  (2*random(0,1) - 1))
			TNT1 A 0 A_SetArg(3, args[3] + 1)
			TNT1 A 0 A_SpawnitemEx("ShellBlast_Pellet",0,0,0,args[0],args[1],args[2], 0,SXF_ABSOLUTEVELOCITY | SXF_ABSOLUTEANGLE)
			loop
		ChangeTheWorld_MyFinalMessage_Goodbye:
			PUFF B 6 bright 
			PUFF C 6 bright
			PUFF D 4 bright
			stop
	}

}


Actor ShellBlast_PelletTracer
{
  Radius 0
  Height 0
  //Gravity 100
  Renderstyle Add
  +FORCEXYBILLBOARD
  	+NOBLOCKMAP
	+FloorClip
	+SPECIALFLOORCLIP
	+NOGRAVITY
	+NOTELEPORT
    +DoomBounce
	+ClientsideOnly
  States
  {
   Melee:
   Spawn:
		PUFF AA 1
		stop
  }
}

Actor ShellBlast_Pellet	: FastProjectile {
	Radius				11
	Height				8
	Speed				5
	Damage				(5)
	DamageType			MarineFire
	RenderStyle			Add
	Projectile
	+MTHRUSPECIES
	DeathSound "weapons/rico"
	MissileType "ShellBlast_PelletTracer"
	MissileHeight 8
	//DeathSound		"weapons/rocklx"
	Obituary			"%o ate some of %k's lead."
	
	States
	{
		Spawn:
			PUFF A 1 
			Loop
		Death:
			PUFF A 1 A_SpawnItem("Bulletpuff_Standard") 
			stop
	}
}
