ACTOR Boomstick : FrenzyWeapon
{
	Weapon.AmmoUse 			1
	Weapon.AmmoGive 		8
	Weapon.AmmoType			"Shell"
	Inventory.PickupMessage		"You got the Boomstick!"
	Obituary			"%o got blasted by %k's Boomstick."
	
	Weapon.UpSound			"w2/load"
	AttackSound			"w2/fire"
	Tag "Boomstick Shotgun"
	Weapon.SelectionOrder 2500
	Weapon.SlotPriority 1.0

	States
	{	
		Ready: 
			W2E1 A 0 
            goto Ready.Raise
		
		Select:
			W2E1 A 0
		goto Super::Select

		Deselect:
			W2E1 A 0
		goto Super::Deselect

		Ready.Main:
			"####" "#" 0 A_JumpIfInventory("Input_Key", 1, "Ready.Inputs")
			W2E1 A 1 A_WeaponReady
		loop

		Fire:
            "####" "#" 0 offset (0,32)  A_GiveInventory("WeaponToken_Rised", 1)
            "####" "#" 0 A_JumpIfNoAmmo("Ready.Main")
		Fire.Main:
			W2E1 A 3 A_GunFlash
			W2E1 A 0 A_FireBullets(6, 6, 7 + CallACS("Player_GetRank", 0), 7 + 2*CallACS("Player_GetRank", 0), "BulletPuff_Standard")
			W2E1 A 1 offset (0,37)
			W2E1 A 1 offset (0,43)
			W2E1 A 1 offset (0,45)
			W2E1 A 0 A_FireCustomMissile("splatShellCasing", 45 + random(-8,8), 0, 2, 3)
			W2E1 A 1 offset (0,42)
			W2E1 A 1 offset (0,38)
			W2E1 A 1 offset (0,34)
			W2E1 A 1 offset (0,32)
			W2E1 BC 4 
			W2E1 D 4 A_PlayWeaponSound("w2/load")
			W2E1 D 0 A_FireCustomMissile("splatShellCasing", 45 + random(-8,8), 0, 2, 3)
			W2E1 CB 4 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
			W2E1 A 0 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
			W2E1 A 0 A_Refire
			W2E1 A 5 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
			goto Ready
		Flash:
			SHTF A 1 offset(1,32) bright A_Light1
			SHTF B 1 offset(0,0) Bright A_Light2
			SHTF A 1 offset(0,0) bright A_Light1
			Goto LightDone
		
	}
}

ACTOR Boomstick_Full : FrenzyWeapon
{
	Weapon.AmmoUse 			1
	Weapon.AmmoGive 		8
	Weapon.AmmoType			"Shell"
	Inventory.PickupMessage		"You got the Full Boomstick!"
	Obituary			"%o got blasted by %k's Boomstick."
	
	Weapon.UpSound			"weapons/sawup"
	Weapon.ReadySound		"weapons/sawidle"
	AttackSound			"w2/fire2"
	DamageType			MarineFire
	Weapon.SelectionOrder 2300
	Weapon.SlotPriority 2.0
	
	Tag "Combat Boomstick Shotgun"
	
	States
	{	
		Ready: 
			W2E2 A 0
            goto Ready.Raise
		
		Select:
			W2E2 A 0
		goto Super::Select

		Deselect:
			W2E2 A 0
		goto Super::Deselect

		// Because We need to check input in each tic on animated idle weapons, we gotta check for each sprite with a small inventory variable.
		Ready.Main:
			W2E2 A 0 A_PlaySound("weapons/sawidle")
			"####" "#" 0 A_JumpIfInventory("Input_Key", 1, "Ready.Inputs")
			W2E2 A 1 A_WeaponReady
			"####" "#" 0 A_JumpIfInventory("Input_Key", 1, "Ready.Inputs")
			W2E2 A 1 A_WeaponReady
			"####" "#" 0 A_JumpIfInventory("Input_Key", 1, "Ready.Inputs")
			W2E2 B 1 A_WeaponReady
			"####" "#" 0 A_JumpIfInventory("Input_Key", 1, "Ready.Inputs")
			W2E2 B 1 A_WeaponReady
			loop

		Fire:
            "####" "#" 0 offset (0,32)  A_GiveInventory("WeaponToken_Rised", 1)
            "####" "#" 0 A_JumpIfNoAmmo("Ready.Main")
		Fire.Main:
			W2E2 A 3 A_GunFlash
			W2E2 A 0 A_FireBullets(7, 7, 7 + CallACS("Player_GetRank", 0), 18 + 3*CallACS("Player_GetRank", 0), "BulletPuff_Explosive")
			W2E2 A 1 offset (0,37)
			W2E2 A 1 offset (0,43)
			W2E2 A 1 offset (0,45)
			W2E2 A 0 A_FireCustomMissile("splatShellCasing", 45 + random(-8,8), 0, 2, 3)
			W2E2 A 1 offset (0,42)
			W2E2 A 1 offset (0,38)
			W2E2 A 1 offset (0,34)
			W2E2 A 1 offset (0,32)
			W2E2 BC 6
			W2E2 D 0 A_PlayWeaponSound("w2/load")
			W2E2 D 5 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
			W2E2 D 0 A_FireCustomMissile("splatShellCasing", 45 + random(-8,8), 0, 2, 3)
			W2E2 CB 4 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
			W2E2 A 5 A_Refire
			goto Ready

		AltFire:
            "####" "#" 0 offset (0,32)  A_GiveInventory("WeaponToken_Rised", 1)
            "####" "#" 0 A_JumpIfNoAmmo("Ready.Main")
		AltFire.Main:
			W2E2 AB 2 A_Saw("weapons/sawfull", "weapons/sawhit", 15, "Saw_Spread", SF_NOUSEAMMO, 90)
			goto Ready
		Flash:
			TNT1 A 0 A_Saw(0, "weapons/sawhit", 15, "Saw_Spread", SF_NOUSEAMMO, 90)
			SHTF A 1 offset(1,32) bright A_Light1
			TNT1 A 0 A_Saw(0, "weapons/sawhit", 15, "Saw_Spread", SF_NOUSEAMMO, 90)
			SHTF B 1 offset(0,0) Bright A_Light2
			TNT1 A 0 A_Saw(0, "weapons/sawhit", 15, "Saw_Spread", SF_NOUSEAMMO, 90)
			SHTF A 1 offset(0,0) bright A_Light1
			TNT1 AAAAAAAAAA 2 A_Saw(0, "weapons/sawhit", 15, "Saw_Spread", SF_NOUSEAMMO, 90)
			Goto LightDone

	}
}