ACTOR SF_Railgun : Weapon
{
	Weapon.AmmoUse 		        2
	Weapon.AmmoGive 		50
	Weapon.AmmoType			"Cell"
	Inventory.PickupMessage		"You got the Laser Railgun!"
	Obituary			"%o got railed by %k's laser railgun."
	
	Weapon.UpSound			"w/load6"
	AttackSound			"x1/fire1"
	//[Samu] No ripping, booo :v
	Tag "Laser Railgun"
	Weapon.SlotPriority 1.0
	Weapon.SelectionOrder 2500
	States
	{
		Ready:
			X1E1 A 1 A_WeaponReady
			goto Ready
		Deselect:
			X1E1 A 1 A_Lower
			X1E1 A 0 A_Lower
			goto Deselect
		Select:
			X1E1 A 1 A_Raise
			X1E1 A 0 A_Raise
			goto Select
		Fire:
			TNT1 A 0 A_TakeInventory("Railgun_Passthrough", 9999)
			X1E1 A 2 A_GunFlash
			// 20 enemies pass-through.
		Fire.PassThorugh:
			TNT1 A 0 A_JumpIfInventory("Railgun_Passthrough", 19, "Fire.Rest")
			TNT1 B 0 A_RailAttack (Random(1,3)*5, 0, 0, "None", "None", RGF_NOPIERCING | RGF_SILENT, 0, "InvisiblePuff",0,0,0,0,10.0,0)	
			TNT1 A 0 A_GiveInventory("Railgun_Passthrough", 1)
			loop
		Fire.Rest:
			TNT1 A 0 A_TakeInventory("Railgun_Passthrough", 9999)
			X1E1 B 0 A_RailAttack (Random(1,3)*5, 0, 1, "", "", RGF_NOPIERCING, 0, "Rail_Puff",0,0,0,0,10.0,0,"Rail_Standard_Tracer")
			X1E1 BCB 1
			X1E1 A 4
			X1E1 A 4 A_Refire
			goto Ready
		Flash:
			X1E1 D 2 bright
			X1E1 E 2 bright
			stop
			
		FireRockets:
			TNT1 A 1
		goto lightdone
	}
}

Actor Railgun_Passthrough : Integer { Inventory.MaxAmount 100}
Actor Railgun_Taser_WindUp : Integer { Inventory.MaxAmount 10}

ACTOR Railgun_Taser : Weapon
{
	Weapon.AmmoUse1			2
	Weapon.AmmoUse2 		8
	Weapon.AmmoGive 		50
	Weapon.AmmoType1		"Cell"
	Weapon.AmmoType2		"Cell"
	Inventory.PickupMessage		"You got the Spear-Taser Railgun!"
	Obituary			"%o was cut by %k's spear-taser railgun."
	
	Weapon.UpSound			"w/load6"
	//AttackSound			"x1/fire1"
	Tag "Spear-Taser Railgun"
	Weapon.SlotPriority 2.0
	Weapon.SelectionOrder 2300
	States
	{
		Ready:
			X1E2 A 1 A_WeaponReady
			goto Ready
		Deselect:
			TNT1 A 0 A_TakeInventory("Railgun_Taser_WindUp", 9999)
			TNT1 A 0 A_StopSoundEx (soundslot7)
			X1E2 A 1 A_Lower
			X1E2 A 0 A_Lower
			goto Deselect
		Select:
			TNT1 A 0 A_TakeInventory("Railgun_Taser_WindUp", 9999)
			TNT1 A 0 A_StopSoundEx (soundslot7)
			X1E2 A 1 A_Raise
			X1E2 A 0 A_Raise
			goto Select

		Fire:
			TNT1 A 0 A_JumpIfNoAmmo("Ready")
			TNT1 A 0 A_PlaySoundEx ("Railgun_Taser/StartLaser", soundslot7, 1)
			X1E2 A 1 A_GunFlash
			X1E2 B 2 
			X1E2 C 2 A_RailAttack (10 + 5*random(0,2), 0, 0, "", "", RGF_SILENT, 0, "Rail_SpearPuff",0,0,1024,0,10.0,0,"Rail_Laser_Tracer1")
			X1E2 A 1 
			X1E2 B 2 
			X1E2 C 2 A_RailAttack (10 + 5*random(0,2), 0, 1, "", "", RGF_NOPIERCING | RGF_SILENT, 0, "Rail_SpearPuff",0,0,1024,0,10.0,0,"Rail_Laser_Tracer1")
			TNT1 A 0 A_GiveInventory("Railgun_Taser_WindUp", 1)
			TNT1 A 0 A_JumpIfInventory("Railgun_Taser_WindUp", 7, "Hold")
			TNT1 A 0 A_JumpIfNoAmmo(2)
			X1E2 A 2 A_Refire("Fire")
			TNT1 A 0 A_StopSoundEx (soundslot7)
			TNT1 A 0 A_PlaySoundEx ("Railgun_Taser/Stop", soundslot9)
			TNT1 A 0 A_TakeInventory("Railgun_Taser_WindUp", 9999)
			X1E2 BABCB 5 A_SpawnItem(Smoke_Spawner, 0, -20, 0)
			goto Ready
		Hold:
			X1E2 B 2 A_Jump(256, "Hold.Tracer1", "Hold.Tracer2")
			goto Hold.Tracer1
		Hold.Tracer1:
			X1E2 A 0 A_GunFlash
			X1E2 C 2 A_RailAttack (10 + 5*random(0,4), 0, 0, "", "", RGF_SILENT, 0, "Rail_SpearPuff",0,0,1024,0,10.0,0,"Rail_Laser_Tracer1")
			TNT1 A 0 A_GunFlash
			X1E2 B 2 A_RailAttack (10 + 5*random(0,4), 0, 0, "", "", RGF_NOPIERCING | RGF_SILENT, 0, "Rail_SpearPuff",0,0,1024,0,10.0,0,"Rail_Laser_Tracer1")
			TNT1 AA 0 A_RailAttack (10 + 5*random(0,4), 0, 1, "", "", RGF_NOPIERCING | RGF_SILENT, 0, "Rail_SpearPuff",0,0,1024,0,10.0,0,"Rail_Laser_Tracer1")
			X1E2 C 2 
			TNT1 A 0 A_PlaySoundEx ("Railgun_Taser/LoopLaser", soundslot7, 1)
			TNT1 A 0 A_JumpIfNoAmmo(2)
			X1E2 A 4 A_Refire("Hold")
			goto Hold.End
		Hold.Tracer2:
			X1E2 A 0 A_GunFlash
			X1E2 C 2 A_RailAttack (10 + 5*random(0,4), 0, 0, "", "", RGF_SILENT, 0, "Rail_SpearPuff",0,0,1024,0,10.0,0,"Rail_Laser_Tracer2")
			TNT1 A 0 A_GunFlash
			X1E2 B 2 A_RailAttack (10 + 5*random(0,4), 0, 0, "", "", RGF_NOPIERCING | RGF_SILENT, 0, "Rail_SpearPuff",0,0,1024,0,10.0,0,"Rail_Laser_Tracer2")
			TNT1 AA 0 A_RailAttack (10 + 5*random(0,4), 0, 1, "", "", RGF_NOPIERCING | RGF_SILENT, 0, "Rail_SpearPuff",0,0,1024,0,10.0,0,"Rail_Laser_Tracer2")
			X1E2 C 2 
			TNT1 A 0 A_PlaySoundEx ("Railgun_Taser/LoopLaser", soundslot7, 1)
			TNT1 A 0 A_JumpIfNoAmmo(2)
			X1E2 A 4 A_Refire("Hold")
			goto Hold.End
		Hold.End:
			TNT1 A 0 A_StopSoundEx (soundslot7)
			TNT1 A 0 A_PlaySoundEx ("Railgun_Taser/Stop", soundslot9)
			TNT1 A 0 A_TakeInventory("Railgun_Taser_WindUp", 9999)
			X1E2 BABCB 5 A_SpawnItem(Smoke_Spawner, 0, -20, 0)
			goto Ready

		AltFire:
			X1E2 A 1 A_PlaySoundEx ("x1/taser", soundslot8)
			X1E2 A 1 A_GunFlash
			X1E2 BC 2 A_RailAttack (20, 0, 0, "", "", RGF_SILENT, 0, "Rail_TaserPuff",0,0,512,0,20.0,0,"Rail_Taser_Tracer1")
			TNT1 A 0 A_GunFlash
			X1E2 BC 2 A_RailAttack (20, 0, 0, "", "", RGF_NOPIERCING | RGF_SILENT, 0, "Rail_TaserPuff",0,0,512,0,20.0,0,"Rail_Taser_Tracer1")
			TNT1 A 0 A_GunFlash
			X1E2 B 2 A_RailAttack (20, 0, 0, "", "", RGF_NOPIERCING | RGF_SILENT, 0, "Rail_TaserPuff",0,0,512,0,20.0,0,"Rail_Taser_Tracer1")
			X1E2 C 2 A_RailAttack (20, 0, 1, "", "", RGF_NOPIERCING | RGF_SILENT, 0, "Rail_TaserPuff",0,0,512,0,20.0,0,"Rail_Taser_Tracer1")
			X1E2 A 4 A_Refire("AltFire")
			goto Ready
		Flash:
			X1E1 D 2 bright
			X1E1 E 2 bright
			stop
			
		FireRockets:
			TNT1 A 1
		goto lightdone
	}
}

ACTOR Railgun_Slug : Weapon
{
	Weapon.AmmoUse1			2
	Weapon.AmmoUse2 		8
	Weapon.AmmoGive 		50
	Weapon.AmmoType1		"Cell"
	Weapon.AmmoType2		"Cell"
	Inventory.PickupMessage		"You got the Double slug Railgun!"
	Obituary			"%o was pierced by %k's double-slug rails."
	
	Weapon.UpSound			"w/load6"
	AttackSound			"x1/fire1"
	Tag "Double Slug Railgun"
	Weapon.SlotPriority 2.0
	Weapon.SelectionOrder 2300
	States
	{
		Ready:
			X1E3 A 1 A_WeaponReady
			goto Ready
		Deselect:
			X1E3 A 1 A_Lower
			X1E3 A 0 A_Lower
			goto Deselect
		Select:
			X1E3 A 1 A_Raise
			X1E3 A 0 A_Raise
			goto Select

		Fire:
			TNT1 A 0 A_TakeInventory("Railgun_Passthrough", 9999)
			X1E3 A 2 A_GunFlash
			// 30 enemies pass-through.
			TNT1 A 0 A_RailAttack (Random(2,3)*3*15, -6, 0, "None", "None", RGF_SILENT, 0, "InvisiblePuff")	
			TNT1 A 0 A_RailAttack (Random(2,3)*3*15, 6, 0, "None", "None", RGF_SILENT, 0, "InvisiblePuff")	
		Fire.PassThorugh:
			TNT1 A 0 A_JumpIfInventory("Railgun_Passthrough", 15, "Fire.Rest")
			TNT1 A 0 A_RailAttack (Random(2,3)*3, -6, 0, "None", "None", RGF_NOPIERCING | RGF_SILENT, 0, "InvisiblePuff")	
			TNT1 A 0 A_RailAttack (Random(2,3)*3, 6, 0, "None", "None", RGF_NOPIERCING | RGF_SILENT, 0, "InvisiblePuff")	
			TNT1 A 0 A_GiveInventory("Railgun_Passthrough", 1)
			loop
		Fire.Rest:
			TNT1 A 0 A_TakeInventory("Railgun_Passthrough", 9999)
			TNT1 A 0 A_RailAttack (Random(2,3)*3, -6, 0, "0C 00 FF", "ff ff ff", RGF_SILENT, 0, "BulletPuff_Player")	
			TNT1 A 0 A_RailAttack (Random(2,3)*3, 6, 0, "0C 00 FF", "ff ff ff", RGF_SILENT, 0, "BulletPuff_Player")
			TNT1 A 0 A_RailAttack (Random(2,3)*3, -6, 1, "", "", 0, 0, "Rail_Puff",0,0,0,0,16.0,0,"Rail_Slug_Tracer")
			TNT1 A 0 A_RailAttack (Random(2,3)*3, 6, 1, "", "", 0, 0, "Rail_Puff",0,0,0,0,16.0,0,"Rail_Slug_Tracer")
			X1E3 BCB 1
			X1E3 A 3
			X1E3 A 4 A_Refire("Fire")
			goto Ready
			
		AltFire:
			X1E3 A 2 A_GunFlash
			X1E3 B 0 A_RailAttack (250, 1, 1, "0C 00 FF", "ff ff ff", 0, 0, "BulletPuff_Player")		// damage from 60 to 100
			X1E3 B 0 A_RailAttack (250, -1, 1, "0C 00 FF", "ff ff ff", 0, 0, "BulletPuff_Player")		// damage from 60 to 100
			X1E3 BCB 5
			X1E3 A 4 A_Refire("AltFire")
			goto Ready
		Flash:
			X1E3 D 2 bright
			X1E3 E 2 bright
			stop
			
		FireRockets:
			TNT1 A 1
		goto lightdone
	}
}