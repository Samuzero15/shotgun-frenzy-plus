Actor Patcher : Weapon
{
  Weapon.SlotPriority 1.0
  Weapon.SelectionOrder 2500
  Inventory.PickupMessage "You found the Patcher Gun!"
  obituary "%o was shocked by %k's Patcher Gun, which it should'nt happen, because this gun is for cooperative, so, wtf just happened?"
  Weapon.UpSound "w/load1"
  +WEAPON.NOAUTOAIM
  states
  {
	Spawn:
		PLSP A -1
		loop
	Ready:
		TNT1 A 0 ACS_NamedExecuteAlways("Patcher_WeaponLoop", 0)
	Ready.Loop:
		TNT1 A 0 A_JumpIfInventory("Patcher_Mode", 2, "Ready.Sell")
		TNT1 A 0 A_JumpIfInventory("Patcher_Mode", 1, "Ready.Move")
		PLPI A 1 A_WeaponReady
		loop
	Ready.Move:
		PLPI D 1 A_WeaponReady
		goto Ready.Loop
	Ready.Sell:
		PLPI G 1 A_WeaponReady
		goto Ready.Loop
	Select:
		PLPI A 1 A_Raise
		PLPI A 0 A_Raise
		loop
	Deselect:
		PLPI A 1 A_Lower
		PLPI A 0 A_Lower
		loop

	Fire:
		PLPI A 0
	Hold:
		PLPI B 3
		PLPI C 4 A_FireBullets(0, 0, 1, 1, "Patcher_FixPuff", FBF_NOFLASH, 256)
		BLAG B 0 A_ReFire
		PLPI A 8
		goto Ready
	Altfire:
		PLPI A 0
	AltHold:
		TNT1 A 0 A_JumpIfInventory("Patcher_Mode", 2, "AltHold.Sell")
		TNT1 A 0 A_JumpIfInventory("Patcher_Mode", 1, "AltHold.Move")
		PLPI B 6
		PLPI C 8 A_FireBullets(0, 0, 1, 1, "Patcher_UpgradePuff", FBF_NOFLASH, 256)
		PLPI A 8 A_ReFire
		goto Ready.Loop
	AltHold.Move:
		PLPI E 2 A_FireBullets(0, 0, 1, 1, "Patcher_MovePuff_Preview", FBF_NOFLASH, 256) 
		BLAG B 0 A_ReFire
		PLPI F 2 A_FireBullets(0, 0, 1, 1, "Patcher_MovePuff", FBF_NOFLASH, 256) 
		PLPI D 8
		goto Ready.Loop
	AltHold.Sell:
		PLPI HG 3
		PLPI G 0 A_ReFire
		PLPI I 8 A_FireBullets(0, 0, 1, 1, "Patcher_SellPuff", FBF_NOFLASH, 256)
		goto Ready.Loop
  }
}

Actor Patcher_Advanced : Patcher
{
	Weapon.SelectionOrder 2300
	states{

	Ready:
		TNT1 A 0  ACS_NamedExecuteAlways("Patcher_WeaponLoop", 0)
		goto Super::Ready.Loop

	Fire:
		PLPI A 0
	Hold:
		PLPI B 2
		PLPI C 2 A_FireBullets(0, 0, 1, 1, "Patcher_FixPuff", FBF_NOFLASH, 512)
		BLAG B 0 A_ReFire
		PLPI A 8
		goto Ready
	Altfire:
		PLPI A 0
	AltHold:
		TNT1 A 0 A_JumpIfInventory("Patcher_Mode", 2, "AltHold.Sell")
		TNT1 A 0 A_JumpIfInventory("Patcher_Mode", 1, "AltHold.Move")
		PLPI B 3
		PLPI C 4 A_FireBullets(0, 0, 1, 1, "Patcher_UpgradePuff", FBF_NOFLASH, 512)
		TNT1 B 0 A_ReFire
		PLPI A 8 
		goto Ready
	AltHold.Move:
		PLPI E 2 A_FireBullets(0, 0, 1, 1, "Patcher_MovePuff_Preview", FBF_NOFLASH, 512) 
		TNT1 A 0 A_ReFire
		PLPI F 2 A_FireBullets(0, 0, 1, 1, "Patcher_MovePuff", FBF_NOFLASH, 512) 
		PLPI D 8
		goto Ready
	AltHold.Sell:
		PLPI HG 3
		PLPI G 0 A_ReFire
		PLPI I 8 A_FireBullets(0, 0, 1, 1, "Patcher_SellPuff", FBF_NOFLASH, 512)
		goto Ready.Loop
	}
}
ACTOR Patcher_Puff
{
	+NOINTERACTION
	+BLOODLESSIMPACT
	-ACTIVATEPCROSS
	+ALWAYSPUFF
	-RANDOMIZE
	DamageType			"IdlePatcherDmg"
	VSpeed				1
	Mass				5
	
	States
	{
		Spawn:
			TNT1 A 1
			goto Melee
		Melee:
			TNT1 A 1
			stop
	}
}

ACTOR Patcher_FixPuff : Patcher_Puff
{
	DamageType			"PriPatcherDmg"
	States
	{
		Spawn:
			TNT1 A 1
			goto Melee
		Melee:
			PLSE ABC 1
			stop
	}
}

ACTOR Patcher_UpgradePuff : Patcher_Puff
{
	DamageType			"AltPatcherDmg"
	States
	{
		Spawn:
			TNT1 A 1
			goto Melee
		Melee:
			PLSE ABC 1
			stop
		Crash:
			TNT1 A 1
			stop
	}
}

ACTOR Patcher_MovePuff_Preview : Patcher_UpgradePuff
{
	+PUFFGETSOWNER
	States
	{
		Crash:
			TNT1 A 0 ACS_NamedExecuteAlways("Building_MoveTo_Preview", 0)
			PLSE CBA 1
			Stop
	}
}

ACTOR Patcher_MovePuff : Patcher_UpgradePuff
{
	+PUFFGETSOWNER
	States
	{
		Crash:
			TNT1 A 0 ACS_NamedExecuteAlways("Building_MoveTo_SpawnSpot", 0)
			PLSE CBA 1
			Stop
	}
}

Actor Building_MoveSpot{
	Mass 0xFFFFFFFF
	radius 16
	height 1
	+NOBLOCKMAP
	+FLOORHUGGER
	States{
		Spawn:
			TNT1 A 8
			stop
	}
}

ACTOR Patcher_SellPuff : Patcher_UpgradePuff
{
	
}

Actor Patcher_Libra : CustomInventory {
	+Inventory.AlwaysPickup
	States{
		Spawn:
			TNT1 A 1
		stop
		Pickup:
			TNT1 A 0 A_FireBullets(0, 0, 1, 1, "Patcher_LibraPuff", FBF_NOFLASH, 256)
		stop
	}
}

Actor Patcher_LibraAdvanced : Patcher_Libra {
	States{
		Pickup:
			TNT1 A 0 A_FireBullets(0, 0, 1, 1, "Patcher_LibraPuff", FBF_NOFLASH, 512)
		stop
	}
}

Actor PatcherEffect_Fixed : Patcher_Libra {
	States{
		Pickup:
			TNT1 A 0 A_PlaySound("p/deploy")
			TNT1 A 0 A_SpawnItemEx("SparksEmitter_Deploy", 0, 0, 0, 0)
		stop
	}
}

Actor PatcherEffect_Upgraded : Patcher_Libra {
	States{
		Pickup:
			TNT1 A 0 A_SpawnItemEx("SparksEmitter_Deploy", 0, 0, 0, 0)
		stop
	}
}

Actor PatcherEffect_Fail : Patcher_Libra {
	States{
		Pickup:
			TNT1 A 0 A_PlaySound("SamuTerm/Error")
		stop
	}
}

Actor Patcher_MoveMe : Boolean {}
Actor Patcher_Mode : Integer {}

Actor Patcher_HudHeal : Boolean {}
Actor Patcher_HudMaxHP : Boolean {}
Actor Patcher_HudNoMoney : Boolean {}
Actor Patcher_Script : Integer {}
Actor Patcher_HudBuff : Boolean {}
Actor Patcher_HudBuffed : Boolean {}
Actor Patcher_FrameRefresh : Boolean {}
Actor Patcher_ScriptActive : Boolean {}
Actor Patcher_MoveNow : Boolean {}
