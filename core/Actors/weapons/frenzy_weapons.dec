
Actor WeaponToken_Rised : Boolean {}
Actor Input_Grenade : Boolean {}
Actor Input_Melee : Boolean {}
Actor Input_Key : Boolean {}
Actor WeaponToken_Tics : Integer {}
// Here it lies some common states for all weapons.

Actor InputCustomItem : CustomInventory {
    inventory.maxamount 1
	-Inventory.INVBAR
    +Inventory.UNDROPPABLE

    States
	{
	Spawn:
		STIM A 2 bright
		loop
	Use:
		TNT1 A 0
		fail
	}
}

ACTOR Input_GrenadeItem : InputCustomItem
{
inventory.pickupmessage "The input of a hand grenade action!"
	States
	{
	Use:
        TNT1 A 0 A_GiveInventory("Input_Key", 1)
		TNT1 A 0 A_GiveInventory("Input_Grenade")
		fail
	}
}

ACTOR Input_MeleeItem : InputCustomItem
{
inventory.pickupmessage "The input of a melee action!"

	States
	{
	Use:
        TNT1 A 0 A_GiveInventory("Input_Key", 1)
        TNT1 A 0 A_GiveInventory("Input_Melee")
		fail
	}
}

ACTOR Input_MeleeItemOff : InputCustomItem
{
inventory.pickupmessage "The input of a melee action!"
	States
	{
	Use:
        TNT1 A 0 A_TakeInventory("Input_Melee")
		fail
	}
}


ACTOR HandGrenade_Ammo : Ammo
{
	Inventory.Icon "GRNSA0"
	Inventory.Amount 1
	Inventory.MaxAmount 5
	
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 10
	
	States
	{
		Spawn:
			GRNS A -1
			stop
	}
}

ACTOR MeleeAttack : FastProjectile
{
	Radius 6
	Height 6
	Projectile 
	+FORCEXYBILLBOARD
	+NOEXTREMEDEATH
	+MTHRUSPECIES
	-BLOODLESSIMPACT
	+BLOODSPLATTER
	RenderStyle Add
	Alpha 0.6
    Damage 9
    Speed 32
	SeeSound "none"
	DeathSound "none"
	Decal "none"
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A 1 BRIGHT
		Stop
	Death:
		PUFF A 3 A_PlaySound("*fist")
		PUFF BCD 3
		Stop
	Crash:
		PUFF A 3 A_PlaySound("*fist")
		PUFF BCD 3
		Stop
	}
}

Actor FrenzyWeapon : Weapon {
    //+NOAUTOFIRE
    +Weapon.NoAutoAim
    Weapon.SwayStyle HorizontalOnly
    Weapon.ViewSwaySpeed 4
    Weapon.ViewPitchStyle Centered
    Weapon.BobStyle Smooth
    states {
        // InstaRise and insta lower
        Select:
            "####" "#" 0 A_TakeInventory("WeaponToken_Rised", 1)
            "####" "#" 0 A_Raise
        wait

        Deselect:
            "####" "#" 0 A_TakeInventory("WeaponToken_Rised", 1)
            "####" "#" 0 A_TakeInventory("WeaponToken_Tics", 9999)
            "####" "#" 1 offset(0, 38)
            "####" "#" 1 offset(0, 48)
            "####" "#" 1 offset(0, 64)
            "####" "#" 1 offset(0, 90)
            "####" "#" 0 A_Lower
        wait

        Ready.Raise:
            "####" "#" 0 A_JumpIfInventory("WeaponToken_Rised", 1, "Ready.Main")
            "####" "#" 1 offset (0,82) 
            "####" "#" 1 offset (0,76) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
            "####" "#" 0 A_JumpIfInventory("Fire_JustPressed", 1, "Fire")
            "####" "#" 1 offset (0,68) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
            "####" "#" 0 A_JumpIfInventory("Fire_JustPressed", 1, "Fire")
            "####" "#" 1 offset (0,60) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
            "####" "#" 0 A_JumpIfInventory("Fire_JustPressed", 1, "Fire")
            "####" "#" 1 offset (0,52) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
            "####" "#" 0 A_JumpIfInventory("Fire_Pri", 1, "Fire")
            "####" "#" 1 offset (0,46) A_WeaponReady(WRF_NOBOB)
            "####" "#" 0 A_JumpIfInventory("Fire_Pri", 1, "Fire")
            "####" "#" 1 offset (0,42) A_WeaponReady(WRF_NOBOB)
            "####" "#" 0 A_JumpIfInventory("Fire_Pri", 1, "Fire")
            "####" "#" 1 offset (0,38) A_WeaponReady(WRF_NOBOB)
            // The last offset.
		Ready.DoneRising:
            "####" "#" 0 offset (0,32) A_GiveInventory("WeaponToken_Rised", 1)
            "####" "#" 10 A_Jump(256, "Ready.Main")
            goto Ready

        Fire:
        Altfire:
        Ready:
        Ready.Main: 
            // Now here it is where we can do some raise animation 
            "####" "#" 0 A_JumpIfInventory("Input_Key", 1, "Ready.Inputs")
            "####" "#" 1 A_WeaponReady
        loop

        Ready.Inputs:
            "####" "#" 0 A_TakeInventory("Input_Key", 1)
            "####" "#" 0 A_JumpIfInventory("Input_Grenade", 1, "Grenade")
		    "####" "#" 0 A_JumpIfInventory("Input_Melee", 1, "Melee")
            "####" "#" 50 A_Jump(256, "Ready.Main")
        goto Ready.Main

        Grenade:
			"####" "#" 0 A_TakeInventory("Input_Grenade", 1)
            "####" "#" 1 A_WeaponReady
            "####" "#" 0 A_JumpIfInventory("HandGrenade_Ammo", 1, "Grenade.LowerWeapon")
            "####" "#" 50 A_Jump(256, "Ready.Main")
		goto Ready.Main

        Grenade.LowerWeapon:
            "####" "#" 1 offset(0, 38)
            "####" "#" 1 offset(0, 46)
            "####" "#" 1 offset(0, 60)
            "####" "#" 1 offset(0, 82)
        Grenade.UseAmmo:
            "####" "#" 0 A_TakeInventory("HandGrenade_Ammo", 1)
        Grenade.Fire:
            "####" "#" 5 A_Gunflash("Throw")
            "####" "#" 0 A_FireCustomMissile("HandGrenade_ShellBlast", 0, 0)
            
            "####" "#" 0 A_JumpIfInventory("Input_Grenade", 1, "Grenade.Again")
            "####" "#" 1 A_WeaponReady(WRF_NOBOB)
            "####" "#" 0 A_JumpIfInventory("Input_Grenade", 1, "Grenade.Again")
            "####" "#" 1 A_WeaponReady(WRF_NOBOB)
            "####" "#" 0 A_JumpIfInventory("Input_Grenade", 1, "Grenade.Again")
            "####" "#" 1 A_WeaponReady(WRF_NOBOB)
            "####" "#" 0 A_JumpIfInventory("Input_Grenade", 1, "Grenade.Again")
            "####" "#" 1 A_WeaponReady(WRF_NOBOB)
            "####" "#" 0 A_JumpIfInventory("Input_Grenade", 1, "Grenade.Again")
            "####" "#" 1 A_WeaponReady(WRF_NOBOB)
        goto Raise.Again

        Grenade.Again:
            "####" "#" 0 A_TakeInventory("Input_Grenade", 1)
            "####" "#" 0 A_JumpIfInventory("HandGrenade_Ammo", 1, "Grenade.UseAmmo")
        goto Raise.Again

        Raise.Again:
            "####" "#" 0 A_TakeInventory("Input_Grenade", 1)
            "####" "#" 1 offset(0, 82) A_WeaponReady(WRF_NOBOB)
            "####" "#" 1 offset(0, 60) A_WeaponReady(WRF_NOBOB)
            "####" "#" 1 offset(0, 46) A_WeaponReady(WRF_NOBOB)
            "####" "#" 1 offset(0, 38) A_WeaponReady(WRF_NOBOB)
            "####" "#" 10 A_Jump(256, "Ready.Main")
        goto Ready.Main

        Melee:
            "####" "#" 0
            "####" "#" 1 offset(0, 38) 
            "####" "#" 1 offset(0, 46)
            "####" "#" 1 offset(0, 60)
            "####" "#" 1 offset(0, 82)
            "####" "#" 0 A_Gunflash("Punch")
            "####" "#" 6 A_FireCustomMissile("MeleeAttack", 0, 0, 0)
            "####" "#" 4 A_WeaponReady(WRF_NOBOB)
            "####" "#" 0 A_JumpIfInventory("Input_Melee", 1, "Melee.Again")
            goto Raise.Again

        Melee.Again:
            "####" "#" 0 
            Goto Melee+4

        Throw:
            HAND ABCDE 1
            goto LightDone
        
        Punch:
            PUCH BCD 2
            PUCH CB 2 A_JumpIfInventory("Fire_JustPressed", 1, "LightDone")
            goto LightDone

        FireRockets:
			TNT1 A 1
		goto lightdone
    }
}

#include "Actors/weapons/frenzy_boomstick.dec"
#include "Actors/weapons/frenzy_buckshot.dec"
#include "Actors/weapons/frenzy_extra.dec"
#include "Actors/weapons/frenzy_flamer.dec"
#include "Actors/weapons/frenzy_flinger.dec"
#include "Actors/weapons/frenzy_launcher.dec"
#include "Actors/weapons/frenzy_pistol.dec"
#include "Actors/weapons/frenzy_plasmagun.dec"
#include "Actors/weapons/frenzy_quadshot.dec"
#include "Actors/weapons/frenzy_railgun.dec"
#include "Actors/weapons/frenzy_spitter.dec"
#include "Actors/weapons/frenzy_ssg.dec"
#include "Actors/weapons/frenzy_repeater.dec"
#include "Actors/weapons/frenzy_dualshot.dec"
#include "Actors/weapons/frenzy_bfg.dec"

#include "Actors/weapons/frenzy_healnstock.dec"
#include "Actors/weapons/fp_tankguns.dec"
#include "Actors/weapons/fp_patcher.dec"