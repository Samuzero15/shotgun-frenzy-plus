actor VoidDarkImp : FPlus_Monster 3101
{
spawnid 241
	//$Category Monsters
    Health 130
    Radius 20
    Height 56
    Speed 7
    PainChance 200
    +FLOORCLIP
    SeeSound "imp/sight"
    PainSound "imp/pain"
    DeathSound "imp/death"
    ActiveSound "imp/active"
    MeleeSound "imp/melee"
    Obituary "%o was cursed by a dark imp."
    HitObituary "%o was touched by a dark imp."
    MissileType DarkNormal
    MeleeDamage 3
	
    States
    {
    Spawn:
        DRKV AB 10 A_Look
        goto Spawn
    See:
        DRKV AABBCCDD 3 A_Chase
        goto See
    Melee:
        DRKV EF 8 A_FaceTarget
        DRKV G 6 A_ComboAttack
        Goto See
    Missile:
        DRKV E 0 A_Jump(64,4)
        DRKV EF 8 A_FaceTarget
        DRKV G 6 A_CustomMissile("DarkNormal", 32, 0, 0)
        Goto Missile+7
        DRKV IJ 8 Bright A_FaceTarget
        DRKV K 6 Bright A_CustomMissile("DarkStrong", 32, 0, 0)
        DRKV E 0 A_Jump(64,4)
        DRKV EF 8 A_FaceTarget
        DRKV G 6 A_CustomMissile("DarkNormal", 32, 0, 0)
        Goto See
        DRKV IJ 8 Bright A_FaceTarget
        DRKV K 6 Bright A_CustomMissile("DarkStrong", 32, 0, 0)
        Goto See
    Pain:
        DRKV H 2
        DRKV H 2 A_Pain
        Goto See
		
    MonDeath:
        DRKV L 8
        DRKV M 8 A_Scream
        DRKV N 5
        DRKV O 5 A_Fall
        DRKV P 650
		goto Super::FadeOut
		
    MonXDeath:
        DRKI N 5
        DRKI O 5 A_XScream
        DRKI P 5
        DRKI Q 5 A_Fall
        DRKI RST 5
        DRKI U 650
		goto Super::FadeOut
		
    MonRaise:
        DRKV PO 8
        DRKV NML 6
        Goto See
    }
}

actor DarkNormal
{
    Radius 8
    Height 6
    Speed 9
    Damage 3
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    Decal DoomImpScorch
	+mthruspecies
    States
    {
    Spawn:
        DKB1 AB 4 Bright
        goto Spawn
    Death:
        DKB1 CDE 4 Bright
        Stop
    }
}

actor DarkStrong
{
    Radius 8
    Height 6
    Speed 9
    Damage 6
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    Decal DoomImpScorch
	+mthruspecies
    States
    {
    Spawn:
        DKB2 AB 4 Bright A_SpawnItemEx("DarkTail", 0, 0, 0, 0, 0, 0, 0, 128)
        goto Spawn
    Death:
        DKB2 CDE 4 Bright
        Stop
    }
}

actor DarkTail
{
    PROJECTILE
    +CLIENTSIDEONLY
    +NOINTERACTION
    RENDERSTYLE ADD
    ALPHA 0.5
    States
    {
    Spawn:
        DKBT ABCDEF 2 Bright
        Stop
    }
}