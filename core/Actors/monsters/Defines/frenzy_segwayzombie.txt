actor SegwayZombie 23132
{
spawnid 247
  obituary "%o was run down by a zombie on a turbo segway!"
  health 60
  radius 20
  height 56
  mass 100
  speed 1
  painchance 100
  seesound "grunt/sight"
  painsound "grunt/pain"
  deathsound "grunt/death"
  activesound "grunt/active"
  meleedamage 6
  meleerange 34
  translation "112:127=[139,134,78]:[0,0,0]"
  MONSTER
  +FLOORCLIP
  +STRIFEDAMAGE
	
  states
  {
  Spawn:
    ZSEG A 10 A_Look
    ZSEG A 0 A_Stop
    ZSEG A 0 A_StopSoundEx ("SoundSlot7")
    loop
  See:
    ZSEG A 0 A_Jump (8, "Coward", "Brave")
    ZSEG A 1 A_Chase
    ZSEG A 0 A_Recoil (-2)
    ZSEG A 0 A_MeleeAttack
    ZSEG A 0 A_PlaySoundEx ("Segway/Go", "SoundSlot7", 1)
    ZSEG A 0 A_SpawnItemEx ("SegwayPuff", 0, 8, 8, -4, 0, 2, 0, 160)
    ZSEG A 0 A_SpawnItemEx ("SegwayPuff", 0, -8, 8, -4, 0, 2, 0, 160) 
    loop
  Coward:
    ZSEG A 0 A_ChangeFlag ("FRIGHTENED", 1)
    goto See
  Brave:
    ZSEG A 0 A_ChangeFlag ("FRIGHTENED", 0)
    goto See
  Pain:
    ZSEG A 0 A_Pain
    ZSEG A 0 A_PlaySoundEx ("Segway/Go", "SoundSlot7", 1)
    goto See
  Death:
NULL A 0 ACS_ExecuteAlways(500, 0, 39, 0)
    ZSEG B 5 A_SpawnItemEx ("Boom", 0, 0, 32, 0, 0, 0, 0, 160)
    ZSEG B 0 A_StopSoundEx ("SoundSlot7")
    ZSEG B 0 A_PlaySound ("weapons/rocklx")
    ZSEG C 5 A_Scream
    ZSEG D 5 A_NoBlocking
    ZSEG E 650
		DeathFade:
			ZSEG E 2 A_FadeOut(0.1)
			loop
  }
}

actor SegwayPuff
{
  radius 2
  height 2
  speed 0
  damage 0
  scale 0.6
  renderstyle Add
  alpha 0.2
  +CLIENTSIDEONLY
  +NOINTERACTION
  states
  {
  Spawn:
    ZPUF ABCDE 1
    stop
  }
}

actor Boom
{
  Radius 11
  Height 8
  Speed 0
  Damage 0
  Projectile
  +CLIENTSIDEONLY
  renderstyle Add
  States
  {
  Spawn:
    MISL B 8 bright
    MISL C 6 bright
    MISL D 4 bright
    stop
  }
}