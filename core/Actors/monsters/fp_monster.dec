
Actor FPMToken_NoCount : Boolean {} // This enemy is too easy to be paid (grant experience only.)
Actor FPMToken_Gibbed : Boolean {} // The enemy became bits.
Actor FPMToken_Frozen : Boolean {} // The enemy got frozen. (Same as gibbed)รง
Actor FPMToken_Hard : Boolean {} // This enemy is hard to kill.
Actor FPMToken_Boss : Boolean {} // This enemy is a boss (granting a lot of experience and credits.)

/*
	Quick Monster Addition guide:
	1) Replace Death, xdeath and Raise states to monDeath, monXDeath, and MonRaise.
	2) In the last frame from the Death and XDeath States, do NOT use the -1 tic, change it to 1 tic if you do. 
	   That will keep the monster forever, and it won't be auto-removed.
	3) Instead of stop, after the last state from death and xdeath states, replace it with:
		"goto FadeOut" if this monster contains a rise state.
		"goto FadeOut.NoRaise" if this monster does not need a rise.
*/

Actor FPlus_Monster {
	health 10
	species "DemonSide"
	DamageFactor "DemonFire", 0.0
	MONSTER
	+MTHRUSPECIES
	
	states{
		
		Spawn:
			TNT1 a 1 A_Log("WARNING: Monster logic issue in Spawn State")
		Loop
		
		See:
			TNT1 a 1 a_Log("WARNING: Monster logic issue in See State")
		Loop
		
		BeBoss:
			"####" "#" 0 A_ChangeFlag("NOPAIN", true)
			"####" "#" 0 A_ChangeFlag("MISSILEMORE", true)
			"####" "#" 0 A_ChangeFlag("MISSILEEVENMORE", true)
			"####" "#" 0 A_ChangeFlag("QUICKTORETALIATE", true)
			"####" "#" 0 A_ChangeFlag("NORADIUSDMG", TRUE)
			"####" "#" 1 A_Jump(256, "Spawn") 
		goto Spawn
		
		BeHard:
			"####" "#" 0 A_ChangeFlag("NOPAIN", true)
			"####" "#" 0 A_ChangeFlag("MISSILEMORE", true)
			"####" "#" 0 A_ChangeFlag("QUICKTORETALIATE", true)
			"####" "#" 0 ACS_NamedExecuteAlways("Monster_HPBar", 0, height)
			"####" "#" 1 A_Jump(256, "Spawn") 
		goto Spawn
		
		MonXDeath: // Overridable, if does not have it, go to it's death.
			"####" "#" 0 
			"####" "#" 0 A_TakeInventory("FPMToken_Gibbed",1)
			"####" "#" 1 A_Jump(256, "MonDeath") // Called for all of the inherited classes.
		goto MonDeath
		
		MonDeath:
			"####" "#" 0 A_TakeInventory("FPMToken_Gibbed",1) // Flag Token.
			"####" "#" 1 a_Log("WARNING: Monster logic issue in Death State")
		goto FadeOut+1
		
		MonRaise:
			"####" "#" 0 a_Log("WARNING: Monster logic issue in Raise State")
		goto FadeOut+1
		
		FadeOut:
			"####" "#" 650 CanRaise
			"####" "#" 2 A_FadeOut(0.1)
			"####" "#" 2 A_Fadein(0.05)
		goto FadeOut+1
		
		FadeOut.NoRaise:
			"####" "#" 650
			"####" "#" 2 A_FadeOut(0.1)
			"####" "#" 2 A_Fadein(0.05)
		goto FadeOut+1
		
		Death.Ice:
			"####" "#" 0 A_GiveInventory("FPMToken_Frozen",1) // Flag Token.
		goto GenericFreezeDeath
		
		Death:
			"####" "#" 0 A_SpawnItemEx("ClassicCreditBoom", 0, 0, 0, 0, 0, 0, 0, 0, 235)
			"####" "#" 1 A_Jump(256, "MonDeath") // Called for all of the inherited classes.
		stop
		
		Death.NoCount:
			"####" "#" 0 A_SpawnItemEx("ClassicCreditBoom", 0, 0, 0, 0, 0, 0, 0, 0, 235)
			"####" "#" 0 A_GiveInventory("FPMToken_NoCount", 1)
			"####" "#" 1 A_Jump(256, "MonDeath") // Called for all of the inherited classes.
		stop
		
		XDeath:
			"####" "#" 0 A_SpawnItemEx("ClassicCreditBoom", 0, 0, 0, 0, 0, 0, 0, 0, 235)
			"####" "#" 0 A_SpawnItemEx("ClassicCreditBoom", 0, 0, 0, 0, 0, 0, 0, 0, 235)
			"####" "#" 0 A_SpawnItem ("splatEmitter_Lite" , 0, 0, 0, 0)
			"####" "#" 0 A_GiveInventory("FPMToken_Gibbed",1) 
			"####" "#" 1 A_Jump(256, "MonXDeath") // Called for all of the inherited classes.
		stop
		
		Raise:
			"####" "#" 0 A_TakeInventory("FPMToken_Gibbed",1)
			"####" "#" 0 A_TakeInventory("FPMToken_Frozen",1)
			"####" "#" 0 A_TakeInventory("FPMToken_NoCount", 1)
			"####" "#" 1 A_Jump(256, "MonRaise") // Called for all of the inherited classes.
		goto MonRaise
	
	}
}

// monsters
#include "Actors/monsters/Defines/fp_defines.dec"
#include "Actors/monsters/fp_aidspawn.dec"