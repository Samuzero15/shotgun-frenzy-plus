ACTOR Cybruiser : FPlus_Monster 30128
{ 
spawnid 205
Health 2000 
Radius 24 
Height 64 
Mass 1000 
Speed 10 
PainChance 20 
MeleeDamage 10
Obituary "%o was blown away by a Cybruiser."
HitObituary "%o was smashed by a Cybruiser."
+FLOORCLIP 
+MISSILEMORE
+DONTHURTSPECIES
SeeSound "monster/brusit" 
PainSound "baron/pain" 
DeathSound "monster/brudth"
MeleeSound "baron/melee"
States 
   { 
   Spawn: 
       BRUS AB 10 A_Look 
       goto Spawn 
   See:
       BRUS A 1 A_Playsound("monster/bruwlk")
       BRUS A 2 A_Chase
       BRUS A 3 A_Chase
       BRUS BB 3 A_Chase
       BRUS C 1 A_Playsound("monster/bruwlk")
       BRUS C 2 A_Chase
       BRUS C 3 A_Chase 
       BRUS DD 3 A_Chase
       goto See 
   Missile:
       BRUS E 8 A_FaceTarget 
       BRUS F 12 Bright A_CustomMissile("BruiserMissile",38,15,0,0)
       BRUS E 12 A_FaceTarget
       BRUS F 12 Bright A_CustomMissile("BruiserMissile",38,15,0,0)
       BRUS E 12 A_FaceTarget
       BRUS F 12 Bright A_CustomMissile("BruiserMissile",38,15,0,0)
       Goto See
   Melee:
       BRUS GH 8 A_FaceTarget
       BRUS I 8 A_MeleeAttack
       Goto See
   Pain: 
       BRUS J 2 
       BRUS J 2 A_Pain 
       Goto See 
	   
	Death.Ice:
		goto Super::Death.Ice
	   
   Death: 
	   NULL A 0 A_SpawnItemEx("ClassicCreditBoom", 0, 0, 0, 0, 0, 0, 0, 0, 235)
       BRUS K 6 Bright A_Scream
       BRUS LMN 6 Bright
       BRUS O 6 Bright A_NoBlocking
       BRUS QRS 6 Bright
       BRUS T 1 A_BossDeath
       goto Super::FadeOut
   } 
}

ACTOR BruiserMissile
{
   Radius 8
   Height 12
   Speed 40
   Damage 10
   Scale 1.0
   SpawnID 251
   PROJECTILE
   RENDERSTYLE Normal
   +THRUGHOST
   SeeSound "monster/brufir"
   DeathSound "weapons/hellex"
   DONTHURTSHOOTER
   +FIREDAMAGE
   +STRIFEDAMAGE
   +MTHRUSPECIES
   States
   {
   Spawn: 
      FBRS A 1 Bright
      FBRS A 1 Bright A_SpawnItem("BruiserTrail",0,0)
      goto Spawn
   Death:
      BAL3 C 0 Bright A_SetTranslucent (0.67,1)
      BAL3 C 6 Bright A_Explode(40,128,0,0,32)
      BAL3 D 6 Bright
      BAL3 E 6 Bright
      stop
   }
}

ACTOR BruiserTrail
{   
   Radius 3
   Height 3
   RENDERSTYLE Translucent
   Alpha 0.67
   PROJECTILE
   +CLIENTSIDEONLY
   +NOINTERACTION

   States
   {
   Spawn:
      NULL A 3 Bright
      PUFF ABCD 4 BRIGHT
      Stop 
   }
}