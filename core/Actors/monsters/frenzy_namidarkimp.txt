actor NamiDarkImp : FPlus_Monster 3100
{
spawnid 240
	//$Category Monsters
    Health 120
    Radius 20
    Height 56
    Speed 8
    PainChance 200
    +FLOORCLIP
    SeeSound "imp/sight"
    PainSound "imp/pain"
    DeathSound "imp/death"
    ActiveSound "imp/active"
    MeleeSound "imp/melee"
    Obituary "%o was cursed by a dark imp."
    HitObituary "%o was touched by a dark imp."
    MissileType DarkSeeker
    MeleeDamage 3
	
    States
    {
    Spawn:
        DRKI AB 10 A_Look
        goto Spawn
    See:
        DRKI AABBCCDD 3 A_Chase
        goto See
    Melee:
    Missile:
        DRKI EF 8 A_FaceTarget
        DRKI G 6 A_ComboAttack
        Goto See
    Pain:
        DRKI H 2
        DRKI H 2 A_Pain
        Goto See
		
	Death.Ice:
		goto Super::Death.Ice

	XDeath:
		TNT1 A 0 A_ChangeFlag("EXTREMEDEATH",true) // Flag Token.
	goto MonXDeath

	Raise:
		TNT1 A 0 A_ChangeFlag("EXTREMEDEATH",false)
		TNT1 A 0 A_ChangeFlag("SKYEXPLODE",false)
	goto MonRaise
	
    Death:
        DRKI I 8
        DRKI J 8 A_Scream
        DRKI K 6
        DRKI L 6 A_Fall
        goto Super::FadeOut
    MonXDeath:
        DRKI N 5
        DRKI O 5 A_XScream
        DRKI P 5
        DRKI Q 5 A_Fall
        DRKI RST 5
        goto Super::FadeOut
    MonRaise:
        DRKI ML 8
        DRKI KJI 6
        Goto See
    }
}

actor DarkSeeker
{
    Radius 8
    Height 6
    Speed 9
    Damage 3
    +SEEKERMISSILE
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    Decal DoomImpScorch
    States
    {
    Spawn:
        DKB1 AB 4 Bright A_SeekerMissile (5,10)
        goto Spawn
    Death:
        DKB1 CDE 4 Bright
        Stop
    }
}