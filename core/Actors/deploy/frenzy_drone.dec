
Actor sf_Drone{

	painchance 180
	radius 8
	height 8
	scale 0.7
	mass 400
	DamageType "MarineFire"
	damagefactor "MarineFire", 0.0
	damageFactor "MarineFire2", 0.0
	DamageFactor "MarineFire3", 0.0
    DamageFactor "BFGSplash", 0.0
    DamageFactor "BFGSpray", 0.0
	Species Marines
	MONSTER
	-isMonster
	+FRIENDLY
	+FLOORCLIP
	+NEVERRESPAWN
	+FORCERADIUSDMG
	+NODAMAGETHRUST
	-ACTIVATEMCROSS
	-ACTIVATEPCROSS
	
	-NoBlockMap
	+ThruSpecies
	+MthruSpecies
	
	+LOOKALLAROUND
	+NOBLOCKMONST
}

ACTOR sf_SawDrone : sf_Drone
{
	hitobituary "%o was shaved by a Saw Drone"
	health 50
	painchance 120
	speed 8
	Damage 8
	seesound "p/drone_saw1"
	activesound "p/drone_saw2"
	attacksound "p/drone_saw3"
	deathsound "Flinger/Grenade/Booms"
	Tag "Deploy a single saw drone. It will attack demons with their blades."
	+NoGravity
	+FLOAT
	+MISSILEMORE
	+MISSILEEVENMORE
	+DONTFALL
	+QUICKTORETALIATE
	
	states
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("p/drone_saw1", CHAN_VOICE)
			SAWD ABABABABABAB 1
		Spawn.Loop:
			SAWD AB 4 A_Look
			goto See
		See:
			SAWD A 0 A_PlaySound("p/drone_saw2",CHAN_VOICE)
			SAWD A 4 A_Chase
			TNT1 A 0 A_Look
			SAWD B 4 A_Chase
			TNT1 A 0 A_Look
			goto See
		Missile:
			SAWD A 1 BRIGHT A_FaceTarget
			SAWD B 1 BRIGHT A_SkullAttack(35)
			goto See
		Melee:
			SAWD A 1 A_FaceTarget
			SAWD B 1 A_CustomMeleeAttack(random(1, 20) * 8, "p/drone_saw1")
			goto See
		Pain:
			SAWD A 1
			SAWD B 1 A_Pain
			goto See
		Death:
			MISL B 8 bright A_Explode(50,96)
			MISL C 6 bright
			MISL D 4 bright
			stop
	}
}

ACTOR sf_MineDrone : sf_Drone
{
	hitobituary "%o was shaved by a Mine Drone"
	health 5
	speed 10
	seesound "baby/sight"
	painsound "baby/pain"
	deathsound "Flinger/Grenade/Booms"
	activesound "baby/active"
	Tag "Deploy a single mine drone. It will blow at the enemies with their cluster grenades."
	states
	{
		Spawn:
			MIND AB 1 A_Look
			goto Spawn
		See:
			MIND A 0
			MIND A 1 A_Chase
			TNT1 A 0 A_Look
			MIND A 1 A_Chase
			TNT1 A 0 A_Look
			MIND B 1 A_Chase
			TNT1 A 0 A_Look
			MIND B 1 A_Chase
			TNT1 A 0 A_Look
			goto See
		Melee:
			MIND A 0 A_FaceTarget
			goto death
		Pain:
			MIND A 1
			MIND B 1 A_Pain
			goto See
		Death:
			MISL B 1 bright A_Explode(100,128,0,0,64)
			MISL B 1 bright A_SpawnDebris("Grenade_Expander_MiniGrenade")
			MISL B 1 bright A_SpawnDebris("Grenade_Expander_MiniGrenade")
			MISL B 1 bright A_SpawnDebris("Grenade_Expander_MiniGrenade")
			MISL B 1 bright A_SpawnDebris("Grenade_Expander_MiniGrenade")
			MISL B 1 bright A_SpawnDebris("Grenade_Expander_MiniGrenade")
			MISL B 1 bright A_SpawnDebris("Grenade_Expander_MiniGrenade")
			MISL B 1 bright A_SpawnDebris("Grenade_Expander_MiniGrenade")
			MISL B 1 bright A_SpawnDebris("Grenade_Expander_MiniGrenade")
			MISL B 1 bright A_SpawnDebris("Grenade_Expander_MiniGrenade")
			MISL B 1 bright A_SpawnDebris("Grenade_Expander_MiniGrenade")
			MISL B 1 bright A_SpawnDebris("Grenade_Expander_MiniGrenade")
			MISL B 1 bright A_SpawnDebris("Grenade_Expander_MiniGrenade")
			MISL C 6 bright
			MISL D 4 bright
			stop
	}
}
