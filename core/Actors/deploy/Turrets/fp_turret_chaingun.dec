Actor TurretBuilders_Chaingun : Deploy_TurretBuilders // the builders, except you can edit their time!
{
	States
	{
		Timer.Set: // Holy shiet.
			TNT1 A 0 A_SetUserVar("user_prog", 35)
		goto Super::Timer
	}
}

ACTOR TurretChaingun : Deploy_Building // The item to be deployed.
{
	states
	{
		Build:
			TBLD A 1 A_SpawnItemEx("TurretBuilders_Chaingun", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER | SXF_NOCHECKPOSITION, 0, args[1])
			goto Super::Delay
	
		Deploy:
			NULL A 1 A_SpawnItemEx("TurretChaingun_Head", 0, 0, 0, 0, 0, 0, 0, 0, 0, args[2])
			stop
	}
}

ACTOR TurretChaingun_Stand : TurretStand
{
	States
	{
		Spawn:
			TBAS C 1
			goto Super::Stay
	}
}


ACTOR TurretChaingun_Head : TurretHeadBase 24306
{
	obituary "%o got some new holes by a chaingun turret."
	health 700
	+FRIENDLY
        DamageFactor "BFGSplash", 0.0
        DamageFactor "BFGSpray", 0.0
	//$Category SF Plus/Toorets
	//$Title Chaingun Turret
	//$Color 1
	var int user_projectiles;
	var int user_shots;
	var int user_rounds;
	states
	{	
		Spawn.Head:
			TNT1 A 0 A_SetUserVar("user_fixcost", 200)
			TNT1 A 0 A_SetUserVar("user_upgcost", 2500)
			TNT1 A 0 A_SetUserVar("user_rangestart", 1028)
			TNT1 A 0 A_SetUserVar("user_rangeup", 256)
			TNT1 A 0 A_SetUserVar("user_range", user_rangestart)
			TNT1 A 0 A_SetUserVar("user_rounds", 0)
			TNT1 A 0 A_SetUserVar("user_shots", 0)
			TNT1 A 0 A_SetUserVar("user_projectiles", 0)
			CTRT ABCDE 3
			CTRT F 2 A_Look
		goto See

		See:
			CTRT F 1 A_Chase
			goto SUPER::See
		
		Pain:
			CTRT F 1
			goto SUPER::Pain

		Missile.Start:
			//TNT1 A 0 A_log("whaaaaaaaaa")
			CTRT G 8 A_SetTics(8 - stamina/2)
			TNT1 A 0 A_FaceTarget
			CTRT H 8 A_SetTics(8 - stamina/2)
			TNT1 A 0 A_PlaySound("x5/powerup")
			TNT1 A 0 A_FaceTarget
			CTRT GH 4 A_SetTics(4 - stamina/3)
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_JumpIf(stamina >= 1, 3)
			CTRT GH 4 
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_JumpIf(stamina >= 1, 3)
			CTRT GH 2 
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_JumpIf(stamina >= 1, 3)
			CTRT GH 2 
			TNT1 A 0 A_FaceTarget
			CTRT GH 2 A_SetTics(2 - stamina/4)
			TNT1 A 0 A_FaceTarget
			CTRT GH 2 A_SetTics(2 - stamina/4)
			goto Missile.Fire
			
		Missile.Fire.F1:
			TNT1 A 0 A_SetUserVar("user_rounds", 1)
			CTRT I 1 bright A_PlaySound("x5/oldfire")
			TNT1 A 0 A_JumpIf(stamina > 2, "Missile.Fire")
			CTRT G 1 //A_SpawnItemEx ("splatBulletCasing_Lite", 32, 16, 45+random(-18,18), 0, 3)
			goto Missile.Fire
		
		Missile.Fire.F2:
			TNT1 A 0 A_SetUserVar("user_rounds", 0)
			CTRT J 1 bright A_PlaySound("x5/oldfire")
			TNT1 A 0 A_JumpIf(stamina > 2, "Missile.Refire")
			CTRT H 1 //A_SpawnItemEx("splatBulletCasing_Lite", 32, 16, 45+random(-18,18), 0, 3)
			goto Missile.Refire
		
		Missile.Fire:
			TNT1 A 0 A_SetUserVar("user_projectiles", 0)
			TNT1 A 0 A_SetUserVar("user_shots", 2 + 1*(stamina >= 2) + 1*(stamina >= 4))
		Missile.Fire.Projectiles:
			TNT1 A 0 A_JumpIf(stamina == 5, 2)
			TNT1 A 0 A_CustomMissile("TurretProj_Chaingun", 50 ,0, frandom(-4.5, 4.5), 0)
			TNT1 A 0 A_JumpIf(stamina != 5, 2)
			TNT1 A 0 bright A_SpawnItem ("TurretChaingun_GunFireLevel5", 20, 52)
			TNT1 A 0 A_SetUserVar("user_projectiles", user_projectiles+1)
			TNT1 A 0 A_JumpIf(user_projectiles >= user_shots, "Missile.Fire.Frames")
		    loop

		Missile.Fire.Frames:
			TNT1 A 0 A_JumpIf(user_rounds == 0, "Missile.Fire.F1")
			TNT1 A 0 A_JumpIf(user_rounds == 1, "Missile.Fire.F2")
			goto Missile.Refire
			
		Missile.Refire:
			TNT1 A 0 A_SetUserVar("user_rounds", 0)
			TNT1 A 0 A_JumpIfHealthLower(user_crithealth, "Missile.Smoke")
			TNT1 A 0 A_LookEx(LOF_NOJUMP)
			CTRT G 1 A_MonsterRefire(0, "Missile.Rest")
			goto Missile.Fire
		
		Missile.Rest:
			TNT1 A 0 A_PlaySound("x5/powerdown")
			TNT1 A 0 A_ClearTarget
			CTRT GH 2 A_SetTics(2 - stamina/4)
			CTRT GH 2 A_SetTics(2 - stamina/4)
			TNT1 A 0 A_JumpIf(stamina >= 1, 2)
			CTRT GH 2 A_SetTics(2 - stamina/4)
			TNT1 A 0 A_JumpIf(stamina >= 1, 2)
			CTRT GH 2 A_SetTics(2 - stamina/4)
			TNT1 A 0 A_JumpIf(stamina >= 1, 2)
			CTRT GH 4 A_SetTics(4 - stamina/3)
			CTRT GH 4 A_SetTics(4 - stamina/3)
			CTRT G 8 A_SetTics(8 - stamina/2)
			CTRT H 8 A_SetTics(8 - stamina/2)
			goto See
		
		Missile.Smoke:
			TNT1 A 0 A_SpawnItemEx("Smoke_Spawner",0,0,40)
			goto Missile.ReFire+1


	}
}

actor TurretChaingun_GunFireLevel5
{
  obituary "%o why did you put yourself in the middle of fire."
  radius 2
  height 2
  //Decal "CrossbowScorch2"
  +NOGRAVITY
  //+NoBlockMap
  +LOOKALLAROUND
  +NOCLIP
  +INVULNERABLE
  states
  {
  Spawn:
    TNT1 A 1 A_FaceTarget
    TNT1 A 0 A_CustomRailgun(random(1,3)*6, 0, "none", "BB BB BB", RGF_SILENT, 0, 0, "InvisiblePuff", 8, 3, 2048, 1)
	TNT1 A 0 
    //TNT1 A 0 A_CustomRailgun(0, 0, none, "88 88 88", 1, 0, 32, "InvisiblePuff")
    stop
  }
}