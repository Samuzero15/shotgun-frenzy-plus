Actor TurretBuilders_Bullet : Deploy_TurretBuilders // the builders, except you can edit their time!
{
	States
	{
		Timer.Set: // Holy shiet.
			TNT1 A 0 A_SetUserVar("user_prog", 15)
		goto Super::Timer
	}
}

ACTOR TurretBullet : Deploy_Building // The item to be deployed.
{
	states
	{
		Build:
			TBLD A 1 A_SpawnItemEx("TurretBuilders_Bullet", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER | SXF_NOCHECKPOSITION, 0, args[1])
			goto Super::Delay
	
		Deploy:
			TNT1 A 0 
			NULL A 1 A_SpawnItemEx("TurretBullet_Head", 0, 0, 0, 0, 0, 0, 0, 0, 0, args[2])
			stop
	}
}

ACTOR TurretBullet2_Base : TurretStand
{
	States
	{
		Spawn:
			TBAS A 1
			goto Super::Stay
	}
}

ACTOR TurretBullet_Head : TurretHeadBase 24301
{
	//$Category SF Plus/Toorets
	//$Title Bullet Turret
	//$Color 1
	obituary "%o was shot up by a Bullet Turret."
	health 350
        DamageFactor "BFGSplash", 0.0
        DamageFactor "BFGSpray", 0.0

	var int user_bulletsfired;
	var int user_extrabullets;
	var int user_smoketic;
	// Stamina's Level Effect: Increases the amount of bullets fired.
	states
	{
		Spawn.Head:
			TNT1 A 0 A_SetUserVar("user_fixcost", 50)
			TNT1 A 0 A_SetUserVar("user_upgcost", 200)
			TNT1 A 0 A_SetUserVar("user_rangeup", 128)
			TNT1 A 0 A_SetUserVar("user_range", user_rangestart)
			BTRT A 1 A_Look
		goto See

		See:
			BTRT A 1 A_Chase
			goto SUPER::See
		
		Pain:
			BTRT A 1
			goto Super::Pain
			/** Alright, let me explain this code.
				The first 2 upgrades will add 2 shots more for the turret.
				The third and fourth upgrade will give an extra projectile per shot.
				The fifth upgrade will make the turret aim better to the targets.

				In Maxed level, the turret fires 3 bullets per shot, 20 shots and aims to the target.
			*/
		Missile.Upgrades:
			TNT1 A 0 A_JumpIf(stamina < 5, 2)
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_JumpIf(stamina < 4, 2)
			TNT1 A 0 A_CustomMissile("TurretProj_Bullet", 50, 0, frandom(-10.5, 10.5), 0)
			TNT1 A 0 A_JumpIf(stamina < 3, "Missile.Fire")
			TNT1 A 0 A_CustomMissile("TurretProj_Bullet", 50, 0, frandom(-10.5, 10.5), 0)
		Missile.Fire:
			BTRT A 1 A_CustomMissile("TurretProj_Bullet", 50, 0, frandom(-10.5, 10.5), 0)
			BTRT B 1 bright A_PlaySound("turret/firebullet")
			BTRT A 0 A_SpawnItemEx("splatBulletCasing", 32, 16, 45+random(-18,18), 0, 3)
			TNT1 A 0 A_SetUserVar("user_smoketic", user_smoketic+1)
			BTRT A 0 A_JumpIfHealthLower(user_crithealth, "Missile.SmokeCount")
			goto Missile.Count

		Missile.Count:
			TNT1 A 0 A_SetUserVar("user_bulletsfired", user_bulletsfired+1)
			TNT1 A 0 A_JumpIf(user_bulletsfired < (15 + stamina), "Missile.Upgrades")
			TNT1 A 0 A_SetUserVar("user_bulletsfired", 0)
			goto Missile.Rest

		Missile.SmokeCount:
			TNT1 A 0 A_JumpIf(user_smoketic < 3, "Missile.Count")
			NULL A 0 A_SpawnItemEx("Smoke_Spawner")
			TNT1 A 0 A_SetUserVar("user_smoketic", 0)
			goto Missile.Count
		
		Missile.Rest:
			TNT1 A 0 A_SetUserVar("user_smoketic", 0)
			TNT1 A 0 A_SetUserVar("user_bulletsfired", 0)
			TNT1 A 0 A_SetUserVar("user_extrabullets", 0)
			BTRT AAAA 1 bright
			BTRT A 5
			goto See
	}
}
