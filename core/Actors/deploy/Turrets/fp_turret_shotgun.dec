Actor TurretBuilders_Shotgun : Deploy_TurretBuilders // the builders, except you can edit their time!
{
	States
	{
		Timer.Set: // Holy shiet.
			TNT1 A 0 A_SetUserVar("user_prog", 18)
		goto Super::Timer
	}
}

ACTOR TurretShotgun : Deploy_Building // The item to be deployed.
{
	states
	{
		Build:
			TBLD A 1 A_SpawnItemEx("TurretBuilders_Shotgun", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER | SXF_NOCHECKPOSITION, 0, args[1])
			goto Super::Delay
	
		Deploy:
			NULL A 1 A_SpawnItemEx("TurretShotgun_Head", 0, 0, 0, 0, 0, 0, 0, 0, 0, args[2])
			stop
	}
}

Actor TurretShotgun_FireToken : Inventory {
	Inventory.MaxAmount 2
}

ACTOR TurretShotgun_Head : TurretHeadBase 24307
{
	obituary "%o got blasted by a shotgun turret."
	health 1000
	+FRIENDLY
        DamageFactor "BFGSplash", 0.0
        DamageFactor "BFGSpray", 0.0
	//$Category SF Plus/Toorets
	//$Title Shotgun Turret
	//$Color 1
	var int user_projectiles;
	var int user_casings;
	
	states
	{
		Spawn.Head:
			TNT1 A 0 A_SetUserVar("user_fixcost", 150)
			TNT1 A 0 A_SetUserVar("user_upgcost", 1500)
			TNT1 A 0 A_SetUserVar("user_range", user_rangestart)
			STRT A 1 A_Look
		goto See

		See:
			STRT A 1 A_Chase
			goto SUPER::See
		
		Pain:
			STRT A 1
			goto Super::Pain
			
			/** Alright, let me explain this code.
				Shotgun turret will fire faster on each upgrade. (Up to 10 tics)
				It will also fire more pellets than the usual. (up to 34 pellets)
				Each upgrade grants a 10% more for firing a flame pellet (like the explosive ssg, up to 50%)
			*/
		Missile.Start:
			TNT1 A 0 A_TakeInventory("TurretShotgun_FireToken", 1)
			TNT1 A 0 A_SetUserVar("user_projectiles", 0)
			TNT1 A 0 A_SetUserVar("user_casings", 0)
			STRT A 2 A_FaceTarget
			STRT B 1 bright 
			STRT C 1 A_PlaySound("turret/shotgun/fire")
			goto Missile.Fire
		
		Missile.Fire: // 24 bullets.
			TNT1 A 0 A_JumpIf(random(0,100) >= (0 + stamina*10), 3) // Jump if we did'nt succeed on firing the flame projectile.
			TNT1 A 0 A_CustomMissile("TurretProj_Shotgun_Flame", 50,0, frandom(-8.5, 8.5), 0)
			TNT1 A 0 A_JumpIf(random(0,100) < (0 + stamina*10), 2) // Jump if we did succeed on firing the flame projectile.
			TNT1 A 0 A_CustomMissile("TurretProj_Shotgun", 50,0, frandom(-8.5, 8.5), 0)
			TNT1 A 0 A_SetUserVar("user_projectiles", user_projectiles+1)
			TNT1 A 0 A_JumpIf(user_projectiles < (20 + stamina*2), "Missile.Fire")
			goto Missile.Casings
		Missile.Casings:
			TNT1 A 0 A_SpawnItemEx("splatShellCasing", 32, 16, 45+random(-18,18), 0, 3)
			TNT1 A 0 A_SetUserVar("user_casings", user_casings+1)
			TNT1 A 0 A_JumpIf(user_casings < 4, "Missile.Casings")
			STRT CB 1 bright
			goto Missile.Reload
		Missile.Reload: // Hi health
			TNT1 A 0 A_JumpIfHealthLower(user_crithealth,"Missile.Smoke")
			
			STRT A 25 A_SetTics(25 - (stamina*3)) // Up to 10 tics in level 5.
			STRT A 1 A_PlaySound("turret/shotgun/load")
			TNT1 A 0 A_JumpIfInventory("TurretShotgun_FireToken", 1, "See")
			TNT1 A 0 A_GiveInventory("TurretShotgun_FireToken", 1)
			goto Missile.Start+1 
		
		Missile.Smoke:
			TNT1 A 0 A_SpawnItemEx("Smoke_Spawner",0,0,40)
			goto Missile.Reload+1

	}
}