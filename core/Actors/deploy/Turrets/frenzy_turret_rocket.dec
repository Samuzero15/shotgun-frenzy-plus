ACTOR TurretRocketProj : Rocket
{
  DamageType MarineFire2
}

ACTOR TurretRocket : Deploy_Building
{
	states
	{
		Deploy:
			NULL A 1 A_SpawnItemEx("TurretRocket_Head", 0, 0, 0, 0, 0, 0, 0, 0, 0, args[2])
			stop
	}
}

ACTOR TurretRocket_Stand : TurretStand
{
	States
	{
		Spawn:
			TBAS D 1
			goto Super::Stay
	}
}


ACTOR TurretRocket_Head : TurretHeadBase 24305
{
	//$Category SF Plus/Toorets
	//$Title Rocket Turret
	//$Color 1
	obituary "%o was shot up by a Bullet Turret."
	health 800 // Let's give it some health.
        DamageFactor "BFGSplash", 0.0
        DamageFactor "BFGSpray", 0.0
		BloodType "none"
	var int user_rounds;
	var int user_shots;
	states
	{
		Spawn.Head:
			TNT1 A 0 A_SetUserVar("user_fixcost", 250)
			TNT1 A 0 A_SetUserVar("user_upgcost", 2500)
			TNT1 A 0 A_SetUserVar("user_rounds", 0)
			RTRT A 1 A_Look
			goto See
		
		See:
			RTRT A 1 A_Chase
			goto SUPER::See
		
		Pain:
			RTRT A 1
			goto Super::Pain

		Death:
			RTRT A 1 
			goto Super::Death

		Missile.Start:
			RTRT A 0 A_JumpIfHealthLower(user_crithealth,"Missile.Smoke")
		Missile.Fire.Setup:
			RTRT A 2 A_FaceTarget
			RTRT A 0 A_PlaySound("weapons/rocklf")
			TNT1 A 0 A_SetUserVar("user_shots", 1 + 1*(stamina >= 2) + 1*(stamina >= 4))
			goto Missile.Fire
		Missile.Fire:
			TNT1 A 0 A_JumpIf(stamina == 5, "Missile.Fire.Lv5")
			TNT1 A 0 A_JumpIf(stamina >= 3, "Missile.Fire.Lv3")
			goto Missile.Fire.Lv1

		Missile.Fire.Lv5:
			RTRT A 0 A_CustomMissile("TurretRocket_RocketLv5", 64, -16)
			RTRT A 0 A_CustomMissile("TurretRocket_RocketLv5", 64, 16)
			goto Missile.Refire

		Missile.Fire.Lv3:
			RTRT A 0 A_CustomMissile("TurretRocket_RocketLv3", 64, -16)
			RTRT A 0 A_CustomMissile("TurretRocket_RocketLv3", 64, 16)
			goto Missile.Refire

		Missile.Fire.Lv1:
			RTRT A 0 A_CustomMissile("TurretRocket_RocketLv1", 64, -16)
			RTRT A 0 A_CustomMissile("TurretRocket_RocketLv1", 64, 16)
			goto Missile.Refire

		Missile.Refire:
			RTRT A 0 A_JumpIf(user_shots < 2, "Missile.Rest")
			RTRT A 10 A_SetTics(10 - 5*(stamina == 5))
			RTRT A 0 A_PlaySound("weapons/rocklf")
			TNT1 A 0 A_SetUserVar("user_rounds", user_rounds+1)
			RTRT A 0 A_JumpIf(user_rounds < user_shots, "Missile.Fire")
			goto Missile.Rest

		Missile.Rest:
			TNT1 A 0 A_SetUserVar("user_rounds", 0)
			RTRT A 35 A_SetTics(35 - stamina*3)
			RTRT A 1 A_SpidRefire
			goto See
		
		Missile.Smoke:
			TNT1 A 0 A_SpawnItemEx("Smoke_Spawner",0,0,40)
			goto Missile.Fire.Setup
		
	}
}

ACTOR TurretRocket_RocketLv1 : TurretRocketProj 
{
  +mthruspecies
}

ACTOR TurretRocket_RocketLv3 : TurretRocket_RocketLv1
{	// Does the full damage, regardless of the distance.
	// Rocket's AOE is doubled
	Speed 35
  States {
	  Death:
		MISL B 8 Bright A_Explode(-1, 256, 0, 0, 256)
		MISL C 6 Bright
		MISL D 4 Bright
		Stop
  }
}

Actor TurretRocket_RocketLv5 : TurretRocket_RocketLv3{
	// Rockets can seek enemies now.
	// More damage per rocket!
	+SeekerMissile
	States {
		Spawn:
			MISL A 2 Bright A_SeekerMissile(0, 15, SMF_Look, 256,8)
			Loop
		Death:
			MISL B 8 Bright A_Explode(-1, 384, 0, 0, 384)
			MISL C 6 Bright
			MISL D 4 Bright
			Stop
	}
}