ACTOR AmmoDispenser : Deploy_Building
{
	States
	{
		Deploy:
			NULL A 1 A_SpawnItemEx("AmmoDispenser_Base", 0, 0, 0, 0, 0, 0, 0, 0, 0, args[2])
			stop
	}
}


ACTOR AmmoDispenser_Base
{
	health 250
	radius 16
	height 64
	mass 10000
	speed 0
	
	MONSTER
	//-ISMONSTER //Makes it spawnable everywhere
	-CountKill

	+FLOORCLIP
	+FRIENDLY
	+NOBLOOD
	+NEVERRESPAWN
	+THRUSPECIES
	+USEDAMAGEEVENTSCRIPT
	
	// protect from marine damage
	damagefactor "MarineFire", 0.0
	damagefactor "MarineFire2", 0.0
	DamageFactor "MarineFire3", 0.0
	DamageFactor "NoProfitKill", 0.0
        DamageFactor "BFGSplash", 0.0
        DamageFactor "BFGSpray", 0.0
	Species Marines
	
	var int user_fixcost;
	var int user_upgcost;
	var int user_range;
	var int user_rangeup;
	var int user_rangestart;
	
	states
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 
			//TNT1 A 0 ACS_NamedExecuteAlways("Turret_HPBar", 0, 60)
			TNT1 A 0 A_SetUserVar("user_fixcost", 300)
			TNT1 A 0 A_SetUserVar("user_upgcost", 1500)
			TNT1 A 0 A_SetUserVar("user_rangeup", 16)
			TNT1 A 0 A_SetUserVar("user_rangestart", 32)
			TNT1 A 0 A_SetUserVar("user_range", user_rangestart)
			TNT1 A 1 ACS_NamedExecuteAlways("Building_AggroLoop", 0)
			TNT1 A 0 ACS_NamedExecuteAlways("Turret_States", 0)
		Loopy:
			DEAM A 3 ACS_NamedExecuteAlways("Dispenser_SpawnVFX", 0, 0, 0)
			DEAM B 3 A_RadiusGive("AmmoDispenser_Pickup",user_range*2, RGF_PLAYERS, 1)
			DEAM C 3 ACS_NamedExecuteAlways("Dispenser_SpawnVFX", 0, 0, 0)
			DEAM D 3 A_RadiusGive("AmmoDispenser_Pickup",user_range*2, RGF_PLAYERS, 1)
			DEAM E 3 ACS_NamedExecuteAlways("Dispenser_SpawnVFX", 0, 1, 0)
			loop
		Death:
			TRT2 A 1 bright A_SpawnItemEx ("Boom", 0, 0, 60, 0, 0, 0, 0, 160)
			TRT2 A 0 bright A_StopSoundEx ("SoundSlot5")
			TRT2 A 0 bright A_PlaySound ("weapons/rocklx")
			stop
	}
}

ACTOR AmmoDispenser_Streak : StreakEffect
{
	states
	{
		Spawn:
			STRK B 1 A_FadeOut(0.02)
			goto Spawn
	}
}

ACTOR AmmoDispenser_StreakBarEmmiter : StreakEffect
{
	states
	{
		Spawn:
			TNT1 A 0 
			TNT1 A 1 ACS_NamedExecuteAlways("ParticleCheck", 0, 2)
			TNT1 A 1 
			TNT1 A 0 A_SpawnItemEx("AmmoDispenser_Streak", 0, 0, 0, 0, 0, 3, 0, 0, SXF_CLIENTSIDE)
			stop
		NoSpawn:
			TNT1 A 0
			Stop
	}
}

ACTOR AmmoDispenser_StreakSparkEmmiter : StreakEffect
{
	states
	{
		Spawn:
			TNT1 A 0 
			TNT1 A 1 ACS_NamedExecuteAlways("ParticleCheck", 0, 7)
			TNT1 A 1 
			TNT1 A 0 A_SpawnItemEx("streak_cfx", 0, 0, 0, 0, 0, 1, 0, 0, SXF_CLIENTSIDE)
			stop
		NoSpawn:
			TNT1 A 0
			Stop
	}
}

ACTOR AmmoDispenser_Pickup : DispenserPickup
{

	inventory.pickupmessage "Stocking..."

	states
	{
		Spawn:
			TNT1 A 1 
			stop
		Pickup:
			TNT1 A 0
			
			TNT1 A 0 ACS_ExecuteAlways(106, 0, -2, 1, 0)
			//[Samu], it's too op to let the players stock to the max anyhere.
			TNT1 A 1
			stop
	}
}
