
// Using the built-in particles in the clever way :)

const int PART_RADIUS = 1;
const int PART_HEIGHT = 2;

Actor ParticleAura : ParticleFountain {
    +ClientsideOnly
    states {
        Spawn:
            TNT1 A 1 NoDelay A_Warp(AAPTR_Target, 0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
            TNT1 A 0 A_JumpIf(CallACS("AltState_Particle_Stop"), "Death")
            loop
        Death:
            TNT1 A 1
            stop
    }
}

Actor PuffYou : InvisiblePuff{
    +BloodlessImpact
}

Actor ParticleThunderbolt {
    +NoBLockMAp
    +NoClip
    States{
        Spawn:
            TNT1 A 0 NoDelay A_SetPitch(90)
            TNT1 A 1 A_CustomRailgun(0, 0, "None", "white",  RGF_SILENT | RGF_CENTERZ | RGF_FULLBRIGHT, 0, 50, "PuffYou", 0, 0, 0, 15, 0.6)
            stop
    }
}

Actor ParticleQuick : ParticleFountain {
    states {
        Spawn:
            TNT1 A 3
            stop
    }
}

Actor ParticleSpark_Spawner {
    +NoBLockMAp
    +NoGravity
    +NoDropOff
    radius 30
    height 64
    var int user_Radius;
    var int user_Height;
    States{
        Spawn:
            TNT1 A 0 NoDelay A_SetUserVar("user_Radius", CallACS("Particle_Get", PART_RADIUS))
            TNT1 A 0 A_SetUserVar("user_Height", CallACS("Particle_Get", PART_HEIGHT))
            TNT1 A 50 A_Jump(256, "Spawn.Go")
            goto Spawn.Go
        Spawn.Go:
            TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("ParticleSpark", random(-user_Radius, user_Radius), random(-user_Radius,user_Radius), random(5, user_Height))
        stop
    }
}

Actor ParticleSmoke_Spawner : ParticleSpark_Spawner{
    States{
        Spawn.Go:
            TNT1 AAAA 0 A_SpawnItemEx("ParticleSmoke", random(-user_Radius, user_Radius), random(-user_Radius,user_Radius), random(5, user_Height), 0, 0, 0, random(0, 360))
        stop
    }
}

Actor ParticleThunderBolt_Spawner : ParticleSpark_Spawner{
    States{
        Spawn.Go:
            TNT1 AAAAAA 0 A_SpawnItemEx("ParticleThunderbolt", random(-user_Radius, user_Radius), random(-user_Radius,user_Radius), user_Height)
        stop
    }
}

Actor ParticleSmoke{
    +GrenadeTrail
    +Noclip
    Projectile
    radius 1
    height 1
    Damage 0
    states {
        Spawn:
            TNT1 A 5
            stop
    }
}

Actor ParticleSpark : Spark {
    Args 4
    radius 10
    states {
        Spawn:
            TNT1 A 0 
            TNT1 A 1 
            TNT1 A 1 Thing_Activate(0)
            stop
    }
}

Actor ParticleAura_Picker : ParticleAura {
    var int user_nextcolor;
    states {
        Spawn:
            TNT1 A 1 NoDelay A_Warp(AAPTR_Target, 0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
            TNT1 A 0 A_JumpIf(CallACS("AltState_Particle_Stop"), "Death")
        goto Set
        Set:
            TNT1 A 0 A_SetUserVar("user_nextcolor", user_nextcolor + 1)
            TNT1 A 1 A_Jump(256, "Frame")
            goto Frame
        Frame:
            TNT1 A 1
            goto Reset
        
        Set.Red:
            TNT1 A 0 A_SpawnItem("ParticleQuick_Red")
            goto Spawn 

        Set.Green:
            TNT1 A 0 A_SpawnItem("ParticleQuick_Green")
            goto Spawn 

        Set.Blue:
            TNT1 A 0 A_SpawnItem("ParticleQuick_Blue")
            goto Spawn 
        
        Set.Yellow:
            TNT1 A 0 A_SpawnItem("ParticleQuick_Yellow")
            goto Spawn 
        
        Set.Purple:
            TNT1 A 0 A_SpawnItem("ParticleQuick_Purple")
            goto Spawn 
        
        Set.Black:
            TNT1 A 0 A_SpawnItem("ParticleQuick_Black")
            goto Spawn 
        
        Set.White:
            TNT1 A 0 A_SpawnItem("ParticleQuick_White")
            goto Spawn 
        
        Set.None:
            TNT1 A 0 
            goto Spawn 
        
        Set.Sparks:
            TNT1 A 0 A_SpawnItemEx("ParticleSpark_Spawner", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
            goto Spawn
        
        Set.Smoke:
            TNT1 A 0 A_SpawnItemEx("ParticleSmoke_Spawner", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
            goto Spawn
        
        Set.ThunderBolts:
            TNT1 A 0 A_SpawnItemEx("ParticleThunderBolt_Spawner", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
            goto Spawn

        Reset:
            TNT1 A 0 A_SetUserVar("user_nextcolor", 0)
            goto Spawn
    }
}

Actor ParticleAura_Rainbow : ParticleAura_Picker{
    states {
        Frame:
            TNT1 A 0 A_JumpIf(user_nextcolor == 1, "Set.Red")
            TNT1 A 0 A_JumpIf(user_nextcolor == 2, "Set.Yellow")
            TNT1 A 0 A_JumpIf(user_nextcolor == 3, "Set.Green")
            TNT1 A 0 A_JumpIf(user_nextcolor == 4, "Set.Blue")
            TNT1 A 0 A_JumpIf(user_nextcolor == 5, "Set.Purple")
            TNT1 A 0 A_JumpIf(user_nextcolor == 6, "Set.Black")
            TNT1 A 0 A_JumpIf(user_nextcolor == 7, "Set.White")
            goto Reset
    }
}

Actor ParticleAura_BurnGFX : ParticleAura_Picker{
    states {
        Frame:
            TNT1 A 1 A_JumpIfInventory("AltState_Burning", 1, "TrueFrame", AAPTR_Target)
            goto Death
        TrueFrame:
            TNT1 A 0 A_JumpIf(user_nextcolor == 1, "Set.Black") 
            TNT1 A 0 A_JumpIf(user_nextcolor == 2, "Set.Yellow") 
            TNT1 A 0 A_JumpIf(user_nextcolor == 3, "Set.Smoke") 

            goto Reset
    }
}

Actor ParticleAura_StunGFX : ParticleAura_Picker{
    states {
        Frame:
            TNT1 A 5 A_JumpIfInventory("AltState_Stunned", 1, "TrueFrame", AAPTR_Target)
            goto Death
        TrueFrame:
            TNT1 A 0 A_JumpIf(user_nextcolor == 1, "Set.Sparks") 
            TNT1 A 0 A_JumpIf(user_nextcolor == 2, "Set.ThunderBolts") 
            TNT1 A 10
            goto Reset
    }
}

Actor ParticleAura_Red : ParticleAura { Health 1 }
Actor ParticleAura_Green : ParticleAura { Health 2 }
Actor ParticleAura_Blue : ParticleAura { Health 3 }
Actor ParticleAura_Yellow : ParticleAura { Health 4 }
Actor ParticleAura_Purple : ParticleAura { Health 5 }
Actor ParticleAura_Black : ParticleAura { Health 6 }
Actor ParticleAura_White : ParticleAura { Health 7 }

Actor ParticleQuick_Red : ParticleQuick { Health 1 }
Actor ParticleQuick_Green : ParticleQuick { Health 2 }
Actor ParticleQuick_Blue : ParticleQuick { Health 3 }
Actor ParticleQuick_Yellow : ParticleQuick { Health 4 }
Actor ParticleQuick_Purple : ParticleQuick { Health 5 }
Actor ParticleQuick_Black : ParticleQuick { Health 6 }
Actor ParticleQuick_White : ParticleQuick { Health 7 }